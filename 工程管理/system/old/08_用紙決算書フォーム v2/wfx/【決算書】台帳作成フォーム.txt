*■**********************************************************************
*■***　 用紙決算書　基本ヘッダー
*■**********************************************************************

*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　基本変数定義
var 数値  { &i , &CHK , &outD , &ReturnNum }
var 文字列{ &msgtxt }
var 数値  { &決算書台帳vix  }
var 数値  { &工程tbxNum , &IDtbxNum , &実数値tbxNum }

var 文字列  { &タイトル , &表示本文  }
var 文字列{ &ReturnS　 , &カテゴリNow }
var 数値　{ &ReturnN , &作業codeNow }

/*▼項目情報 */
var 数値  { &項目Num = 20 }
var 文字列  { &CN1[&項目Num] ={""} , &CN2[&項目Num] ={""} , &CN3[&項目Num] ={""} }
var 文字列  { &CN4[&項目Num] ={""} , &CN5[&項目Num] ={""} , &CN6[&項目Num] ={""} }
var 文字列  { &CN7[&項目Num] ={""} , &CN8[&項目Num] ={""} , &CN9[&項目Num] ={""} }
var 文字列  { &CN10[&項目Num] ={""} , &CN11[&項目Num] ={""} , &CN12[&項目Num] ={""} }
var 文字列  { &CN13[&項目Num] ={""} , &CN14[&項目Num] ={""} , &CN15[&項目Num] ={""} }

var 数値  { &決算項目tbxNum }

*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　アドレス変更
var 文字列{ &dataPath = #データパス名 +"..\" } /* ←vixフォルダなので、tbxに指定されている。１個戻す。 */

/*▼tbxPath */
var 文字列{ &tbxPath = &dataPath +"tbx\" }
var 文字列{ &IDtbx = &tbxPath + "ID.tbx" }
var 文字列{ &決算項目tbx = &tbxPath + "決算項目.tbx" }
var 文字列{ &工程tbx = &tbxPath + "工程.tbx" }

/*▼工程管理表 */
var 文字列{ &工程管理tbx = "X:\工程管理\kotei_MASTER.TBX" }
var 文字列{ &工程完了tbx = "X:\工程管理\工程完了\kotei_kanryou.TBX" }


/*▼WfxPath */
var 文字列{ &wfxPath = &dataPath +"wfx\" }
var 文字列{ &カテゴリwfx = &wfxPath + "Modal1.wfx" }
var 文字列{ &作業名wfx = &wfxPath + "Modal2.wfx" }
var 文字列{ &用紙管理記入台帳wfx = &wfxPath + "用紙管理記入台帳作成.wfx" }
var 文字列{ &実数値入力wfx = &wfxPath + "用紙管理記入台帳作成.wfx" }

/*▼cmxPath */
var 文字列{ &cmxPath = &dataPath +"cmx\" }
var 文字列{ &伝票検索cmx = &cmxPath + "VoucherNumberSearch.cmx" }

/*▼rpxPath */
var 文字列{ &rpxPath = &dataPath +"rpx\" }
var 文字列{ &記入台帳rpx = &rpxPath + "用紙決算台帳.rpx" }



*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　項目用変数定義
    var 数値 { &IDzb }
    var 文字列 { &品名zb }
    var 数値 { &定数zb }
    var 日時 { &作成日zb }
    var 日時 { &作業表出力日zb }
    var 日時 { &正式版出力日zb }
    var 数値 { &IDzb }
    var 数値 { &工程Nozb }
    var 文字列 { &工程IDzb }
    var 文字列 { &部署zb }
    var 文字列 { &担当者zb }
    var 数値 { &作業Codezb }
    var 数値 { &表示Nozb }
    var 文字列 { &作業名zb }
    var 文字列 { &内容1zb }
    var 文字列 { &内容2zb }
    var 文字列 { &内容3zb }
    var 文字列 { &内容4zb }
    var 文字列 { &内容5zb }
    var 文字列 { &内容6zb }
    var 文字列 { &内容7zb }
    var 文字列 { &内容8zb }
    var 文字列 { &内容9zb }
    var 文字列 { &内容9zb }
    var 文字列 { &内容10zb }
    var 文字列 { &内容11zb }
    var 文字列 { &内容12zb }
    var 文字列 { &内容13zb }
    var 文字列 { &内容14zb }
    var 文字列 { &内容15zb }

*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　工程入力用変数定義
var 数値  { &最大工程数 = 15 }
var 数値  { &no[&最大工程数] = {} }
var 数値  { &作業code[&最大工程数] = {} }
var 文字列  { &カテゴリ[&最大工程数] = {} }
var 文字列  { &作業名[&最大工程数] = {} }
var 文字列{ &作業項目[&最大工程数] }

var 数値  { &前回ID検索n }
var 数値  { &工程表No }
var 数値  { &データ検索Num = 0 }

*■*----------------------------------------------------------
*■*-----　手続き作成
*■*----------------------------------------------------------
手続き定義開始　フォーム::フォーム開始(長整数　&表番号)

    &決算書台帳vix = #IS表

手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　工程管理 or 工程完了から　伝票番号を検索し、必要情報を抜き出す。
*■*:　伝票番号、　品名、　定数を探す
*■*:
手続き定義開始 伝票番号検索module1（）


    編集表 &決算書台帳vix

    /*▼まずは自分の所に既に情報があるか検索する */
    検索 [伝票] { &IDzb } , 終了状態 = &CHK
    if ( &CHK = 1 )

        &品名zb = [品名]
        &定数zb =  [定数]
        &msgtxt = "ID作成済みなので、グループを表示します。"
        確認 &msgtxt
        グループ検索　{ [伝票] = &IDzb }，終了状態 = &CHK

    else
    *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
    *■*:　検索して無い場合は、工程管理から情報を抜き出し、ID.tbxに行追加する

        手続き実行 工程ID調べる君（ &outD　）

        if (&outD = -1 )

            手続き実行 工程管理完了ID調べる君（ &outD　）
        
            if (　&outD = -1　)

                &msgtxt = "検索された伝票番号は存在しませんでした。処理を中止します。"
                確認 &msgtxt 
                手続き終了

            end
        end

        *■*-------------------------------------
        *■*-- IDtbxに登録する処理
        *■*-------------------------------------
        表 &IDtbx  , 表番号 = 15 , モード = 共有更新
        /*▼行追加するまえに既にIDとして登録されていたら、処理しないようにする。 */

            検索 [ID] { &IDzb } , 終了状態 = &CHK 
            if (&CHK = 1 )
                &msgtxt = "ID.tbxには既にデータがありました。処理を中止します。"
                確認 &msgtxt
            else
            *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
            *■*:　1じゃないという事は検索できなかった。無かったということ。
                &タイトル = "以下の情報を登録しようとしています。"
                &表示本文 = "伝票番号："+ #文字列( &IDzb ) + "\n" +"品名：" + &品名zb +　"\n" +"定数：" +#文字列( &定数zb ) +"\n\n" + "内容に問題無ければ、「OK」を選択してください。"
                メッセージボックス &タイトル , &表示本文 , アイコン = ? , ボタン指定 = 2 , 制御文字展開 = する , &CHK
                if (&CHK = 1 )
                    行追加 終了状態 = &CHK , [ID] = &IDzb　, [品名] = &品名zb , [定数] = &定数zb , [作成日] = &作成日zb
                    &msgtxt = "伝票番号の新規登録ができました。"
                    確認 &msgtxt
                
                else
                    &msgtxt = "キャンセルしました。"
                    確認 &msgtxt
                end
            end
        終了 表 15
    end
手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　工程管理のテーブルから、検索を行う。見つかれば１を返す。
手続き定義開始 工程ID調べる君（参照　数値　&結果　）

        表 &工程管理tbx  , モード = 共有更新
        &i = #IS表

            検索 [伝票ＮＯ] { &IDzb } , 終了状態 = &CHK 


                if ( &CHK = 1 )
                *■*-------------------------------------
                *■*-- 見つかったということで１を返す。

                    &品名zb = [品名]
                    &定数zb = [部数]
                    &作成日zb = #日時値
                    &結果 = 1

                else
                *■*-------------------------------------
                *■*-- 見つかってないので-1

                    &結果 = -1
                end
        
        終了 表 &i
手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　工程完了で同様の処理をする
手続き定義開始 工程管理完了ID調べる君（参照　数値　&結果　）

        表 &工程完了tbx , モード = 共有更新
        &i = #IS表

            検索 [伝票ＮＯ] { &IDzb } , 終了状態 = &CHK 


                if ( &CHK = 1 )
                *■*-------------------------------------
                *■*-- 見つかったということで１を返す。

                    &品名zb = [品名]
                    &定数zb = [部数]
                    &作成日zb = #日時値
                    &結果 = 1

                else
                *■*-------------------------------------
                *■*-- 見つかってないので-1

                    &結果 = -1
                end
        
        終了 表 &i
手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　プリントする前に、各工程の内容を変数に格納する
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
手続き定義開始 工程を変数に格納（数値 &元表番号 ）

    var 数値  { &vb = 1 }

    表 &決算項目tbx  , 表番号 = 26 , モード = 共有更新
    
        編集表 26 
        繰り返し（ .NOT #終端行 ）
            
            &CN1[&vb] = [項目1]　&CN2[&vb] = [項目2]　&CN3[&vb] = [項目3]
            &CN4[&vb] = [項目4] &CN5[&vb] = [項目5] &CN6[&vb] = [項目6]
            &CN7[&vb] = [項目7] &CN8[&vb] = [項目8] &CN9[&vb] = [項目9]
            &CN10[&vb] = [項目10] &CN11[&vb] = [項目11] &CN12[&vb] = [項目12]
            &CN13[&vb] = [項目13] &CN14[&vb] = [項目14] &CN15[&vb] = [項目15]

            &vb =　&vb　+ 1 　
            ジャンプ 行番号 = 次行 
        繰り返し終了 

    終了 表 26
    編集表 &元表番号

手続き定義終了
*■*----------------------------------------------------------
*■*----- プリントアウトしてみる
*■*----------------------------------------------------------
手続き定義開始 プリントアウト（）

    手続き実行 IDエラーチェック(　&IDzb , &ReturnNum )
    条件 ( &ReturnNum = 1 ) 手続き終了


    /*▼登録情報をしぼりこむ */
    絞り込み [伝票] { &IDzb } , 終了状態 = &CHK
    絞り込み [表No] { 1 } , 終了状態 = &CHK
    
        
        *■*-------------------------------------
        *■*--　変数の格納を行う 
        *■*-------------------------------------
        並べ替え  { [工程No] 昇順 }
        手続き実行　工程を変数に格納（ &決算書台帳vix ）
    
        並べ替え  { [部署] 昇順 }

        レポート印刷  &記入台帳rpx  ,\
        カラー印刷 = する ,\
        プレビュー = する ,\
        終了状態 = &CHK

        *■*-------------------------------------
        *■*-- 出力情報を記載する
        *■*-------------------------------------
        メッセージボックス "確認" , "出力日を記録しますか？" , アイコン = ? , ボタン指定 = 5 , 制御文字展開 = しない , &CHK
        if ( &CHK = 6 )
            表  &IDtbx , モード = 共有更新
                &IDtbxNum = #IS表
                置換 終了状態 = &CHK , [作業表出力日] = #日時値
            終了 表 &IDtbxNum
            編集表 &決算書台帳vix
        end
        
    解除 *

手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　IDが空欄か調べる手続き処理
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
手続き定義開始 IDエラーチェック（数値　&ID　, 参照　数値　&ReturnNum ）

    var 数値  { &errorCHKnum = 0 }
    &ReturnNum = 0

    *■*-------------------------------------
    *■*-- 条件定義
    *■*-------------------------------------
    条件 ( &ID = "" ) &errorCHKnum = 1

    *■*-------------------------------------
    *■*-- 発動文言
    *■*-------------------------------------
    if ( &errorCHKnum = 1 )
        &msgtxt = "E-0010 IDが空欄です。"
        確認 &msgtxt
        &ReturnNum = 1
    end

手続き定義終了
