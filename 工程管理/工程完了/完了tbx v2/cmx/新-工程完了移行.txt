
*■***********************************************************
*■*** 完了Boot
*■***********************************************************

/*▼チェック系 */
var 数値  { &CHK , &CHK2 , &CHK3 }


/*▼データパス */
var 文字列{ &完了detaPath = #データパス名 }

/*▼工程管理 */
var 文字列{ &工程tbx = &完了detaPath + "..\..\kotei_MASTER.TBX" }

/*▼旧工程完了 */
var 文字列{ &旧工程完了tbx = &完了detaPath + "..\..\kotei_kanryou.TBX" }

/*▼新工程完了 */
var 文字列{ &新工程完了tbx = &完了detaPath + "..\作業完了Base.tbx" }


/*▼Smasta */
var 文字列{ &Smastatbx = &完了detaPath + "..\..\..\作業伝票\Smasta.tbx" }


/*▼入力操作用変数 */
var 数値  { &FileCount }
var 数値  { &IN }
var 文字列{ &KYE , &SmastaKye , &PASS }
    &KYE="sawayaka"
    &SmastaKye = "kanri"


*■*----------------------------------------------------------
*■*-----　メイン処理
*■*----------------------------------------------------------
名札 MAIN

    /*▼実行メニュー */
    繰り返し
        キー入力　(12,16)-(12,30) , プロンプト = "パスワードは" , モード=半角 , &PASS
        条件　(&PASS <> &KYE )　メッセージボックス　"パスワードが違います","処理を継続しますか" , ボタン指定=2 , &IN
        条件　(&PASS = &KYE )　繰り返し中止
        条件　(&IN = 3)　終了
        条件　(&IN = 2)　終了
    繰り返し終了

    /*▼実行処理 */
　　　画面表示　( 8 , 16 ) , "仕上完了を移動中" , , 緑

    表 /*←一度、全ての表を閉じる */
    
    *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
    *■*:　必要な表をここで定義しておく

    表　&工程tbx , 表番号 = 1 , モード = 専有 /*←工程管理を開く */

        /*▼１．完了定義として、完了チェック、完了担当印字を絞り込む　*/
        絞り込み　[仕上完了１]<>"" , [仕上印１]<>"" 
        /*▼２．今日の0時より前に完了にした項目は該当する。　*/
        絞り込み　[仕上完了１]<　#日時日付( #日時値 )
    
        &FileCount = #総件数

　　　利用者コード　&SmastaKye
　　　表　&Smastatbx , 表番号 = 2 , モード = 共有更新


    *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
    *■*:　ここから作業開始

    編集表　1
    繰り返し（ .NOT #終端行 ）
     
        手続き実行 Smasta更新作業( [伝票ＮＯ] , [確定頁数] , [確定部数] , [確定サイズ] , [納品日] )
    
        ジャンプ 行番号 = 次行 
    繰り返し終了
    終了　表　２ /*←Smastaはお役御免なので閉じる */

    *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
    *■*:　各完了への書き出し処理

    書出し　表 , &旧工程完了tbx , 追加 , , *
    書出し　表 , &新工程完了tbx , 追加 , , *

　　　行削除　＊
　　　絞り込み解除　＊
　　　画面消去

    /*▼後始末 */
　　　画面表示　( 7 , 16 ) - ( 7 , 26 ) , "完了件数の" , , 黄
　　　画面表示　( 7 , 30 ) - ( 7 , 36 ) , &FileCount , , 緑
　　　画面表示　( 7 , 40 ) , "件を処理しました。" , , 緑
　　　メッセージボックス　"確認" , "処理が終了しました" , ボタン指定 = 1
　　　メッセージボックス　"確認" , "表整理を実行しますか" , ボタン指定 = 2 , &IN
    if ( &IN = 1)
        表整理　余白割合 = 0 
    end

　　　表

    メッセージボックス　"確認" , "すべての処理が終了しました" , ボタン指定 = 1

終了
*■*----------------------------------------------------------
*■*-----　手続き定義　Smasta更新
*■*-----　工程の情報をSmastaに反映させます
*■*----------------------------------------------------------
手続き定義開始 Smasta更新作業（ 数値 &伝票No , 文字列 &Page , 数値 &Circulation , 文字列 &Size , 日時　&deliveryDate ）

    編集表　2

    検索 [伝票ＮＯ] { &伝票No } , 終了状態 = &CHK

    /*▼伝票番号がヒットすれば処理する */
    if ( &CHK = 1)
    
        行訂正 終了状態 = &CHK2 , [確定頁数]　= &Page ,　[確定部数] = &Circulation , [確定仕上日] = &deliveryDate
        条件 ( &Size <>"" ) 行訂正 終了状態 = &CHK3 , [サイズ] = &Size

    end

    編集表　1

手続き定義終了