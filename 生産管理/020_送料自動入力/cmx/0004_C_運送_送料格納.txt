*■***********************************************************
*■******　　SAGAWAから来る送料の金額を集計テーブルに反映させる
*■******　　入力ミスがあるかもなので、Data格納出来なかったらエラーログを表示するようにする
*■******
*■******　　＜エラーケース＞
*■******　　　・番号がヒットしない　→　入力ミスの可能性あり。
*■******　　　・番号がヒットするけど、会社が違う　→　入力ミスの可能性あり。
*■******
*■******
*■***********************************************************


*■*-----------------------------------------------------------
*■*----　　　　　   　
*■*-----------------------------------------------------------
名札　MAIN

    *■* ID 1, 佐川
    *■* ID 2, 福山
    *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
    *■*: １だったら佐川さん情報を格納
    if (&ID = 1)
        &NameCode = "佐川"
        &確認tbx = &tbxPath + "佐川_確認.tbx"
        &未確認tbx = &WfxPath + "佐川_未反映確認.tbx"
        &CsvFileName = &InnerSAGAWA
    end
    if (&ID = 2)
        &NameCode = "福山"
        &確認tbx = &tbxPath + "福山_確認.tbx"
        &未確認tbx = &WfxPath + "福山_未反映確認.tbx"
        &CsvFileName = &InnerFUKUYAMA

        

    end

手続き実行 CSV名解体（）
手続き実行　送料格納処理（）

終了
*■*----------------------------------------------------------
*■*-----　　CSV解体
*■*----------------------------------------------------------
手続き定義開始　CSV名解体（）

    &InputCsv = #ファイル名( &CsvFileName , 2 )
    *■*20221231_seikyu(4212217440)
    &YEAR = #部分列 (&InputCsv ,0 ,4 ）
    &MONTH = #部分列 (&InputCsv ,5 ,2 ）
    &DAY = #部分列 (&InputCsv ,7 ,2 ）

    &ShDay = &YEAR + "/" + &MONTH

手続き定義終了
*■*----------------------------------------------------------
*■*-----　　各手続き
*■*----------------------------------------------------------
手続き定義開始　送料格納処理（）

    
    /*▼カウント情報の初期化　*/
    &notUpCount = 0

    /*▼エラー処理　*/
    if (.not &CsvFileName )
        確認　"CSVのアドレスが設定されていません。処理を中止します"
        中止
    end

    表　&送料tbx  , 表番号 = 26 , モード = 共有更新
    絞り込み　[日付] { ≧ #日時値( &ShDay) }, 終了状態 = &CHK

    /*▼csvを確認tableに載せる　*/
    表　&確認tbx , 表番号 = 30 , モード = 専有
        
        行削除 * , 圧縮
        読み込み　CSV , &CsvFileName , 項目名 = しない , 終了状態 = &CHK
        &ロード件数chk = #総件数 /*←読み込んだcsvの総件数を調査 */

        /*▼エラー処理　*/
        if (&CHK　 = 0 )
            確認　"CSV読み込み時にエラーが発生しました。アドレスを再確認してください。処理は中止します。"
            終了　表　30
            終了　表　26
            中止
        end
        if (&CHK　 = -2 )
            確認　"該当のCSVが開いているのでデータが読み込めません。CSVを閉じてやり直してください。"
            終了　表　30
            終了　表　26
            中止
        end

        /*▼反映処理　*/
        印字開始　&確認txt  , 終了状態　= &CHK
    
            繰り返し（ .NOT #終端行 ）
            &numChk = &numChk + 1
        
                &問い合わせNo = [お問合せNO]
                &金額 = [運賃請求金額]

                編集表　26

                    絞り込み [送状ＮＯ] {&問い合わせNo } , 終了状態　= &CHK
                        &件数調 = #総件数

                        /*▼絞り込んだ件数が0の場合はエラー */
                        if (&件数調 = 0 )
                            &ErrorComment = "該当する番号がありませんでした。"
                            印字 &ErrorComment , &↑ , &問い合わせNo , &↑ , &金額　, &↑ , #日時値
                            &notUpCount = &notUpCount + 1

                        else
                            /*▼0でなく、1件のみなら処理 */
                            if (&件数調 = 1)
                                if ([運送会社] = &NameCode )
                                    行訂正 終了状態 = &CHK , [金額] = &金額
                                else
                                    &ErrorComment ="番号は検知しましたが、運送会社が条件と違う。"
                                    印字 &ErrorComment , &↑ , &問い合わせNo , &↑ , &金額　, &↑ , #日時値
                                    &notUpCount = &notUpCount + 1
                                end

                            else
                            /*▼ということは２件以上という事　*/
                                &i = 0
                                &積立 = 0
                                &並列金額 = 0
                                &並列金額切り捨て = 0
                                
                                /*▼加工　*/
                                &並列金額　= &金額　/ &件数調
                                &並列金額切り捨て = #切り捨て( &並列金額 , 0 )

                                繰り返し（ &i < &件数調 ）

                                    /*▼最後の行で帳尻を合わせるため、最終ループを検知する　*/
                                    if (&i + 1  = &件数調 )
                                        &印字金額 = &金額 - &積立
                                    else
                                        &印字金額 = &並列金額切り捨て
                                        &積立　= &積立 + &並列金額切り捨て
                                    end

                                    /*▼ちゃんと佐川になってるか確認。問題無ければ行訂正　*/
                                    if ([運送会社] = &NameCode )
                                        行訂正 終了状態 = &CHK , [金額] = &印字金額
                                    else
                                        &ErrorComment ="複数あるウチの１件が、運送会社が条件と違う。"
                                        印字 &ErrorComment , &↑ , &問い合わせNo , &↑ , &金額　, &↑ , #日時値
                                        &notUpCount = &notUpCount + 1
                                    end
                                    
                                    ジャンプ 行番号 = 次行 
                                    &i = &i + 1
                                繰り返し終了 
                            end
                        end
                    解除　1
                編集表　30
                ジャンプ 行番号 = 次行 
            繰り返し終了 
        印字終了

    終了　表　30
    終了　表　26

    if (&notUpCount = 0 )

        確認　"正常に完了しました"
    else
        var 文字列{ &hoge = "反映できない件数が "+#文字列( &notUpCount )+"件　ありました。　未反映確認tbx　をご確認ください。"　 }

        表　&未確認tbx  , 表番号 = 27 , モード = 専有
        行削除 * , 圧縮
        読み込み　テキスト , &確認txt , 区切り = &↑ 
        確認　&hoge
        編集表　27

    end


手続き定義終了