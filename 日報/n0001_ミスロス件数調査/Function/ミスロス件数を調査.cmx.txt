*■***********************************************************
*■*** ミスロス件数を調査するためForm。
*■***　の、ヘッダーファイルです。
*■***
*■***
*■***********************************************************
/*▼汎用変数 */
var 数値  { &CHK , &i }
var 文字列{ &↑ = "↑" }


var 日時  { &指定月始 , &指定月終 }
&指定月始 = #月数加算( #日時値生成( #年( &調査月 ) , #月( &調査月 ) )　, 0 , 1)
&指定月終 = #月数加算( #日時値生成( #年( &調査月 ) , #月( &調査月 ) , #日( #月末( &調査月 ) ) )　, 0 , 1)


/*▼ファイルアドレス */
var 文字列{ &tbx = "testOut.tbx" }


*■*----------------------------------------------------------
*■*-----　メイン処理
*■*----------------------------------------------------------
名札 MAIN

    /*▼各日報を調査する */
    手続き実行 日報調査（　&日報印刷tbx　）
    手続き実行 日報調査（　&日報管理tbx　）
    手続き実行 日報調査（　&日報仕上tbx　）
    手続き実行 日報調査（　&日報製版tbx　）
    手続き実行 日報調査（　&日報組版tbx　）

    *■*::::::::::::::::::::::::::::::::
    *■*:　テキスト化したらtbxに書き込みます
    手続き実行　ミスロスtbxに記載（）



終了
*■*----------------------------------------------------------
*■*-----　各手続き処理
*■*----------------------------------------------------------
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　日報を調査して、ミス件数を全てテキストに書き出します。
手続き定義開始 日報調査（ 文字列 &日報tbx　）

    var 文字列{ &なまえ }
    &なまえ = #右側文字列( &日報tbx , 8 )
    &なまえ = #部分列( &なまえ , 3 , 2 )    

    表 &日報tbx , 表番号 = 15 , モード = 共有更新　, 終了状態 = &CHK

        if ( &CHK = 1 )

            /*▼日付絞り込み_指定月の月初～月末まで */
            絞り込み [日付] {   &指定月始 =< , &指定月終 =>   } , 終了状態 = &CHK

            /*▼ミス件数の絞り込み_0以外の情報を検出 */
            絞り込み [ミス] { 0< } , 終了状態 = &CHK

            *■*-------------------------------------
            *■*-- テスト処理
            *■*-------------------------------------
            *&tbx =  &なまえ
            *書き出し 表 , &tbx , 終了状態 = &CHK 
            
            *■*-------------------------------------
            *■*-- 抜き出し印字処理
            *■*-------------------------------------
            印字開始 &outTxt , 追加 , 終了状態 = &CHK
                繰り返し（ .NOT #終端行 ）
                 
                    印字 &なまえ ,　&↑　, [日付]　,　&↑　, [伝票ＮＯ] , &↑　, [得意先] , &↑　, [品名] , &↑　, [担当者] , &↑　,\
                        [ミス] , &↑　, [内容] , &↑　, [時間] , &↑　, [備考]

                    ジャンプ 行番号 = 次行 
                繰り返し終了 
            印字終了

        else

            var 文字列{ &msgtxt }
            &msgtxt = #文字列( &CHK ) + "  : 問題がありファイルが開けませんでした。"
            確認 &msgtxt

        end
    
    終了 表 15

手続き定義終了
*■*----------------------------------------------------------
*■*-----　テキストをまとめtbxに起こします。
*■*-----　必要な内容などはテキスト化します。
*■*----------------------------------------------------------
手続き定義開始 ミスロスtbxに記載（）

    var 文字列{ &内容名 }

    表 &ミスロスtbx  , 表番号 = 15 , モード = 専有

        行削除 * , 圧縮

        読み込み テキスト , &outTxt , 区切り = &↑ 
 
        *■*-------------------------------------
        *■*-- 読み込みを終えたら不足情報を追加します
        *■*-------------------------------------
        ジャンプ 行番号 = 1
        繰り返し（ .NOT #終端行 ）
        
            手続き実行 内容名を取得（ [内容] , &内容名 ）
            行訂正 終了状態 = &CHK , [H内容] = &内容名

        ジャンプ 行番号 = 次行 
        繰り返し終了
        
        *■*-------------------------------------
        *■*-- 並べ替え処理
        *■*-------------------------------------
        並べ替え  { [伝票№] 昇順 , [日付] 昇順 }
        
        
        *■*-------------------------------------
        *■*-- レポート出力
        *■*-------------------------------------
        レポート印刷 &ミスロスrpx , 会話 = しない , プレビュー = する , 終了状態 = &CHK


    終了 表 15
    終了 表 16

    /*▼無事読み込みの仕事を終えたらテキストを削除 */
    ファイル削除 &outTxt , 終了状態 = &CHK

手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　内容情報を取得して返す処理
手続き定義開始 内容名を取得（ 数値　&ID , 参照　文字列　&内容名）

    表 &作業CodeTbx , 表番号 = 16 , モード = 共有更新
    編集表 16

        検索 [ID] { &ID } , 終了状態 = &CHK

            if (&CHK = 1)

                &内容名 = [Type]

            else

                &内容名 =　""

            end

        解除 1
    
    編集表 15

手続き定義終了