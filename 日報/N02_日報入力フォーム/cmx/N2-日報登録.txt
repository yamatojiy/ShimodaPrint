
*■***********************************************************
*■***　作成した日報情報を日報tbx（本番）に戻す一括処理です。
*■*** 一連の動作ではあるので、ある程度関数化する。
*■***　テーブルに記載した情報を、必要情報を足して、テキストに書き出す。
*■***　指定した日報テーブルで読み取る。と言った流れ。
*■***********************************************************

/*▼汎用　*/
var 文字列{ &↑ = "↑" }
var 数値  { &chk }
var 文字列{ &msgtxt , &備考改　, &title = "" }
var 数値  { &reSize = 999 }

var 文字列{ &dataPath = #データパス名 } /*←cmxディレクトリ */

/*▼日報tbx  */
var 文字列{ &日報path = &dataPath　+ "..\..\" }
var 文字列{ &印刷日報tbx = &日報path　+ "日報印刷.tbx" }
var 文字列{ &仕上日報tbx = &日報path　+ "日報仕上.tbx" }
var 文字列{ &組版日報tbx = &日報path　+ "日報組版.tbx" }
var 文字列{ &製版日報tbx = &日報path　+ "日報製版.tbx" }
/*▼用紙tbx */
var 文字列{ &用紙tbx = &dataPath　+ "..\..\..\用紙管理\用紙.TBX" }


/*▼テキスト置き場 */
var 文字列{ &iniPath = &dataPath　+ "..\tbx\" }
var 文字列{ &印刷txt = &iniPath　+ "印刷ini.txt" }
var 文字列{ &仕上txt = &iniPath　+ "仕上ini.txt" }
var 文字列{ &組版txt = &iniPath　+ "組版ini.txt" }
var 文字列{ &製版txt = &iniPath　+ "製版ini.txt" }
var 文字列{ &用紙txt = &iniPath　+ "用紙ini.txt" }

/*▼パスコード */
var 文字列{ &passCode = "gyoumu" }
var 文字列{ &youshiCode = "kami" }

/*▼書き出し用コード　*/
var 文字列{ &書き出し印刷日報項目 = "印字 [日付],&↑ ,[係],&↑ , [CD] ,&↑ , [担当者]　,　&↑　,　[伝票num]　,　&↑　, [得意先]　,　&↑　,　[品名]　,　&↑　,[ミス]　,　&↑　,[内容]　,　&↑　,[時間]　,　&↑　, &reSize　,　&↑　, [版数]　,　&↑　,[台数]　,　&↑　,[表]　,　&↑　,[裏]　,　&↑　, [実数]　,　&↑　,[予備数]　,　&↑　,[用紙計]　,　&↑　,[通し枚数]　,　&↑　,[積算枚数]　,　&↑　, &備考改 " }
var 文字列{ &書き出し仕上日報項目 = "印字 [日付],&↑ ,[係],&↑ , [CD] ,&↑ , [担当者]　,　&↑　,　[伝票num]　,　&↑　, [得意先]　,　&↑　,　[品名]　,　&↑　,[ミス]　,　&↑　,[内容]　,　&↑　,[時間]　,　&↑　, [材料]　,　&↑　,　&reSize ,　&↑　,[折りコマ],　&↑　,[枚数],　&↑　, &備考改 " }
var 文字列{ &書き出し組版日報項目 = "印字 [日付],&↑ ,[係],&↑ , [CD] ,&↑ , [担当者]　,　&↑　,　[伝票num]　,　&↑　, [得意先]　,　&↑　,　[品名]　,　&↑　,[内容]　,　&↑　,[時間]　,　&↑　, [材料]　,　&↑　,　&reSize ,　&↑　,[枚数],　&↑　,[ミス]　,　&↑　, &備考改 " }
var 文字列{ &書き出し製版日報項目 = "印字 [日付],&↑ ,[係],&↑ , [CD] ,&↑ , [担当者]　,　&↑　,　[伝票num]　,　&↑　,　[得意先]　,　&↑　,　[品名]　,　&↑　,[内容]　,　&↑　,[時間]　,　&↑　, [材料]　,　&↑　,　&reSize ,　&↑　,[枚数],　&↑　,[ミス]　,　&↑　, &備考改 " }

var 文字列{ &書き出し用紙情報 = "印字 [日付] , &↑ , [CD] ,&↑ , [担当者]　,　&↑　,　[伝票num]　,　&↑　, [得意先]　,　&↑　,　[品名]　,　&↑　,[ミス]　,　&↑　,[紙質]　,　&↑　,&用紙結合名　,　&↑　, [用紙サイズ]　,　&↑　, [目]　,　&↑　,[重さ]　,　&↑　, [実数]　,　&↑　,[予備数],　&↑　, 　, &↑　, , &↑　, , &↑　, ,　&↑　, , &↑　, &備考改 ,　&↑　, [積算枚数] " }
var 数値  { &OPnum = 0  , &openF = 0 }
var 文字列 { &mode = "共有更新" }
/*▼ライブラリ */
var 文字列{ &Lib = &dataPath　＋"..\..\..\system\Lib\Lib_ライブラリ桐処理.cmx" }
ライブラリ &Lib

*■*----------------------------------------------------------
*■*-----　基本処理
*■*----------------------------------------------------------
名札 MAIN

    /*▼最初に作業して問題無いか確認 */
    手続き実行 イレギュラー処理（）

    ケース開始
    ケース（&outPutNum = 1）/*▼印刷*/
        手続き実行　用紙tbx入力（　&書き出し用紙情報 , &用紙txt , &用紙tbx ）
        手続き実行　日報格納（　&書き出し印刷日報項目 , &印刷txt , &印刷日報tbx ）
        
    ケース（&outPutNum = 2）/*▼仕上*/
        手続き実行　日報格納（　&書き出し仕上日報項目 , &仕上txt , &仕上日報tbx ）
        
    ケース（&outPutNum = 3）/*▼組版*/
        手続き実行　日報格納（　&書き出し組版日報項目 , &組版txt , &組版日報tbx ）
        
    ケース（&outPutNum = 4）/*▼製版*/
        手続き実行　日報格納（　&書き出し製版日報項目 , &製版txt , &製版日報tbx ）
        
    ケース終了
    
終了

*■*----------------------------------------------------------
*■*-----　各手続き処理
*■*-----　全てテキストに吐き出す
*■*-----　日報に付け足す
*■*-----　元のデータを消す
*■*----------------------------------------------------------
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　汎用手続きにまとめました。
手続き定義開始 日報格納（　文字列　&書出印刷日報項目 , 文字列　&書出txt , 文字列 &日報tbx ）

    *■*-------------------------------------
    *■*-- 日報内容をテキストにする
    *■*-------------------------------------
    編集表 &作業tbxId

    グループ選択解除　 

    ジャンプ　行番号　= 1
    印字開始 &書出txt , 終了状態 = &chk
        繰り返し（ .NOT #終端行 ）

            /*▼日報番号未入力だった時の特殊処理 */
            if ([N:伝未])
                &備考改 = "【伝票未入力】" + [備考]
            else
                &備考改 = [備考]
            end

            /*▼サイズ情報はこちらに集約する。 */
            &reSize = [N:未サイズ]
            
            コマンド　&書出印刷日報項目
            ジャンプ 行番号 = 次行 

        繰り返し終了
    印字終了

    *■*-------------------------------------
    *■*--  印刷日報に読み込ませる
    *■*-------------------------------------
    利用者コード &passCode
    手続き実行 表オープン（ &日報tbx , &mode , &OPnum , &openF ）
    編集表 &OPnum   
        読み込み テキスト , &書出txt , 区切り = &↑
    条件 ( &openF = 0 ) 終了 表 &OPnum

    *■*-------------------------------------
    *■*-- 入力情報は削除する
    *■*-------------------------------------
    ファイル削除　&書出txt , 終了状態 = &chk

    編集表 &作業tbxId

    行削除 * , 圧縮

    グループ選択

    &msgtxt = "入力情報を日報に反映させました。"
    確認 &msgtxt

手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　用紙情報を格納する
*■*:　印刷予定の場合、使用した用紙の情報を用紙tbxに書き込む必要がある。
*■*:　テキストに書き出して、用紙tbxに読み込ませる
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
手続き定義開始 用紙tbx入力（文字列　&書出用紙項目 , 文字列　&用紙txt , 文字列 &用紙tbx ）

    var 文字列{ &用紙結合名 }
    var 文字列{ &msgtxt }

    *■*::::::::::::::::::::::::::::::::::::
    *■*:　テキスト書き出し処理
    編集表 &作業tbxId

    ジャンプ　行番号　= 1
    印字開始 &用紙txt　, 終了状態 = &chk

        *&msgtxt = #文字列( &chk ) + " :  " + &用紙txt 
        *確認　&msgtxt

        絞り込み [銘柄] { <>"" } , 終了状態 = &chk /*←用紙情報があるものを絞る */ 

        繰り返し（ .NOT #終端行 ）

            *■*:------------------------
            *■*:　用紙名を結合します
            &用紙結合名　= [銘柄]　+ [サブ銘柄]
            
            if ([N:伝未])
                &備考改 = "【伝票未入力】" + [備考]
            else
                &備考改 = [備考]
            end
                
            コマンド　&書出用紙項目
            ジャンプ 行番号 = 次行 
        繰り返し終了 
    印字終了
    *■*-------------------------------------
    *■*--  印刷日報に読み込ませる
    *■*-------------------------------------
    利用者コード &youshiCode

    手続き実行 表オープン（ &用紙tbx , &mode , &OPnum , &openF ）
    編集表 &OPnum
    
        読み込み テキスト , &用紙txt , 区切り = &↑

    条件 ( &openF = 0 ) 終了 表 &OPnum
    *■*-------------------------------------
    *■*-- 入力情報は削除する
    *■*-------------------------------------
    ファイル削除　&用紙txt , 終了状態 = &chk

    編集表 &作業tbxId
    解除 *                         /*←それを解除する */ 

    &msgtxt = "用紙情報を 用紙.tbxに反映させました。"
    確認 &msgtxt

手続き定義終了
*■*----------------------------------------------------------
*■*-----　処理実行前に問題が無いかチェックする。問題あれば処理を中止する。
*■*----------------------------------------------------------
手続き定義開始 イレギュラー処理（）

    var 数値  { &errorCHK = 0 }
    var 文字列{ &タイトル = "エラー" , &表示本文 }
    編集表 &作業tbxId

    /*▼処理する行が１行も無い場合はエラー */
    if ( #総件数 = 0 )
        &errorCHK = 1
        &表示本文 = "日報情報は１行もありません。"
    end

    if ( &errorCHK )    
        メッセージボックス &タイトル , &表示本文 , アイコン = E , ボタン指定 = 1 , 制御文字展開 = する , &CHK
        中止
    end

手続き定義終了
