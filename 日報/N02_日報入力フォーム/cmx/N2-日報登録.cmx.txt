*■***********************************************************
*■***　作成した日報情報を日報tbx（本番）に戻す一括処理です。
*■***********************************************************

/*▼汎用　*/
var 文字列{ &↑ = "↑" }
var 数値  { &chk }
var 文字列{ &msgtxt }


/*▼日報tbx  */
var 文字列{ &日報path = #データパス名　+ "..\..\" }
var 文字列{ &印刷日報tbx = &日報path　+ "日報印刷.tbx" }

/*▼テキスト置き場 */
var 文字列{ &iniPath = #データパス名　+ "..\ini\" }
var 文字列{ &印刷txt = &iniPath　+ "印刷ini.txt" }

/*▼パスコード */
var 文字列{ &passCode = "gyoumu" }

*■*----------------------------------------------------------
*■*-----　基本処理
*■*----------------------------------------------------------
名札 MAIN

    手続き実行　印刷日報格納（）

終了
*■*----------------------------------------------------------
*■*-----　各手続き処理
*■*-----　全てテキストに吐き出す
*■*-----　日報に付け足す
*■*-----　元のデータを消す
*■*----------------------------------------------------------
手続き定義開始 印刷日報格納（）

    /*▼debug用 */
    var 数値  { &作業tbxId }
    &作業tbxId = 1

    *■*-------------------------------------
    *■*-- 日報内容をテキストにする
    *■*-------------------------------------
    編集表 &作業tbxId

    グループ選択解除　 

    ジャンプ　行番号　= 1
    印字開始 &印刷txt , 終了状態 = &chk
        繰り返し（ .NOT #終端行 ）
        
            印字 [日付],&↑ ,[係],&↑ , [CD] ,&↑ , [担当者]　,　&↑　,　[伝票num]　,　&↑　,\
            　[得意先]　,　&↑　,　[品名]　,　&↑　,[ミス]　,　&↑　,[内容]　,　&↑　,[時間]　,　&↑　,\
            [印刷サイズ]　,　&↑　, [版数]　,　&↑　,[台数]　,　&↑　,[表]　,　&↑　,[裏]　,　&↑　,\
            [実数]　,　&↑　,[予備数]　,　&↑　,[用紙計]　,　&↑　,[通し枚数]　,　&↑　,[積算枚数]　,　&↑　,[備考]
            ジャンプ 行番号 = 次行 

        繰り返し終了 
    印字終了

    *■*-------------------------------------
    *■*--  印刷日報に読み込ませる
    *■*-------------------------------------
    利用者コード &passCode
    表 &印刷日報tbx  , 表番号 = 21 , モード = 共有更新
    編集表　21
    
        読み込み テキスト , &印刷txt , 区切り = &↑

    終了 表 21

    *■*-------------------------------------
    *■*-- 入力情報は削除する
    *■*-------------------------------------
    ファイル削除　&印刷txt , 終了状態 = &chk

    編集表 &作業tbxId

    行削除 *

    グループ選択

    &msgtxt = "入力情報を日報に反映させました。"
    確認 &msgtxt

手続き定義終了
