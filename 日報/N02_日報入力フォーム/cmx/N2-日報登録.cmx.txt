*■***********************************************************
*■***　作成した日報情報を日報tbx（本番）に戻す一括処理です。
*■***********************************************************

/*▼汎用　*/
var 文字列{ &↑ = "↑" }
var 数値  { &chk }
var 文字列{ &msgtxt , &備考改　, &title = "" }
var 数値  { &reSize = 999 }

/*▼日報tbx  */
var 文字列{ &日報path = #データパス名　+ "..\..\" }
var 文字列{ &印刷日報tbx = &日報path　+ "日報印刷.tbx" }
var 文字列{ &仕上日報tbx = &日報path　+ "日報仕上.tbx" }
var 文字列{ &組版日報tbx = &日報path　+ "日報組版.tbx" }
var 文字列{ &製版日報tbx = &日報path　+ "日報製版.tbx" }
/*▼用紙tbx */
var 文字列{ &用紙tbx = &日報path　+ "..\用紙管理\用紙.TBX" }


/*▼テキスト置き場 */
var 文字列{ &iniPath = #データパス名　+ "..\ini\" }
var 文字列{ &印刷txt = &iniPath　+ "印刷ini.txt" }
var 文字列{ &仕上txt = &iniPath　+ "仕上ini.txt" }
var 文字列{ &組版txt = &iniPath　+ "組版ini.txt" }
var 文字列{ &製版txt = &iniPath　+ "製版ini.txt" }
var 文字列{ &用紙txt = &iniPath　+ "用紙ini.txt" }

/*▼パスコード */
var 文字列{ &passCode = "gyoumu" }
var 文字列{ &youshiCode = "kami" }

/*▼書き出し用コード　*/
var 文字列{ &書き出し印刷日報項目 = "印字 [日付],&↑ ,[係],&↑ , [CD] ,&↑ , [担当者]　,　&↑　,　[伝票num]　,　&↑　, [得意先]　,　&↑　,　[品名]　,　&↑　,[ミス]　,　&↑　,[内容]　,　&↑　,[時間]　,　&↑　, &reSize　,　&↑　, [版数]　,　&↑　,[台数]　,　&↑　,[表]　,　&↑　,[裏]　,　&↑　, [実数]　,　&↑　,[予備数]　,　&↑　,[用紙計]　,　&↑　,[通し枚数]　,　&↑　,[積算枚数]　,　&↑　, &備考改 " }
var 文字列{ &書き出し仕上日報項目 = "印字 [日付],&↑ ,[係],&↑ , [CD] ,&↑ , [担当者]　,　&↑　,　[伝票num]　,　&↑　, [得意先]　,　&↑　,　[品名]　,　&↑　,[ミス]　,　&↑　,[内容]　,　&↑　,[時間]　,　&↑　, [材料]　,　&↑　,　&reSize ,　&↑　,[折りコマ],　&↑　,[枚数],　&↑　, &備考改 " }
var 文字列{ &書き出し組版日報項目 = "印字 [日付],&↑ ,[係],&↑ , [CD] ,&↑ , [担当者]　,　&↑　,　[伝票num]　,　&↑　, [得意先]　,　&↑　,　[品名]　,　&↑　,[内容]　,　&↑　,[時間]　,　&↑　, [材料]　,　&↑　,　&reSize ,　&↑　,[枚数],　&↑　,[ミス]　,　&↑　, &備考改 " }
var 文字列{ &書き出し製版日報項目 = "印字 [日付],&↑ ,[係],&↑ , [CD] ,&↑ , [担当者]　,　&↑　,　[伝票num]　,　&↑　,　[得意先]　,　&↑　,　[品名]　,　&↑　,[内容]　,　&↑　,[時間]　,　&↑　, [材料]　,　&↑　,　&reSize ,　&↑　,[枚数],　&↑　,[ミス]　,　&↑　, &備考改 " }

var 文字列{ &書き出し用紙情報 = "印字 [日付] , &↑  [CD] ,&↑ , [担当者]　,　&↑　,　[伝票num]　,　&↑　, [得意先]　,　&↑　,　[品名]　,　&↑　,[ミス]　,　&↑　,[紙質]　,　&↑　,&用紙結合名　,　&↑　, [用紙サイズ]　,　&↑　, [目]　,　&↑　,[重さ]　,　&↑　, [実数]　,　&↑　,[予備数],　&↑　,0　,&↑　,0,&↑　,0,&↑　,0,　&↑　,0,&↑　,&備考改,　&↑　,[積算枚数] " }
*■*----------------------------------------------------------
*■*-----　基本処理
*■*----------------------------------------------------------
名札 MAIN

    ケース開始
    ケース（&outPutNum = 1）/*▼印刷*/
        手続き実行　用紙tbx入力（　&書き出し用紙情報 , &用紙txt , &用紙tbx ）
        手続き実行　日報格納（　&書き出し印刷日報項目 , &印刷txt , &印刷日報tbx ）
        

    ケース（&outPutNum = 2）/*▼仕上*/
        手続き実行　日報格納（　&書き出し仕上日報項目 , &仕上txt , &仕上日報tbx ）
        
    
    ケース（&outPutNum = 3）/*▼組版*/
        手続き実行　日報格納（　&書き出し組版日報項目 , &組版txt , &組版日報tbx ）
        

    ケース（&outPutNum = 4）/*▼製版*/
        手続き実行　日報格納（　&書き出し製版日報項目 , &製版txt , &製版日報tbx ）
        

    ケース終了
    
終了

*■*----------------------------------------------------------
*■*-----　各手続き処理
*■*-----　全てテキストに吐き出す
*■*-----　日報に付け足す
*■*-----　元のデータを消す
*■*----------------------------------------------------------
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　汎用手続きにまとめました。
手続き定義開始 日報格納（　文字列　&書出印刷日報項目 , 文字列　&書出txt , 文字列 &日報tbx ）

    *■*-------------------------------------
    *■*-- 日報内容をテキストにする
    *■*-------------------------------------
    編集表 &作業tbxId

    グループ選択解除　 

    ジャンプ　行番号　= 1
    印字開始 &書出txt , 終了状態 = &chk
        繰り返し（ .NOT #終端行 ）

            /*▼日報番号未入力だった時の特殊処理 */
            if ([N:伝未])
                &備考改 = "【伝票未入力】" + [備考]
            else
                &備考改 = [備考]
            end

            /*▼サイズ情報はこちらに集約する。 */
            &reSize = [N:未サイズ]
        
            コマンド　&書出印刷日報項目
            ジャンプ 行番号 = 次行 

        繰り返し終了 
    印字終了

    *■*-------------------------------------
    *■*--  印刷日報に読み込ませる
    *■*-------------------------------------
    利用者コード &passCode
    表 &日報tbx  , 表番号 = 21 , モード = 共有更新
    編集表　21
    
        読み込み テキスト , &書出txt , 区切り = &↑

    終了 表 21

    *■*-------------------------------------
    *■*-- 入力情報は削除する
    *■*-------------------------------------
    ファイル削除　&書出txt , 終了状態 = &chk

    編集表 &作業tbxId

    行削除 *

    グループ選択

    &msgtxt = "入力情報を日報に反映させました。"
    確認 &msgtxt

手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　用紙情報を格納する
手続き定義開始 用紙tbx入力（文字列　&書出用紙項目 , 文字列　&用紙txt , 文字列 &用紙tbx ）

    var 文字列{ &用紙結合名 }

    編集表 &作業tbxId

    ジャンプ　行番号　= 1
    印字開始 &用紙txt　, 終了状態 = &chk

        絞り込み [銘柄] { <>"" } , 終了状態 = &chk /*←用紙情報があるものを絞る */ 

        繰り返し（ .NOT #終端行 ）

            *■*:------------------------
            *■*:　用紙名を結合します
            &用紙結合名　= [銘柄]　+" "+ [サブ銘柄]
            
            if ([N:伝未])
                &備考改 = "【伝票未入力】" + [備考]
            else
                &備考改 = [備考]
            end
        
            コマンド　&書出用紙項目
            ジャンプ 行番号 = 次行 

        繰り返し終了 
    印字終了

    
    *■*-------------------------------------
    *■*--  印刷日報に読み込ませる
    *■*-------------------------------------
    利用者コード &youshiCode
    表 &用紙tbx  , 表番号 = 21 , モード = 共有更新
    編集表　21
    
        読み込み テキスト , &用紙txt , 区切り = &↑

    終了 表 21

    *■*-------------------------------------
    *■*-- 入力情報は削除する
    *■*-------------------------------------
    ファイル削除　&用紙txt , 終了状態 = &chk

    編集表 &作業tbxId
    解除 *                                    /*←それを解除する */ 

    &msgtxt = "用紙情報を 用紙.tbxに反映させました。"
    確認 &msgtxt

手続き定義終了




*■*----------------------------------------------------------
*■*-----　ここから下は不要なコード　今は使ってない。いずれ消す。
*■*----------------------------------------------------------

*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　空欄削除
*■*:　面倒なので各テーブルごとに加工を変えて0を消す処理を入れる
*■*:
*■*:　※このコード自体は不要となりました。　桐で未定義を0変換するところを削除したため。
*■*:
手続き定義開始 ゼロ表示削除（ 文字列 &日報tbx ）

    利用者コード &passCode
    表 &日報tbx  , 表番号 = 21 , モード = 共有更新
    編集表　21

    ケース開始
        *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
        *■*:　印刷日報ならこの処理をする
        ケース（ &日報tbx　= &印刷日報tbx ）

            &title = "印刷用ゼロ絞り込み"
            検索条件登録　条件名 = &title , { [印刷サイズ] = 999  , [台数] = 0 , [実数] = 0 , [予備数] = 0 }

            *■*:摘要条件のゼロを全て空欄に戻す
            絞り込み 条件名　= &title 
                置換 終了状態 = &chk , [印刷サイズ] = "" , [台数] = "" , [表] = "" , [裏] = "" , [実数] = "" , [予備数] = "" , [通し枚数] = "" , [積算枚数] =""
            解除 1

            /*▼空欄だった所を削除する */
            絞り込み [版数] { 0 } , 終了状態 = &chk
                置換 終了状態 = &chk , [版数] = ""
            解除 1

        *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
        *■*:　仕上ならこの処理をする
        ケース（ &日報tbx　= &仕上日報tbx ）

            &title = "仕上用ゼロ絞り込み"
            検索条件登録　条件名 = &title , { [材料] = 0 　, [サイズ] = 999 , [折コマ] = 0 }

            *■*:摘要条件のゼロを全て空欄に戻す
            絞り込み 条件名　= &title 
                置換 終了状態 = &chk , [材料] = "" , [サイズ] = "" , [折コマ] = ""　, [枚数] = ""
            解除 1

            /*▼空欄だった所を削除する */
            絞り込み [サイズ] { 999 } , 終了状態 = &chk
                置換 終了状態 = &chk , [サイズ] = ""
            解除 1
            
            /*▼材料と折コマは単独で0にならないので削除しておく空欄だった所を削除する */
            絞り込み [材料] { 0 } , 終了状態 = &chk
                置換 終了状態 = &chk , [材料] = ""
            解除 1
            絞り込み [折コマ] { 0 } , 終了状態 = &chk
                置換 終了状態 = &chk , [折コマ] = ""
            解除 1

        *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
        *■*:　仕上ならこの処理をする
        ケース（ &日報tbx　= &製版日報tbx ）

            &title = "製版用ゼロ絞り込み"
            検索条件登録　条件名 = &title , { [材料] = 0 　, [サイズ] = 999 , [枚数] = 0 }

            *■*:摘要条件のゼロを全て空欄に戻す
            絞り込み 条件名　= &title 
                置換 終了状態 = &chk , [材料] = "" , [サイズ] = "" , [枚数] = ""
            解除 1
        
            /*▼空欄だった所を削除する */
            絞り込み [サイズ] { 999 } , 終了状態 = &chk
                置換 終了状態 = &chk , [サイズ] = ""
            解除 1

        *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
        *■*:　仕上ならこの処理をする
        ケース（ &日報tbx　= &組版日報tbx ）

            /*▼空欄だった所を削除する */
            絞り込み [サイズ] { 999 } , 終了状態 = &chk
                置換 終了状態 = &chk , [サイズ] = ""
            解除 1
            /*▼空欄だった所を削除する */
            絞り込み [材料] { 0 } , 終了状態 = &chk
                置換 終了状態 = &chk , [材料] = ""
            解除 1
            /*▼空欄だった所を削除する */
            絞り込み [枚数] { 0 } , 終了状態 = &chk
                置換 終了状態 = &chk , [枚数] = ""
            解除 1

    ケース終了
    終了 表 21

手続き定義終了