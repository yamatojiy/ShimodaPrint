*■***********************************************************
*■***
*■*** デイリーでプリントアウトしておく予定表を朝６時に対応するプログラムです。
*■***　デイリーでメインマップを作成するプログラムです。
*■***
*■***********************************************************
var 文字列{ &msgtxt }
var 数値  { &CHK , &表No　, &i }


*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　/*▼プリントアウト用の専用変数 */
var 日時  { &作業日 = #日時値 , &前作業日 }
var 数値  { &総件数x , &総枚葉x , &総冊子x , &総複写x , &総新聞x　 }
var 数値  { &新着件数x , &昨校了件数x , &完了数x , &外注発生件数x }

var 数値  { &M総数x , &M校了済x ,  &M枚葉x , &M冊子x , &M新聞x , &M複写x }
var 数値  { &M1総数x , &M1校了済x , &M1枚葉x , &M1冊子x , &M1新聞x , &M1複写x }
var 数値  { &M2総数x , &M2校了済x , &M2枚葉x , &M2冊子x , &M2新聞x , &M2複写x }

/*▼進捗 */
var 数値  { &未校了x , &製版x , &印刷x , &仕上x  }

/*▼印刷予定定義用 */
    var 文字列{ &専有 =　"専有" }
    var 文字列{ &共有更新 = "共有更新" }

*■*:　PDFを作成します
var 文字列{ &PDFAddress = #一括パス名 }
var 文字列{ &PDFDataAddress = &PDFAddress +"PDF\MainSchedule\"  }
var 文字列{ &PDFNameMain = &PDFDataAddress + #日時文字列 (　&作業日 , 11 , 1 , 2 ) +"_MainSchedule.pdf" }


/*▼ライブラリ情報 */
var 文字列{ &Lib前日日抜き出し = &PDFAddress + "..\..\..\System\Lib\Lib_前回営業日検出.cmx"}
var 文字列{ &holidayListTbx = &PDFAddress + "..\..\..\System\tbx\01_01_HolidayList.tbx" }
var 文字列{ &mainScheduleTbx = &PDFAddress + "..\..\..\工程管理\K-社内全体共有用資料\tbx\MainSchedule.tbx" }
var 数値  { &mainScheduleNum }

var 文字列{ &OutscTbx = &PDFAddress + "..\..\..\工程管理\K-社内全体共有用資料\tbx\OutputSchedule.tbx" }
var 数値  { &OutscNum }

/*▼レポートデータ */

var 文字列{ &MainScheduleRpx = &PDFAddress +"..\..\..\工程管理\K-社内全体共有用資料\tbx\MainSchedule.rpx" }


var 文字列{ &Ad工程管理tbx = &PDFAddress + "..\..\..\工程管理\kotei_MASTER.TBX" }
var 数値  { &Ad工程管理Num }
var 文字列{ &Ad工程完了tbx = &PDFAddress + "..\..\..\工程管理\工程完了\完了tbx v2\作業完了Base.tbx" }
var 数値  { &Ad工程完了Num }

/*▼画像データ */
var 文字列{ &shimodaLogoPng = &PDFAddress + "..\..\..\system\logo\SHIMODA\Logo1(背景白).png" }


/*▼ファイルパス */
ライブラリ　&Lib前日日抜き出し

*■*----------------------------------------------------------
*■*-----　基本処理
*■*----------------------------------------------------------
名札 MAIN

    *■*確認　"kita"
    *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
    *■*:　事前準備
    手続き実行 Lib前回営業日検出( &作業日 ,　&前作業日 , &holidayListTbx )

    手続き実行 テーブルID調査( &Ad工程管理tbx , &専有 , &Ad工程管理Num )
    手続き実行 テーブルID調査( &Ad工程完了tbx , &専有 , &Ad工程完了Num )
    手続き実行 テーブルID調査( &mainScheduleTbx , &専有 , &mainScheduleNum )


    手続き実行 工程管理必要情報抜き出し ( &Ad工程管理Num )
    手続き実行 工程完了情報抜き出し ( &Ad工程完了Num )


    *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
    *■*:　プリント表へ書き出し
    編集表　&mainScheduleNum

        検索 [前日作業日] { &前作業日 } , 終了状態 = &CHK
        
            if ( &CHK = 1) /*←検索にひっかかれば行訂正を行う */

                行訂正 終了状態 = &CHK , ,[作業日]  = &作業日 ,[前日作業日]  = &前作業日 ,[総件数]  = &総件数x ,[総-枚葉]  = &総枚葉x ,[総-冊子]  = &総冊子x ,\
                [総-複写]  = &総複写x ,[総-新聞]  = &総新聞x ,[新着件数]  = &新着件数x ,[昨校了件数]  = &昨校了件数x ,\
                [昨了件数] = &完了数x ,[外注発生件数]  = &外注発生件数x ,\
                [M総量]  = &M総数x ,[M校了済]  = &M校了済x ,[M枚葉]  = &M枚葉x  ,[M冊子]  = &M冊子x ,[M新聞]  = &M新聞x ,[M複写]  = &M複写x ,[M+1総量]  = &M1総数x ,\
                [M+1校了済]  = &M1校了済x ,[M+1枚葉]  = &M1枚葉x ,[M+1冊子]  = &M1冊子x ,[M+1新聞]  = &M1新聞x ,[M+1複写]  = &M1複写x ,[M+2総量]  = &M2総数x ,[M+2校了済]  = &M2校了済x ,\
                [M+2枚葉]  = &M2枚葉x ,[M+2冊子]  = &M2冊子x ,[M+2新聞]  = &M2新聞x ,[M+2複写]  = &M2複写x ,[進捗未校了]  = &未校了x ,[進捗製版]  = &製版x ,[進捗印刷]  = &印刷x ,\
                [進捗仕上]  = &仕上x ,[グラフ名]  = "進捗確認表"

            else

                行追加 終了状態 = &CHK , ,[作業日]  = &作業日 ,[前日作業日]  = &前作業日 ,[総件数]  = &総件数x ,[総-枚葉]  = &総枚葉x ,[総-冊子]  = &総冊子x ,\
                [総-複写]  = &総複写x ,[総-新聞]  = &総新聞x ,[新着件数]  = &新着件数x ,[昨校了件数]  = &昨校了件数x ,\
                [昨了件数] = &完了数x ,[外注発生件数]  = &外注発生件数x ,\
                [M総量]  = &M総数x ,[M校了済]  = &M校了済x ,[M枚葉]  = &M枚葉x  ,[M冊子]  = &M冊子x ,[M新聞]  = &M新聞x ,[M複写]  = &M複写x ,[M+1総量]  = &M1総数x ,\
                [M+1校了済]  = &M1校了済x ,[M+1枚葉]  = &M1枚葉x ,[M+1冊子]  = &M1冊子x ,[M+1新聞]  = &M1新聞x ,[M+1複写]  = &M1複写x ,[M+2総量]  = &M2総数x ,[M+2校了済]  = &M2校了済x ,\
                [M+2枚葉]  = &M2枚葉x ,[M+2冊子]  = &M2冊子x ,[M+2新聞]  = &M2新聞x ,[M+2複写]  = &M2複写x ,[進捗未校了]  = &未校了x ,[進捗製版]  = &製版x ,[進捗印刷]  = &印刷x ,\
                [進捗仕上]  = &仕上x ,[グラフ名]  = "進捗確認表"

            end

            *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
            *■*:　絞り込み
            ジャンプ 行番号 = 最終
            絞り込み 行数 = 1

            *■*確認 &PDFAddress　
            *■*確認 &MainScheduleRpx
            *■*確認　&PDFNameMain

            レポート印刷　&MainScheduleRpx , 文書ファイル = &PDFNameMain , カラー印刷 = する　, 終了状態 = &CHK

            *■*確認 #文字列( &CHK )
            *■*-------------------------------------
            *■*--  そのままだと会話エラーが出て解消方法がわからない。一旦別ファイルに退避させて、そのファイルを開く様にする
            *■*-------------------------------------
            *■*書き出し 表 , &OutscTbx , 終了状態 = &CHK
            
            *■*ジャンプ 行番号 = 最終
            *■*絞り込み 行数 = 1
            
            *■*手続き実行 テーブルID調査( &OutscTbx , &専有 , &OutscNum )
            *■*編集表　&OutscNum

            *■*レポート印刷　&MainScheduleRpx , 文書ファイル = &PDFNameMain , 会話 = しない , カラー印刷 = する　, 終了状態 = &CHK

終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　テーブルID調査
*■*:　ターゲットのテーブルが開いてても開いて無くても表番号を返す関数
*■*:　引数１：ターゲットのテーブルアドレス
*■*:　引数２：新規で開くステータス（専有とか参照とか、すでに開いている場合はそのまま）
*■*:　戻り値：表のＩＤ
    手続き定義開始 テーブルID調査（ 文字列 &tbx ,文字列　&モード　, 参照　数値　&表ID ）

        *■*-------------------------------------
        *■*--  編集表番号取得（開いてても問題無し
        *■*-------------------------------------
        &表ID = #表番号取得( &tbx )
        if ( .not &表ID ) /*←未定義なら */
            
            表 &tbx , モード = &モード , 終了状態 = &CHK 
            &表ID = #表番号取得( &tbx )　　

        end

    手続き定義終了   
*■***********************************************************
*■***　　　　　　手続き処理
*■***
*■***　工程管理の情報を抜き出して、変数に格納する処理です。
*■***
*■***
*■***
*■***
*■***
*■***
手続き定義開始 工程管理必要情報抜き出し（　数値　&表ID　）

    編集表　&表ID

        *■*-------------------------------------
        *■*-- ここから調査開始
        *■*-------------------------------------
        &総件数x = #総件数

        絞り込み [分類] { "枚葉" } , 終了状態 = &CHK
            &総枚葉x = #総件数
        解除 1
        絞り込み [分類] { "冊子" } , 終了状態 = &CHK
            &総冊子x = #総件数
        解除 1
        絞り込み [分類] { "新聞" } , 終了状態 = &CHK
            &総新聞x = #総件数
        解除 1
        絞り込み [分類] { "複写" } , 終了状態 = &CHK
            &総複写x = #総件数
        解除 1
        
        *■*:::::::::::::::::::::::::::::::::::::::::::::
        *■*:　前日差分
        絞り込み [挿入日] { #日([挿入日]) = #日(#日時日付( &前作業日 )) , #月([挿入日]) = #月(#日時日付( &前作業日 )) , #年([挿入日]) = #年(#日時日付( &前作業日 )) } , 終了状態 = &CHK
            &新着件数x = #総件数
        解除 1

        絞り込み [確認完了] { #日([確認完了]) = #日(#日時日付( &前作業日 )) , #月([確認完了]) = #月(#日時日付( &前作業日 )) , #年([確認完了]) = #年(#日時日付( &前作業日 )) } , 終了状態 = &CHK
            &昨校了件数x = #総件数
        解除 1

        *■*::::::::::::::::::::::::::::::::::::::::::::::
        *■*:　外注件数
        絞り込み [備考] { *"外注"* } , 終了状態 = &CHK
            &外注発生件数x = #総件数    
        解除 1

        *■*::::::::::::::::::::::::::::::::::::::::::::::
        *■*:　進捗
        絞り込み [確認完了] { <>"" } , 終了状態 = &CHK
        
            &未校了x = &総件数x - #総件数

            絞り込み [製版印１] { <>"" } , 終了状態 = &CHK
            
                &製版x = ( &総件数x - &未校了x ) - #総件数
            
                絞り込み [印刷印１] { <>"" } , 終了状態 = &CHK

                    &印刷x = ( &総件数x - &未校了x - &製版x ) - #総件数
                    &仕上x = ( &総件数x - &未校了x - &製版x - &印刷x )

                解除 1
            解除 1
        解除 1

        *■*::::::::::::::::::::::::::::::::::::::::::::::
        *■*:　月集計
        *■*: 変数作成初期は校了済みを集計していましたが、未校了の方が情報価値が高くなったので、変数に入っているのは未校了になります。注意。

        絞り込み [仕上日] { #月([仕上日]) = #月(#日時日付( #日時値 )) , #年([仕上日]) = #年(#日時日付( #日時値 )) } , 終了状態 = &CHK
            &M総数x = #総件数

            絞り込み [確認完了] { #未定義 } , 終了状態 = &CHK
                &M校了済x = #総件数
            
                絞り込み [分類] { "枚葉" } , 終了状態 = &CHK
                    &M枚葉x = #総件数
                解除 1
                絞り込み [分類] { "冊子" } , 終了状態 = &CHK
                    &M冊子x = #総件数
                解除 1
                絞り込み [分類] { "新聞" } , 終了状態 = &CHK
                    &M新聞x = #総件数
                解除 1
                絞り込み [分類] { "複写" } , 終了状態 = &CHK
                    &M複写x = #総件数
                解除 1
            解除 1
        解除 1
        
        絞り込み [仕上日] { #月([仕上日]) = #月(#日時日付( #日時値 )) + 1 , #年([仕上日]) = #年(#日時日付( #日時値 )) } , 終了状態 = &CHK
            &M1総数x = #総件数

            絞り込み [確認完了] { #未定義 } , 終了状態 = &CHK
                &M1校了済x = #総件数

                絞り込み [分類] { "枚葉" } , 終了状態 = &CHK
                    &M1枚葉x = #総件数
                解除 1
                絞り込み [分類] { "冊子" } , 終了状態 = &CHK
                    &M1冊子x = #総件数
                解除 1
                絞り込み [分類] { "新聞" } , 終了状態 = &CHK
                    &M1新聞x = #総件数
                解除 1
                絞り込み [分類] { "複写" } , 終了状態 = &CHK
                    &M1複写x = #総件数
                解除 1
            解除 1
        解除 1
        
        絞り込み [仕上日] { #月([仕上日]) = #月(#日時日付( #日時値 )) + 2 , #年([仕上日]) = #年(#日時日付( #日時値 )) } , 終了状態 = &CHK
            &M2総数x = #総件数

            絞り込み [確認完了] { #未定義 } , 終了状態 = &CHK
                &M2校了済x = #総件数

                絞り込み [分類] { "枚葉" } , 終了状態 = &CHK
                    &M2枚葉x = #総件数
                解除 1
                絞り込み [分類] { "冊子" } , 終了状態 = &CHK
                    &M2冊子x = #総件数
                解除 1
                絞り込み [分類] { "新聞" } , 終了状態 = &CHK
                    &M2新聞x = #総件数
                解除 1
                絞り込み [分類] { "複写" } , 終了状態 = &CHK
                    &M2複写x = #総件数
                解除 1
            解除 1
        解除 1

    終了 表 &表ID

手続き定義終了
*■***********************************************************
*■***　　　　　　手続き処理
*■***
*■***　工程完了の情報を抜き出す処理
*■***
*■***
*■***
*■***
手続き定義開始 工程完了情報抜き出し（　数値　&表ID　）

    編集表　&表ID

        *■*:::::::::::::::::::::::::::::::::::::::::::::
        *■*:　前日差分 工程管理の値に加算する。
        絞り込み [挿入日] { #日([挿入日]) = #日(#日時日付( &前作業日 )) , #月([挿入日]) = #月(#日時日付( &前作業日 )) , #年([挿入日]) = #年(#日時日付( &前作業日 )) } , 終了状態 = &CHK
            &新着件数x = &新着件数x　+ #総件数
        解除 1

        絞り込み [仕上完了１] { #日([仕上完了１]) = #日(#日時日付( &前作業日 )) , #月([仕上完了１]) = #月(#日時日付( &前作業日 )) , #年([仕上完了１]) = #年(#日時日付( &前作業日 )) } , 終了状態 = &CHK
           &完了数x = #総件数
        解除 1


    終了 表 &表ID

手続き定義終了