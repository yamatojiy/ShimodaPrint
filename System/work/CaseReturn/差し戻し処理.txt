*■***********************************************************
*■*** 差し戻し実行処理
*■***********************************************************

    var 数値  { &CHK , &CHK1 ,&CHK2 , &i }
    var 文字列{ &datapath = #データパス名 }
    var 文字列{ &msgtxt }
    *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
    *■*:　表整理タスクtbx
    var 数値  { &TaskNum = 3 }

    var 文字列{ &tbxFileName[&TaskNum] ={""} }
        &tbxFileName[1] = "kotei_MASTER.TBX"
        &tbxFileName[2] = "kotei_kanryou.TBX"
        &tbxFileName[3] = "作業完了Base.tbx"

    var 文字列{ &tbxAddress[&TaskNum] = {} }
        &tbxAddress[1] = #データパス名 + "..\..\..\工程管理\"                           /*←工程管理 */
        &tbxAddress[2] = #データパス名 + "..\..\..\工程管理\工程完了\"                  /*←工程完了 */
        &tbxAddress[3] = #データパス名 + "..\..\..\工程管理\工程完了\完了tbx v2\"       /*←工程完了v2 */

    var 文字列{ &tbxPass[&TaskNum] = {} }
        &tbxPass[1] = "okuda06"
        &tbxPass[2] = "okuda06"
        &tbxPass[3] = "okuda06"

    var 数値  { &tbxOpenChk[&TaskNum] = {0} }

    var 文字列{ &tbxFilePath[&TaskNum] = {""} }
        &tbxFilePath[1] = &tbxAddress[1] + &tbxFileName[1]
        &tbxFilePath[2] = &tbxAddress[2] + &tbxFileName[2]
        &tbxFilePath[3] = &tbxAddress[3] + &tbxFileName[3]

    var 文字列{ &JobTbx = &datapath + "JobTbx.tbx" }
    var 文字列{ &FilersChkPath = "" }


*■***********************************************************
*■*** ここから実行処理
*■***********************************************************
名札 MAIN

    var 数値  { &r = 1　, &max = &TaskNum  + 1 }

    *■*----------------------------------------------------------
    *■*-----　専有作業できるか確認
    *■*----------------------------------------------------------
    var 文字列{ &タイトル , &表示本文 }
    &タイトル = "注意！"
    &表示本文 = "実行する前に、\n ・工程管理　\n ・工程完了 \n ・新工程完了 \nいずれも誰も開いていない状態かご確認ください。"

    メッセージボックス &タイトル , &表示本文 , アイコン = ! , ボタン指定 = 2 , 制御文字展開 = する , &CHK
    if (&CHK = 2)
        中止
    end





    *■*----------------------------------------------------------
    *■*-----　ファイルオープンチェック
    *■*----------------------------------------------------------
    繰り返し（ &r < &max　 ）

        
        &FilersChkPath = &tbxAddress[&r] + &tbxFileName[&r]
        手続き実行 RSCチェック　( &FilersChkPath , &tbxOpenChk[&r] )
        
        *■*-------------------------------------
        *■*-- 　
        if ( &tbxOpenChk[&r] <> 0 )
            
            &msgtxt =　&tbxFileName[&r] + "　を誰か開いているため作業できません。処理を中止します。 "
            確認　&msgtxt
            中止

        end
        &r = &r + 1

    繰り返し終了
    *■*----------------------------------------------------------
    *■*----- ここからが本番じゃ
    *■*----------------------------------------------------------
    表 &tbxFilePath[3] , 表番号 = 21 , モード = 専有
    編集表 21

    絞り込み [伝票ＮＯ] { &伝No } , 終了状態 = &CHK
    
        var 数値  { &Oncount = 0 }
        &Oncount = #総件数

        /*▼0だと該当しなかったという事 */
        if ( &Oncount = 0 )

            &msgtxt = "工程完了に伝票番号がありません。処理を中止します。"
            確認 &msgtxt
            終了 表 21
            中止
        else

            書き出し 表 , &JobTbx , 終了状態 = &CHK
            
            解除

                検索 [伝票ＮＯ] { &伝No } , 終了状態 = &CHK1        
                行削除 終了状態 = &CHK2
             
            

            /*▼次の表　（旧工程完了） */
            表 &tbxFilePath[2] , 表番号 = 22 , モード = 専有
            編集表 22

                検索 [伝票ＮＯ] { &伝No } , 終了状態 = &CHK1
                行削除 終了状態 = &CHK2
            
            /*▼工程管理に追加する */
            表 &tbxFilePath[1]  , 表番号 = 23 , モード = 専有
            編集表 23

            ジャンプ 行番号 = 終端

            読み込み 表 , &JobTbx , 編集表 = しない , 終了状態 = &CHK ,　*

        終了　表

            &msgtxt = "戻し作業が完了しました"
            確認　&msgtxt

        end

終了
*■***********************************************************
*■***　毎回使う手続き
*■***********************************************************
*■***********************************************************
*■***　RSCチェック（表をだれかが開いていれば１、いなければ０を返します。
*■***********************************************************
手続き定義開始 RSCチェック（　文字列 &filepath ,　参照　数値 &openCHK ）

    var 文字列{ &fileRsc = "" , &chkAns  }
    &fileRsc    = &filepath + ".rsc"
    &chkAns     = #ファイル検索( &fileRsc , 1 )

    if ( &chkAns = #未定義 )
        &openCHK =  0
    else
        &openCHK =  1
    end

手続き定義終了