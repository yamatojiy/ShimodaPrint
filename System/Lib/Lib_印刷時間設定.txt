*■***********************************************************
*■***
*■***　印刷時間がどれぐらいかかるか調査する手続きです
*■***
*■***
*■***
var 数値  { &CHK }
var 文字列{ &重要txt = "【色調整重要】" }

名札 MAIN

    var 文字列{ &印刷機T = "菊全機"　, &区分T = "冊子" , &色T = "4+4" , &備考T = "aaaaa" + &重要txt }
    var 数値  { &部数T = 400 , &台数T = 1.5 ,　&面付数　= 16  }
    var 時間　{ &timeT }
    var 文字列{ &msgtxt }
    var 文字列{ &印刷スペックtbx = #一括パス名 + "..\tbx\07_04_印刷機スペック.tbx" }
    var 文字列{ &印刷色設定tbx = #一括パス名 + "..\tbx\07_03_印刷用色設定.tbx" }
    
    繰り返し（ .NOT #終端行 ）

        手続き実行 Lib印刷作業時間概算　(　[印刷機] , [確定部数]　, [台数] , [面付数]　, [分類] , [色] , [備考]　, &timeT , &印刷スペックtbx , &印刷色設定tbx ) 
        行訂正 終了状態 = &CHK , [作業時間] = &timeT

        ジャンプ 行番号 = 次行
    繰り返し終了 

    確認 "Done"

終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　手続き処理
*■*:　
*■*: その項目の必要情報を取得し、ある程度テーブルから算出したあと、印刷にかかる概算数値を返す関数を作る
*■*:
*■*:　引数１：文字列：印刷機
*■*:　引数２：数値　　：部数
*■*:　引数３：数値　　：台数
*■*:　引数４：数値　　：面付
*■*:　引数５：文字列：区分
*■*:　引数６：文字列：色
*■*:　引数７：文字列：備考
*■*:
*■*:　戻り値：数値：作業概算時間
*■*:
*■*: 外部パス：&印刷スペックtbx
*■*: 外部パス：&印刷色設定tbx
*■*:
手続き定義開始 Lib印刷作業時間概算（ 文字列 &印刷機 , 数値　&部数 , 数値　&台数 , 数値　&面付 , 文字列 &区分　, 文字列 &色　, 文字列 &備考　, 参照　時間　&Time , 文字列　&印刷スペックtbx , 文字列 &印刷色設定tbx ）

    var 数値  { &通し枚数 , &作業時間 , &版替え時間 , &色調整時間 , &補助業務時間 , &S級調整時間　, &合計作業時間 }
    var 数値  { &区分補正値 , &通し回数値 }
    var 数値  { &回転数 , &版替えmin , &色調整min , &S級min , &S級boot , &補正台数　}
    var 文字列{ &TimeTXT }
    *■*-------------------------------------
    *■*-- 補正値調査
    *■*-------------------------------------
    手続き実行 区分補正値計算　( &面付 , &区分 , &区分補正値 )
    手続き実行 作業係数調査 ( &色　, &印刷機 , &通し回数値 , &印刷色設定tbx )
    手続き実行 印刷スペック情報格納 (&印刷機 , &回転数 , &版替えmin , &色調整min , &S級min　, &印刷スペックtbx )
    手続き実行　備考S級判定 ( &備考 , &S級boot )
    /*▼台数が1以下だった場合は、補正を掛ける */
    if ( 1 > &台数 )
        &補正台数 = 1
    else
        &補正台数 = &台数
    end
    *■*-------------------------------------
    *■*-- 通し枚数　設定
    *■*-------------------------------------
    &通し枚数 = ( &部数 * &台数 ) *  &通し回数値　/ &区分補正値
    &作業時間 = ( &通し枚数 / &回転数 ) * 60
    &版替え時間 = ( &版替えmin * &補正台数 ) * &通し回数値
    &補助業務時間 = ( &色調整min * &補正台数 ) * &通し回数値
    &S級調整時間　= &S級min * &S級boot
    &合計作業時間 = &作業時間 + &版替え時間 + &補助業務時間 + &S級調整時間
    &TimeTXT = #時間文字列( &合計作業時間 , 2 )
    &Time = #時間値( &TimeTXT )

手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　
*■*:　S級判定を行う関数。　特定文字があれば１無ければ０を返す関数
*■*:　
*■*:　引数１：備考
*■*:　
*■*:　戻り値：数値：判定
手続き定義開始 備考S級判定（ 文字列 &備考 , 参照　数値　&判定　）

    &判定　= #文字位置( &備考 , "【色調整重要】" )

    if ( &判定 > 0 )
        &判定 = 1
    else
        &判定 = 0
    end

手続き定義終了

*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　
*■*:　印刷機のスペック情報テーブルから、回転数と版替えにかかる時間などを変数に格納する
*■*:　
*■*:　引数１：印刷機情報
*■*:　
*■*:　戻り値：数値：回転数
*■*:　戻り値：数値：版替え時間
*■*:　戻り値：数値：色変え時間
*■*:　
手続き定義開始 印刷スペック情報格納（ 文字列 &印刷機 , 参照　数値 &回転数 , 参照　数値 &版替え時 , 参照　数値 &色調整時 ,  参照　数値 &S級時 , 文字列　&印刷スペックtbx ）

    var 文字列{ &tbx }
    &tbx = &印刷スペックtbx

    表 &tbx , 表番号 = 13 , モード = 共有更新
    
        検索 [機械名] { &印刷機 } , 終了状態 = &CHK
        
        if ( &CHK = 1 )

            &回転数     =　[回転数]
            &版替え時   =　[版替え]
            &色調整時   =　[色調整]
            &S級時      = [S級]
            
        else

            *確認 "手続　印刷スペック情報格納　にて　印刷機参照ができませんでした。"
        end

    終了 表 13

手続き定義終了

*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　
*■*:　印刷機と色情報から、作業係数を返す関数
*■*:　
*■*:　引数１：色（4+4）とかの情報
*■*:　引数２：印刷機：菊全機とか
*■*:　
*■*:　戻り値：数値：作業係数
*■*:　
手続き定義開始 作業係数調査（ 文字列 &色　, 文字列 &印刷機 , 参照　数値　&作業係数 , 文字列　&印刷色設定tbx　）

    var 文字列{ &印刷用色設定tbx }
    &印刷用色設定tbx = &印刷色設定tbx

    表 &印刷用色設定tbx , 表番号 = 13 , モード = 共有更新

        絞り込み [印刷機] { &印刷機 } , 終了状態 = &CHK

        検索 [ReturnS] { &色 } , 終了状態 = &CHK 
        
        if ( &CHK = 1 )
            &作業係数 = [ReturnN]
        else
            &作業係数 = 3
        end
     
    終了 表 13

手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　
*■*:　区分によっては面付数で通し枚数を減らす場合があるので、その判定をする関数
*■*:
*■*: 枚葉と新聞は面付に関係があるが、冊子の場合はページ面付になるので、台数の方で係数をかけるようにする。
*■*:　
*■*:　引数１：面付数
*■*:　引数２：区分
*■*:　
*■*:　戻り値：数値：区分補正値
*■*:　
手続き定義開始 区分補正値計算（ 数値　&面付 , 文字列 &区分　,　参照 数値 &区分補正値 ）

    ケース開始
        ケース（ &区分 = "枚葉" ）

            &区分補正値 = &面付
        
        ケース（　&区分 = "冊子"　）

            &区分補正値 = 1

        ケース（　&区分 = "新聞"　）

            &区分補正値 = 1

        ケース（　&区分 = "複写"　）

            &区分補正値 = 1

    ケース終了

手続き定義終了
