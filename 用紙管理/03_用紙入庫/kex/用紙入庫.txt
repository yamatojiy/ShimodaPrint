*■***********************************************************
*■*** 
*■*** 用紙の入力情報をまとめる入力フォームです
*■*** 今まで入庫と納品を別フォームにしていましたが、今回１つにまとめます。
*■*** 
*■***********************************************************
    /*▼汎用 */
    var 数値  { &CHK }
    var 文字列{ &msgtxt }
    var 数値  { &入庫TbxNum = 0 }

    /*▼案件用変数 */
    var 日時  { &days }
    var 文字列{ &担当者 }

    /*▼伝票関連 */
    var 数値  { &伝票ID , &ns部数 }
    var 文字列{ &ns得意先 , &ns品名 }

    /*▼用紙情報 */
    var 文字列{ &ns銘柄 , &nsサブ銘柄 , &nsサイズ名 , &ns目 }
    var 数値  { &nsサイズID , &ns重さ }

    /*▼用紙情報 */
    var 数値  { &ns単価 }
    var 文字列{ &ns単価種 }

    /*▼表開き権限 */
    var 文字列{ &共有参照 = "共有参照" }
    var 文字列{ &専有 　　　= "専有" }

*■*-------------------------------------
*■*--  各パス関連
*■*-------------------------------------
    /*▼ファイルパス */
    var 文字列{ &用紙入庫DPath = #データパス名 }

    /*▼画像データ */
    var 文字列{ &iconPng = &用紙入庫DPath + "png\cut.png" }

    *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
    *■*:　モーダルフォーム

    /*▼モーダルフォーム用変数 */
    var 数値  { &Num }
    var 文字列{ &Str }
    var 日時  { &Day }

    /*▼カレンダー */
    var 文字列{ &nSカレンダーmodal = &用紙入庫DPath + "..\..\System\modal\Calendar\INF_DatePicker.wfx" }

    /*▼電卓 */
    var 文字列{ &nS電卓modal = &用紙入庫DPath + "..\..\System\modal\Calculator\INF_電卓.wfx" }

    /*▼用紙仕入先選択 */
    var 文字列{ &nS仕入会社modal = &用紙入庫DPath + "..\..\System\modal\General\004_用紙_08_01_製紙会社リスト_1.wfx" }

    /*▼単価種リスト */
    var 文字列{ &nS単価種modal = &用紙入庫DPath + "..\..\System\modal\General\004_用紙_02_08_単価種_1.wfx" }

    /*▼用紙索引 */
    var 文字列{ &S用紙IDmodal = &用紙入庫DPath + "..\01_用紙ID\用紙ID索引フォーム.wfx"  }

    /*▼用紙登録 */
    var 文字列{ &S用紙登録modal = &用紙入庫DPath + "..\01_用紙ID\用紙ID登録フォーム.wfx"  }

    *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
    *■*:　外部参照
    *■*:
    *■*:　共有ファイルから、サイズIDを使ってサイズ名称を取ってきます。

    /*▼用紙ID */
    var 文字列{ &用紙IDTbx = &用紙入庫DPath + "..\01_用紙ID\用紙ID.tbx" }

    /*▼subMasta */
    var 文字列{ &subMastaTbx = &用紙入庫DPath + "..\..\submaster.tbx"}

    /*▼用紙サイズ */
    var 文字列{ &用紙サイズTbx = &用紙入庫DPath + "..\..\System\tbx\04_01_用紙サイズリスト.tbx" }




*■*----------------------------------------------------------
*■*-----　基本処理
*■*----------------------------------------------------------
名札 MAIN

*
*■*----------------------------------------------------------
*■*-----　各オブジェクトイベント
*■*----------------------------------------------------------
    *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::
    *■*:　フォーム開始
    手続き定義開始　フォーム::フォーム開始(長整数　&表番号)

        /*▼呼び出した時の表番号を変数に保存する */
        &入庫TbxNum = &表番号

    手続き定義終了
    *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
    *■*:　伝票ID　ソース更新
    手続き定義開始　伝票ID::ソース値更新()

        手続き実行 伝票情報格納　( [伝票ID] , &ns得意先 , &ns品名 , &ns部数 )
        編集表 &入庫TbxNum
        メソッド呼び出し @フォーム.更新モード設定(0)
        
        if ( [伝票ID] )
            行訂正 終了状態 = &CHK , [得意先] = &ns得意先 , [品名] = &ns品名 , [部数] = &ns部数
        end

    手続き定義終了
    *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
    *■*:　用紙ID　ソース更新
    手続き定義開始　用紙ID::ソース値更新()

        手続き実行 用紙ID情報格納　( [用紙ID] , [仕入先]　, &ns銘柄　, &nsサブ銘柄  , &nsサイズID , &nsサイズ名 , &ns目  , &ns重さ , &ns単価種　, &ns単価　)
        編集表 &入庫TbxNum
        メソッド呼び出し @フォーム.更新モード設定(0)

        if ( [用紙ID] )
            行訂正 終了状態 = &CHK , [銘柄] =  &ns銘柄 , [紙色] = &nsサブ銘柄　, [サイズID] = &nsサイズID ,\
                [サイズ名] = &nsサイズ名 , [目] = &ns目　, [重さ] = &ns重さ
        end

        if ( [仕入先] )
                行訂正 終了状態 = &CHK , [単価種] = &ns単価種 , [入庫単価] = &ns単価 
        end

    手続き定義終了
    *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
    *■*:　仕入れ先　ソース更新
    手続き定義開始　仕入先::ソース値更新()
        
        手続き実行 仕入れ先用紙単価格納( [仕入先]　, [用紙ID] , &ns単価 , &ns単価種 )
        編集表 &入庫TbxNum
        メソッド呼び出し @フォーム.更新モード設定(0)

        if ( [仕入先] )
                行訂正 終了状態 = &CHK , [単価種] = &ns単価種 , [入庫単価] = &ns単価 
        end

    手続き定義終了
*■*----------------------------------------------------------
*■*----- 各手続き
*■*----------------------------------------------------------
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　テーブルID調査
*■*:　ターゲットのテーブルが開いてても開いて無くても表番号を返す関数
*■*:　引数１：ターゲットのテーブルアドレス
*■*:　引数２：新規で開くステータス（専有とか参照とか、すでに開いている場合はそのまま）
*■*:　戻り値：表のＩＤ
    手続き定義開始 テーブルID調査（ 文字列 &tbx ,文字列　&モード　, 参照　数値　&表ID ）

        *■*-------------------------------------
        *■*--  編集表番号取得（開いてても問題無し
        *■*-------------------------------------
        &表ID = #表番号取得( &tbx )
        if ( .not &表ID ) /*←未定義なら */
            
            表 &tbx , モード = &モード , 終了状態 = &CHK 
            &表ID = #表番号取得( &tbx )　　

        end
    手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　伝票番号から案件詳細情報を変数に格納する
*■*:　引数１：ターゲットの伝票番号
*■*:　戻値１：参照：得意先情報
*■*:　戻値２：参照：品名
*■*:　戻値３：参照：部数
    手続き定義開始 伝票情報格納（ 数値　&ID , 参照 文字列 &得意先 , 参照 文字列 &品名 , 参照 数値 &部数 ）

        var 数値  { &表ID = 0 }
        手続き実行 テーブルID調査( &subMastaTbx , &共有参照 , &表ID )
        編集表　&表ID

        検索 [伝票ＮＯ] { &ID } , 終了状態 = &CHK 

            if (&CHK = 1)

                &部数 =     [部数]
                &得意先 =  [得意先]
                &品名 =　　　[品名]
        
            end
        
        終了 表 &表ID
    手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　用紙IDから用紙詳細情報を変数に格納する
*■*:　引数１：ターゲットの用紙ID
*■*:　戻値１：参照：銘柄
*■*:　戻値２：参照：サブ銘柄
*■*:　戻値３：参照：サイズＩＤ
*■*:　戻値４：参照：サイズ名目
*■*:　戻値５：参照：YT(横目縦目)
*■*:　戻値６：参照：重さ
    手続き定義開始 用紙ID情報格納（ 数値　&ID , 文字列 &仕入れ先　,　参照 文字列 &銘柄 , 参照 文字列 &サブ銘柄 , 参照 数値 &サイズID , 参照 文字列 &サイズ名 , 参照 文字列 &YT , 参照 数値 &重さ , 参照　文字列　&単価種　,　参照　数値　&単価　）

        var 数値  { &表ID = 0 }
        手続き実行 テーブルID調査( &用紙IDTbx , &共有参照 , &表ID )
        編集表　&表ID

        検索 [ID] { &ID } , 終了状態 = &CHK 

            if (&CHK = 1)

                &銘柄       = [銘柄]
                &サブ銘柄    = [紙色]
                &サイズID    = [サイズ]
                &サイズ名   = [サイズ名目]
                &YT         = [目]
                &重さ       = [重さ]

            end
        
        終了 表 &表ID

        /*▼仕入れ先に情報があれば */
        if ( &仕入れ先 )

            手続き実行 仕入れ先用紙単価格納( &仕入れ先　, &ID , &単価 , &単価種 )
        
        end
    手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　仕入れ先を設定すると用紙IDから単価と単価種を返す。
*■*:　引数１：ターゲットの仕入れ先
*■*:　引数２：ターゲットの用紙ID
*■*:　戻値１：参照：単価
*■*:　戻値２：参照：単価種
    手続き定義開始　仕入れ先用紙単価格納（ 文字列 &仕入れ先　, 数値 &用紙ID , 参照　数値 &単価　, 参照　文字列　&単価種 ）

        var 数値  { &仕入表ID = 0 }
        手続き実行 テーブルID調査( &用紙IDTbx , &共有参照 , &仕入表ID )
        編集表　&仕入表ID

            検索 [ID] { &用紙ID } , 終了状態 = &CHK

            /*▼仕入れ先分岐 */
            ケース開始
                ケース（ &仕入れ先  = "児島" ）
                    &単価 = [児島単価]
                    &単価種 = [単価種]
                ケース（ &仕入れ先  = "レイメイ" ）
                    &単価 = [レイメイ単価]
                    &単価種 = [単価種]
                ケース（ &仕入れ先  = "網中" ）
                    &単価 = [網中単価]
                    &単価種 = [単価種]
                ケース（ &仕入れ先  = "紙弘" ）
                    &単価 = [紙弘単価]
                    &単価種 = [単価種]
                ケース（ &仕入れ先  = "紙弘福岡" ）
                    &単価 = [紙弘単価]
                    &単価種 = [単価種]
                ケース　その他
                    &単価 = ""
                    &単価種 = [単価種]

            ケース終了

        終了 表 &仕入表ID
    手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　選択している行の単価を修正する手続きです。
    手続き定義開始 単価修正処理（）

        *■*-------------------------------------
        *■*-- 専用変数定義
        *■*-------------------------------------
        var 数値  { &選択肢 }
        var 数値  { &用紙ID , &重さ　}
        var 文字列{ &銘柄 , &紙色 , &サイズ名 , &YT  }

        var 文字列{ &仕入先 , &単価種 }
        var 数値  { &入庫単価 , &単価 }

        var 文字列{ &タイトル , &表示本文 }
        *■*-------------------------------------
        *■*-- 変数格納
        *■*-------------------------------------

        &用紙ID = [用紙ID]
        &重さ　　 = [重さ]

        &銘柄   = [銘柄]
        &紙色   = [紙色]
        &サイズ名 = [サイズ名]
        &YT     = [目]

        &仕入先 = [仕入先]
        &単価種 = [単価種]
        &入庫単価 = [入庫単価]
        &単価  = [単価]


        *■*-------------------------------------
        *■*-- 確認表示
        *■*-------------------------------------

        &タイトル = "以下の単価情報を更新します。"
        &表示本文 = "用紙ID:　" +#文字列( &用紙ID ) + " " + &銘柄 + " " + &紙色 + " " + &サイズ名 + " " + &YT + " " + #文字列(　&重さ )　+ "         \n\n仕入先: ｛ " + &仕入先 + " ｝　　 単価種:　　" + &単価種 + " \n\n        " + #文字列( &入庫単価 ) + "  →  " + #文字列( &単価 )

        メッセージボックス &タイトル ,\
            &表示本文 , アイコン = ? , ボタン指定 = 5 , 制御文字展開 = する , &選択肢
        
        *■*-------------------------------------
        *■*-- 実行処理
        *■*-------------------------------------
        if (　&選択肢 = 6　)

            利用者コード "ADDIDP"
            var 数値  { &用紙表ID = 0 }
            手続き実行 テーブルID調査( &用紙IDTbx , &専有 , &用紙表ID )
                

            検索 [ID] { &用紙ID } , 終了状態 = &CHK 
            if ( &CHK = 1 )
            
                /*▼仕入れ先分岐 */
                ケース開始
                    ケース（ &仕入先  = "児島" ）
                        行訂正 終了状態 = &CHK , [児島単価] = &単価 , [単価種] = &単価種

                    ケース（ &仕入先  = "レイメイ" ）
                        行訂正 終了状態 = &CHK , [レイメイ単価] = &単価 , [単価種] = &単価種
                    
                    ケース（ &仕入先  = "網中" ）
                        行訂正 終了状態 = &CHK , [網中単価] = &単価 , [単価種] = &単価種

                    ケース（ &仕入先  = "紙弘" ）
                        行訂正 終了状態 = &CHK , [紙弘単価] = &単価 , [単価種] = &単価種

                    ケース（ &仕入先  = "紙弘福岡" ）
                        行訂正 終了状態 = &CHK , [紙弘単価] = &単価 , [単価種] = &単価種

                    ケース　その他

                ケース終了

            end
            終了 表 &用紙表ID

            *■*:::::::::::::::::::::::::::
            *■*:　入庫テーブルに戻って現状を更新
            編集表 &入庫TbxNum
            
            行訂正 終了状態 = &CHK ,　[入庫単価] = &単価 , [単価種] = &単価種

            &msgtxt　= "用紙tbx の単価情報を更新しました。"
            確認 &msgtxt

        end
    手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　金額と単価種から、単価の項目を返す関数
*■*:　引数１：金額
*■*:　引数２：単価種
*■*:　戻値１：単価
*■*:
*■*: #条件選択(　[単価種] = "㌔単価" ,([重さ]*[単価]/1000)*[枚数], [単価種] = "枚単価",[枚数]*[単価] ,[単価種] = "㌘単価" ,([重さ]*[単価]/100)*[枚数])
手続き定義開始 単価作成関数（）



手続き定義終了
