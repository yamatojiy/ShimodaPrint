*■***********************************************************
*■*** 
*■*** 台帳に関わるテーブルデータ更新 cmx
*■*** とにかくテーブルデータを更新するところまでを役割とし､ライブラリで呼び出して､上手いこと使ってつかーさい｡
*■*** 
*■*** 
*■***********************************************************
*■*----------------------------------------------------------
*■*-----　基本処理
*■*----------------------------------------------------------
名札 MAIN

＊
*■*----------------------------------------------------------
*■*-----　手続き
*■*----------------------------------------------------------
手続き定義開始 テーブルデータ更新（）

    *■*:一旦すべての表を閉じます。
    手続き実行 台帳用に各テーブルを集計()
    手続き実行 台帳集計()
    手続き実行 履歴テーブル作成()

手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　起動時に行う初回手続き。現状を一元化できるように台帳tbxに情報をまとめる
*■*:
*■*:
手続き定義開始 台帳用に各テーブルを集計（）

    手続き実行 表オープン（ &用紙入庫tbx , &共有参照 , &用紙入庫num , &用紙入庫F ）
    編集表 &用紙入庫num

        *■*::::::::::::::::::::::::::::::::::::::
        *■*:　まずは作業できるように作業場所へコピーします。
        書き出し 表 , &入庫COPYtbx , 終了状態 = &CHK 

    条件 ( &用紙入庫F = 0 ) 終了 表 &用紙入庫num

    /*▼作業用テーブルから、入庫情報などを分割する */
    手続き実行 表オープン（ &入庫COPYtbx , &共有参照 , &入庫COPYnum , &入庫COPYF ）
    編集表　&入庫COPYnum

        /*▼必要な設定条件を登録します。 */
        *■*::::::::::::::::::【 行集計して、各用紙の合算値を表示する 】::::::::::::::::::::
        行集計条件登録　条件名 = "用紙ID別合計" , 並べ替え　= する　,\
            大計[用紙ID]{ [用紙ID] #項目値( [用紙ID] ) , [銘柄] #項目値( [銘柄] ) , [紙色] #項目値( [紙色] ) ,　[サイズID] #項目値( [サイズID] )　,\
            [サイズ名] #項目値( [サイズ名]　) , [目] #項目値( [目] ) , [重さ] #項目値( [重さ] ) , [枚数]　#合計 , [入庫予定日付] #項目値( [入庫予定日付] )}
        *■*::::::::::::::::::【 入庫合計テーブルを書き出す処理 】::::::::::::::::::::
        書き出し条件登録　表，条件名 = "現入庫用紙リスト" , &現入庫数tbx ,　ファイル名変更　= しない　,\
            { [用紙ID] , [銘柄] , [紙色] , [サイズID] , [サイズ名] , [目] ,　[重さ] , [枚数] }
        *■*::::::::::::::::::【 入庫予定合計テーブルを書き出す処理 】::::::::::::::::::::
        書き出し条件登録　表，条件名 = "現入庫予定リスト" , &現入庫予定数tbx　, ファイル名変更　= しない　,\
            { [用紙ID] , [銘柄] , [紙色] , [サイズID] , [サイズ名] , [目] ,　[重さ] , [枚数] , [入庫予定日付] }
        *■*::::::::::::::::::【 履歴用にまとめて書き出す処理 】::::::::::::::::::::
        書き出し条件登録　表，条件名 = "入庫履歴書き出し" , &入庫履歴tbx　, ファイル名変更　= しない　,\
            { [用紙ID] , [銘柄] , [紙色] , [サイズID] , [サイズ名] , [目] ,　[重さ] , [枚数] , [受領日] , [仕入先] , [発注ID]　, [伝票ID] , [品名] }

        *■*::::::::::::::::::::::::::::::::::::::
        *■*:　1 ．入庫情報を抜き出す。
        絞り込み [受領日] { <>"" } , 終了状態 = &CHK
        並べ替え  { [用紙ID] 昇順 }

        if ( #総件数 <> 0 )
        
            行集計 条件名 = "用紙ID別合計" , データ行 = 無効
            書き出し 表 , 条件名 = "現入庫用紙リスト" , 終了状態 = &CHK
        
        end
         
        解除
        *■*-------------------------------------
        *■*-- 消費履歴用にも書き出しを行います。
        絞り込み [受領日] { <>"" } , 終了状態 = &CHK
        並べ替え  { [用紙ID] 昇順 }

        if ( #総件数 <> 0 )
            書き出し 表 , 条件名 = "入庫履歴書き出し" , 終了状態 = &CHK
            
            /*▼----------------------------項目計算式を入れて､見出しを自動作成する------------------------- */
            表 &入庫履歴tbx  , モード = &専有
            編集表 &入庫履歴tbx
            

                項目属性変更2 追加 {"見出し" , "文字列" , , , " #条件選択( [品名] <>#未定義 ,""【 "" +  [仕入先] + "" 】 :  "" + #部分列( [品名] , 0 , 6) +"" …""  , [品名] = #未定義 ,  [仕入先]  ) " }

            終了 表 編集対象表
            編集表　&入庫COPYnum
            /*▼----------------------------ここまでの処理----------------------------------------------- */
        end

        解除
        *■*::::::::::::::::::::::::::::::::::::::
        *■*:　２ ．予定情報を抜き出す。
        絞り込み [受領日] { ="" } , 終了状態 = &CHK
        並べ替え  { [用紙ID] 昇順 }

        if ( #総件数 <> 0 )
    
            行集計 条件名 = "用紙ID別合計" , データ行 = 無効 
            書き出し 表 , 条件名 = "現入庫予定リスト" , 終了状態 = &CHK
        
        end

    条件 ( &入庫COPYF = 0 ) 終了 表 &入庫COPYnum

    /*▼断裁情報を書き出します */
    手続き実行 表オープン（ &元資材断裁tbx , &共有参照 , &元資材断裁num , &元資材断裁F ）
    編集表 &元資材断裁num

        /*▼必要な設定条件を登録します。 */
        *■*::::::::::::::::::【 行集計して、各用紙の合算値を表示する 】::::::::::::::::::::
        行集計条件登録　条件名 = "用紙ID別合計" , 並べ替え　= する　,\
            大計[用紙ID]{ [用紙ID] #項目値( [用紙ID] ) , [銘柄] #項目値( [銘柄] ) , [サブ銘柄] #項目値( [サブ銘柄] ) ,　[サイズID] #項目値( [サイズID] )　,\
            [サイズ名] #項目値( [サイズ名]　) , [目] #項目値( [目] ) , [重さ] #項目値( [重さ] ) , [消費枚数]　#合計 }
        *■*::::::::::::::::::【 合算して消費用にまとめて出す 】::::::::::::::::::::
        書き出し条件登録　表，条件名 = "現消費リスト" , &断裁tbx , ファイル名変更　= しない　,\
            { [用紙ID] , [銘柄] , [サブ銘柄] , [サイズID] , [サイズ名] , [目] ,　[重さ] , [消費枚数] , [日付] }

        *■*::::::::::::::::::【 履歴用にまとめて書き出す処理 】::::::::::::::::::::
        書き出し条件登録　表 , 条件名 = "出庫履歴書き出し" , &出庫履歴tbx　, ファイル名変更　= しない　,\
            { [用紙ID] , [銘柄] , [サブ銘柄] , [サイズID] , [サイズ名] , [目] ,　[重さ] , [枚数] , [日付] , [伝票番号] , [品名] }


        *■*::::::::::::::::::::::::::::::::::::::
        *■*:　消費量概算表を出力します。

        並べ替え  { [用紙ID] 昇順 }

        if ( #総件数 <> 0 )
            行集計 条件名 = "用紙ID別合計" , データ行 = 無効
            
            書き出し 表 , 条件名 = "現消費リスト" , 終了状態 = &CHK

            解除

            *■*::::::::::::::::::::::::::::::::::::::
            *■*:　履歴用に書き出し処理

            書き出し 表 , 条件名 = "出庫履歴書き出し" , 終了状態 = &CHK

        end
        条件 ( &元資材断裁F = 0 ) 終了 表 &元資材断裁num

手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　履歴テーブルを作成する。
*■*:　各情報をベースに
*■*:
手続き定義開始 履歴テーブル作成（）

    /*▼まずは表を作成します。ディスク上に同ファイルがあれば上書きする。らしい。 */
    表作成　&用紙履歴tbx , 定義ファイル = &用紙履歴k3x , 終了状態 = &CHK , リトライ = する
    
    if ( &CHK <> 1 ) /*▼何らかの理由で履歴テーブルが生成できなかったらエラー番号を表示する */
        &msgtxt = #文字列( &CHK )
        確認 &msgtxt 

    end
    
    &用紙履歴num = #IS表

    編集表 &用紙履歴tbx

    *■*::::::::::::::::::【 並べ替え時ID、日付け順になる様に登録 】::::::::::::::::::::
    索引定義　 索引名 = "ID順"　, 重複 = 許可 , 条件登録 = "ID順" ,\
    { [用紙ID]　昇順 , [日付]　降順 , [項目1] 昇順 }

    *■*::::::::::::::::::::::::::::::::::::::
    *■*::　入庫履歴を読み込み
    読み込み 表 , &入庫履歴tbx , 編集表 = しない , 終了状態 = &CHK ,\
     { [用紙ID] , [銘柄] , [紙色] , [サイズID] , [サイズ名] , [目] ,　[重さ] , [枚数] , [日付][受領日]　, [項目1][伝票ID]　,　[項目2][見出し] }

    *■*::　出庫履歴を読み込み
    読み込み 表 , &出庫履歴tbx , 編集表 = しない , 終了状態 = &CHK ,\
     { [用紙ID] , [銘柄] , [紙色][サブ銘柄] , [サイズID] , [サイズ名] , [目] ,　[重さ] , [枚数] , [日付] , [項目1][伝票番号] , [項目2][品名]　}

    *■*::　棚卸在庫を読み込み
    読み込み 表 , &棚卸在庫tbx , 編集表 = しない , 終了状態 = &CHK ,\
     { [用紙ID] , [銘柄] , [紙色] , [サイズID] , [サイズ名] , [目] ,　[重さ] , [枚数] , [日付][入力日] , [項目2][項目名称]　}

    条件 ( &用紙履歴F = 0 ) 終了 表 &用紙履歴num

手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　用紙台帳tbxに集約させ、最新の台帳を作り上げます。
*■*:
*■*:
手続き定義開始 台帳集計（）

    手続き実行 表オープン（ &台帳tbx , &共有更新 , &台帳num , &台帳F ）
    編集表　&台帳num
    行削除 *

    読み込み 表 , &現入庫数tbx , 編集表 = しない , 終了状態 = &CHK ,\
    { [用紙ID] , [銘柄] , [紙色] , [サイズID] , [サイズ名] , [目] ,　[重さ] , [入庫枚数] [枚数] }


    手続き実行 入庫予定情報を台帳へ（ &台帳num ）

    手続き実行 棚卸在庫情報を台帳へ（ &台帳num ）

    手続き実行 消費情報を台帳へ（ &台帳num ）


    条件 ( &台帳F = 0 ) 終了 表 &台帳num

手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　入庫予定表の情報を台帳にまとめる処理です。
*■*:　予定表の用紙IDが台帳にあれば、行訂正。無ければ行追加を行います。
手続き定義開始 入庫予定情報を台帳へ（ 数値 &台帳num ）


    /*▼移行用変数 */
    var 数値{ &用紙IDt , &サイズIDt , &重さt , &枚数t }
    var 文字列{ &銘柄t , &紙色t , &サイズ名t , &目t }
    var 日時  { &予定日t }

    /*▼入庫予定表をオープン */
    手続き実行 表オープン（ &現入庫予定数tbx , &共有参照 , &現入庫予定数num　, &現入庫予定数F　）
    編集表 &現入庫予定数num

    繰り返し（ .NOT #終端行 ）
    
        /*▼格納します */
        &用紙IDt = [用紙ID]　&サイズIDt = [サイズID] &重さt = [重さ]　&枚数t = [枚数]
        &銘柄t = [銘柄] &紙色t = [紙色]　&サイズ名t = [サイズ名] &目t = [目]
        &予定日t = [入庫予定日付]

        /*▼台帳に入力を開始します */
        編集表 &台帳num
        
        検索 [用紙ID] { &用紙IDt } , 終了状態 = &CHK 
        if ( &CHK = 1 )
            行訂正 終了状態 = &CHK , [予定枚数] = &枚数t , [直近予定日] = &予定日t
        else
            行追加 終了状態 = &CHK , [用紙ID] = &用紙IDt , [サイズID] = &サイズIDt , [重さ] = &重さt , [予定枚数] = &枚数t ,\
            [銘柄] = &銘柄t , [紙色] = &紙色t ,　[サイズ名] = &サイズ名t , [目] = &目t , [直近予定日] = &予定日t
        end
        編集表 &現入庫予定数num
        ジャンプ 行番号 = 次行 
    繰り返し終了

    条件 ( &現入庫予定数F = 0 ) 終了 表 &現入庫予定数num

手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　在庫情報の登録をする場所です。
*■*:　予定表の用紙IDが台帳にあれば、行訂正。無ければ行追加を行います。
手続き定義開始 棚卸在庫情報を台帳へ（ 数値 &台帳num ）

    /*▼移行用変数 */
    var 数値{ &用紙IDt , &サイズIDt , &重さt , &枚数t }
    var 文字列{ &銘柄t , &紙色t , &サイズ名t , &目t }
    var 日時  { &予定日t }

    /*▼入庫予定表をオープン */
    手続き実行 表オープン（ &棚卸在庫tbx , &共有参照 , &棚卸在庫num　, &現入庫予定数F　）
    編集表 &棚卸在庫num

    繰り返し（ .NOT #終端行 ）
    
        /*▼格納します */
        &用紙IDt = [用紙ID]　&サイズIDt = [サイズID] &重さt = [重さ]　&枚数t = [枚数]
        &銘柄t = [銘柄] &紙色t = [紙色]　&サイズ名t = [サイズ名] &目t = [目]
        &予定日t = [入力日]

        /*▼台帳に入力を開始します */
        編集表 &台帳num
        
        検索 [用紙ID] { &用紙IDt } , 終了状態 = &CHK 
        if ( &CHK = 1 )
            行訂正 終了状態 = &CHK , [棚卸在庫数] = &枚数t
        else
            行追加 終了状態 = &CHK , [用紙ID] = &用紙IDt , [サイズID] = &サイズIDt , [重さ] = &重さt , [棚卸在庫数] = &枚数t ,\
            [銘柄] = &銘柄t , [紙色] = &紙色t ,　[サイズ名] = &サイズ名t , [目] = &目t
        end

        編集表 &棚卸在庫num
        ジャンプ 行番号 = 次行 
    繰り返し終了

    条件 ( &棚卸在庫F = 0 ) 終了 表 &棚卸在庫num

手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　消費情報を台帳にまとめる処理です。
*■*:　予定表の用紙IDが台帳にあれば、行訂正。追加しません。（無い上情報をマイナスしても意味が無い）
手続き定義開始 消費情報を台帳へ（ 数値 &台帳num ）

    /*▼移行用変数 */
    var 数値{　&用紙IDt , &枚数t }

    /*▼消費テーブルをオープン */
    手続き実行 表オープン（ &断裁tbx , &共有参照 , &断裁num　, &断裁F　）
    編集表 &断裁num

    繰り返し（ .NOT #終端行 ）
    
        /*▼格納します */
        &用紙IDt = [用紙ID]　&枚数t = [消費枚数]

        /*▼台帳に入力を開始します */
        編集表 &台帳num
        
        検索 [用紙ID] { &用紙IDt } , 終了状態 = &CHK 
        if ( &CHK = 1 )
            行訂正 終了状態 = &CHK , [消費枚数] = &枚数t
        end
        編集表 &断裁num
        ジャンプ 行番号 = 次行
    繰り返し終了

    条件 ( &断裁F = 0 ) 終了 表 &断裁num

手続き定義終了
