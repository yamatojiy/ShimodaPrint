*■***********************************************************
*■*** 
*■*** 用紙IDを登録するフォームです。
*■*** 　引用が可能にします。　重複確認を先にします。　最後登録させます。
*■*** 
*■***********************************************************
/*▼汎用 */
var 数値  { &CHK ,　&CHK2　,　&CHK3 }
var 文字列{ &msgtxt }
var 文字列{ &タイトル , &表示本文  }
var 数値  { &用紙IDNum }

var 文字列{ &Str }
var 数値　{ &Num }

/*▼戻り用変数 */
var 数値  { &Num = "" }
var 文字列{ &登録用文言 = "" }
var 数値  { &登録NG =  0 }

/*▼案件用変数 */
var 数値  { &用紙ID }
var 文字列{ &銘柄A , &紙色A , &YTA , &サイズ名A }
var 数値  { &重さA , &サイズA　, &サイズ詳細A }
var 文字列{ &廃番A = "" }
var 日時  { &廃番日A = "" , &登録日A = ""　}
var 文字列{ &備考A = "" }
var 文字列{ &結合名 }
var 数値  { &登録error = 0 }

/*▼バックアップ用 */
var 文字列{ &銘柄B , &紙色B , &YTB , &サイズ名B }
var 数値  { &重さB , &サイズB , &サイズ詳細B　}
var 文字列{ &廃番B = "" }
var 日時  { &廃番日B = "" , &登録日B = "" }
var 文字列{ &備考B = "" }
var 数値  { &重複ID = "" }

/*▼変更チェック用 */
var 数値  { &ans }
var 数値  { &変更boot[7] ={0} }
*■* １：銘柄
*■* ２：紙色
*■* ３：サイズ
*■* ４：目
*■* ５：重さ
*■* ６：廃番
*■* ７：備考

var 数値  { &選択行番号 }
var 文字列{ &ketu }

/*▼ID順オン/オフの確認 */
var 数値  { &IDchkONOFF = 1 }
var 文字列{ &SrotSTR = "結合名順" }

/*▼検索動作用 */
var 文字列{ &銘柄S , &紙色S , &YTS , &サイズ名S , &備考S }
var 数値  { &重さS }

/*▼pass */
var 文字列{ &passCode = "res06" }
var 文字列{ &OpenPassCode = "ADDIDP" }

/*▼動作✓用配列 */
var 数値  { &bt[4] ={""} }

/*▼ファイルパス */
var 文字列{ &用紙ID検索wfxPath = #データパス名 }
var 文字列{ &用紙IDTbx        =  &用紙ID検索wfxPath     +  "用紙ID.tbx" }

/*▼画像データ */
var 文字列{ &iconPng2        = &用紙ID検索wfxPath       + "png\in.png" }

*■*----------------------------------------------------------
*■*----- ライブラリ
*■*----------------------------------------------------------
    var 文字列  { &Lib = &用紙ID検索wfxPath + "..\..\System\Lib\Lib_ライブラリ桐処理.cmx" }
    ライブラリ &Lib

*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　索引か登録かどちらかなのかフラグ。　0　索引　1登録
    var 数値  { &登録変更変更boot = 1 } 

*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　外部参照
*■*:
    *■*:　共有ファイルから、サイズIDを使ってサイズ名称を取ってきます。
        var 文字列{ &外部tbxPath = &用紙ID検索wfxPath + "..\..\System\tbx\" }
        var 文字列{ &用紙サイズtbx = &外部tbxPath + "04_01_用紙サイズリスト.tbx" }
        var 文字列{ &用紙サイズmodal = &外部tbxPath + "..\modal\General\004_用紙_04_01_用紙サイズリスト_1_(基本).wfx" }

*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　/*▼モーダルフォーム */
    var 文字列{ &銘柄選択modal    = &用紙ID検索wfxPath    + "\modal\【用紙ID】銘柄選択モーダル.wfx" }
    var 文字列{ &紙色選択modal = &用紙ID検索wfxPath       + "\modal\【用紙ID】SUB銘柄選択モーダル.wfx" }
    var 文字列{ &サイズ選択modal   = &用紙ID検索wfxPath    + "\modal\【用紙ID】サイズ選択モーダル.wfx" }
    var 文字列{ &目選択modal      = &用紙ID検索wfxPath    + "\modal\【用紙ID】目_選択モーダル.wfx" }
    var 文字列{ &重さ選択modal    = &用紙ID検索wfxPath    + "\modal\【用紙ID】重さ選択モーダル.wfx" }

*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　/*▼一括処理 */
    var 文字列{ &修正データ反映cmx = &用紙ID検索wfxPath    + "\cmx\用紙ID反映.cmx" }


*■*----------------------------------------------------------
*■*-----　基本処理
*■*----------------------------------------------------------
名札 MAIN


*
*■*----------------------------------------------------------
*■*----- フォーム専用
*■*----------------------------------------------------------
手続き定義開始　フォーム::フォーム開始(長整数　&表番号)

    &用紙IDNum　= &表番号

手続き定義終了
手続き定義開始　フォーム::フォーム終了()

    手続き実行　リセット（）

手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　変数リセット
手続き定義開始 リセット（）

    /*▼変数をリセットします */
    &銘柄A ="" &紙色A ="" &YTA =""&サイズ名A =""
    &重さA ="" &サイズA =""　&サイズ詳細A = ""
    &廃番A = ""
    &廃番日A = "" &登録日A = ""　
    &備考A = ""

    &銘柄B ="" &紙色B ="" &YTB ="" &サイズ名B =""
    &重さB ="" &サイズB ="" &サイズ詳細B = ""
    &廃番B = ""
    &廃番日B = "" &登録日B = ""
    &備考B = ""

    &ans = #配列代入("変更boot" , "" )

    &銘柄S ="" , &紙色S ="" , &YTS ="" , &サイズ名S ="" , &備考S =""
    &重さS =""
    &登録error = 0
    &重複ID = ""
    &登録用文言 = ""
    &用紙ID = ""

    メソッド呼び出し @フォーム.変数変更()

手続き定義終了
*■***********************************************************
*■***　選択行を変数呼び出し
*■***********************************************************
手続き定義開始 変数呼び出し（）

    &銘柄A = [銘柄] , &紙色A　= [紙色] , &YTA　= [目] , &サイズ名A　= [サイズ名目]
    &重さA = [重さ] , &サイズA = [サイズ] , &サイズ詳細A = [サイズ詳細ID]
    &廃番A = [廃番]
    &廃番日A = [廃番設定日] &登録日A = [登録日]
    &備考A = [備考]

    &銘柄B = [銘柄] , &紙色B　= [紙色] , &YTB　= [目] , &サイズ名B　= [サイズ名目]
    &重さB = [重さ] , &サイズB = [サイズ] , &サイズ詳細B = [サイズ詳細ID]
    &廃番B = [廃番]
    &廃番日B = [廃番設定日] &登録日B = [登録日]
    &備考B = [備考]

    &用紙ID = [ID]

    &結合名 = [結合名（半）]

    &ans = #配列代入("変更boot" , 0 )

    &登録error = 0
    &重複ID = ""
    &登録用文言 = ""


    &選択行番号 = #行番号

手続き定義終了
*■*----------------------------------------------------------
*■*-----　検索
*■*----------------------------------------------------------
手続き定義開始　Word検索::ソース値更新()
    手続き実行 ワード絞り込み（）
手続き定義終了
*■*----------------------------------------------------------
*■*-- ワード絞込関数
*■*-- 入力が更新されるたびに、現状入力されている絞り込みを行います。
*■*----------------------------------------------------------
手続き定義開始 ワード絞り込み（）

    var 文字列{ &SeachWord }

    解除
    *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::
    *■*: 1 銘柄
    if ( &銘柄S )

        &SeachWord = #半角( &銘柄S )
        絞り込み [結合名（半）] { * &SeachWord * } , 終了状態 = &CHK

    end
    *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::
    *■*: ２ 紙色
    if ( &紙色S )

        &SeachWord = #半角( &紙色S )
        絞り込み [結合名（半）] { * &SeachWord * } , 終了状態 = &CHK

    end
    *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::
    *■*: ３ サイズ
    if ( &サイズ名S )

        &SeachWord = #半角( &サイズ名S )
        絞り込み [結合名（半）] { * &SeachWord * } , 終了状態 = &CHK

    end
    *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::
    *■*: ４ YT　目
    if ( &YTS )

        &SeachWord = #半角( &YTS )
        絞り込み [結合名（半）] { * &SeachWord * } , 終了状態 = &CHK

    end
    *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::
    *■*: ５ 重さ
    if ( &重さS )

        &SeachWord = #半角( #文字列( &重さS ) )
        絞り込み [結合名（半）] { * &SeachWord * } , 終了状態 = &CHK

    end
    *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::
    *■*: ６ 備考
    if ( &備考S )

        &SeachWord = #半角( &備考S )
        絞り込み [結合名（半）] { * &SeachWord * } , 終了状態 = &CHK

    end
    並べ替え 条件名 = "結合名順"
手続き定義終了
*■***********************************************************
*■***　オブジェクトの項目を変更する
*■* １：銘柄
*■* ２：紙色
*■* ３：サイズ
*■* ４：目
*■* ５：重さ
*■* ６：廃番
*■* ７：備考
*■***********************************************************
    手続き定義開始　OBJ銘柄::ソース値更新()
        if ( &銘柄A <> &銘柄B )
            &変更boot[1] = 1
            手続き実行　同IDチェック()
        else
            &変更boot[1] = 0
            手続き実行　同IDチェック()
        end
    手続き定義終了
    手続き定義開始　OBJ紙色::ソース値更新()
        if ( &紙色A <> &紙色B )
            &変更boot[2] = 1
            手続き実行　同IDチェック()
        else
            &変更boot[2] = 0
            手続き実行　同IDチェック()
        end
    手続き定義終了
    手続き定義開始　OBJサイズ::ソース値更新()

        検索 [サイズ] { &サイズA } , 終了状態 = &CHK 
        &サイズ名A = [サイズ名目]
        &サイズ詳細A　= [サイズ詳細ID]

        if ( &サイズA <> &サイズB )
            &変更boot[3] = 1
            手続き実行　同IDチェック()
        else
            &変更boot[3] = 0
            手続き実行　同IDチェック()
        end
    手続き定義終了
    手続き定義開始　OBJサイズ詳細::ソース値更新()

        var 数値  { &OPnum = 0  , &openF = 0 }
        var 文字列 { &mode = ”共有更新” }
        手続き実行 表オープン（ &用紙サイズtbx , &mode , &OPnum , &openF ）
        編集表 &OPnum

        検索 [名称ID] { &サイズ詳細A } , 終了状態 = &CHK 
        &サイズA = [作業ID]
        &サイズ名A = [名称]

        条件 ( &openF = 0 ) 終了 表 &OPnum
        編集表 &用紙IDNum

        if ( &サイズ詳細A <> &サイズ詳細B )
            &変更boot[3] = 1
            手続き実行　同IDチェック()
        else
            &変更boot[3] = 0
            手続き実行　同IDチェック()
        end
    手続き定義終了
    手続き定義開始　OBJ目::ソース値更新()
        if ( &YTA <> &YTB )
            &変更boot[4] = 1
            手続き実行　同IDチェック()
        else
            &変更boot[4] = 0
            手続き実行　同IDチェック()
        end
    手続き定義終了
    手続き定義開始　OBJ重さ::ソース値更新()
        if ( &重さA <> &重さB )
            &変更boot[5] = 1
            手続き実行　同IDチェック()
        else
            &変更boot[5] = 0
            手続き実行　同IDチェック()
        end
    手続き定義終了
    手続き定義開始　OBJ廃番::ソース値更新()
        if ( &廃番A <> &廃番B ) /*▼設定を変更するとへ変更した時間が表示されるようになりました。 */
            &変更boot[6] = 1
            &廃番日A = #日時値
            手続き実行　同IDチェック()
        else
            &廃番日A = &廃番日B
            &変更boot[6] = 0
        end
    手続き定義終了
    手続き定義開始　OBJ備考::ソース値更新()
        if ( &備考A <> &備考B )
            &変更boot[7] = 1
        else
            &変更boot[7] = 0
        end
    手続き定義終了

*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　ID順のオンオフ切替
手続き定義開始　ID順判定（）

    /*▼1なら結合順からID順に変更。0ならID順から結合順に変更 */
    if ( &IDchkONOFF = 1 .or #絞り込み状態 = 0 )

        &SrotSTR　= "ID順"
        並べ替え 条件名 = "ID順"
        &IDchkONOFF = 0
        
    else

        &SrotSTR = "結合名順"
        並べ替え 条件名 = "結合名順"
        &IDchkONOFF = 1
        
    end

    オブジェクト操作 @IDソート.標題 = &SrotSTR

手続き定義終了
*■*----------------------------------------------------------
*■*-----　オブジェクト専用手続き
*■*----------------------------------------------------------
*■***********************************************************
*■***　同じ情報になっていないかを常にチェックする
*■***********************************************************
手続き定義開始 同IDチェック（）
    
    var 数値  { &sum }

    &sum = #配列合計（ "変更boot" ）

    if( &sum )
        /*▼加工した情報を変数にまとめる*/
        &ketu = #半角( &銘柄A ) +"-"+ #半角( &紙色A )　+"-"+　#半角( #文字列( &サイズA ) )　+"-"+　#半角( &YTA )　+"-"+　#半角( #文字列( &重さA ) )

        /*▼まずは呼び出した名称と変更が無いか確認する。違う場合は警告を出す。同じなら同IDで処理できるので、特記どうこうしない */
        if ( &ketu <> &結合名 )

            *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
            *■*:　違う場合は調査検索にひっかかればそのIDを。無ければ新規となる。
            検索 [結合名（半）] { &ketu } , 終了状態 = &CHK 
            if (&CHK = 1 )
            
                &登録用文言 = #文字列( [ID] ) +" : "+ [結合名] +" と、同じ情報が入力されています。"
                &登録error = 1
                &重複ID = [ID]
            
            else

                &登録用文言 = "他IDにヒットしませんでした。修正OR新規登録ができます。"
                &登録error = 0

            end
        else
            &登録用文言 = ""
            &登録error = 0
        end
    
    else
        &登録用文言 = ""
    end

    メソッド呼び出し @フォーム.変数変更()

手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　用紙ID登録処理
*■*:　　修正した変数を登録しよう！
*■*:　
手続き定義開始 用紙ID登録処理（）

    var 数値  { &sum1All = 0  , &Ans = 0 , &sum2 = 0 , &blankError }

    &sum1All = &変更boot[1] + &変更boot[2] + &変更boot[3] + &変更boot[4] + &変更boot[5]
    &sum2 = #配列合計（ "変更boot" ）

    /*▼登録error状態だったら処理を終了する */
    if ( &登録error )
        
        &msgtxt = "変更後のデータが　ID:" + #文字列( &重複ID ) +" と重複するため登録できません。"
        確認 &msgtxt
        手続き終了
    end
    /*▼必要箇所の空白を見つけたら処理を終了にする */
    条件 ( &銘柄A = #未定義 ) &blankError = 1
    条件 ( &銘柄A = #未定義 ) &msgtxt = "銘柄が未記入で登録できません。"

    条件 ( &サイズA = #未定義 ) &blankError = 3
    条件 ( &サイズA = #未定義 ) &msgtxt = "サイズが未記入で登録できません。"

    条件 ( &YTA =　　#未定義 ) &blankError = 4
    条件 ( &YTA =　　#未定義 ) &msgtxt = "目が未記入で登録できません。"

    条件 ( &重さA = #未定義 ) &blankError = 5
    条件 ( &重さA = #未定義 ) &msgtxt = "重さが未記入で登録できません。"

    if ( &blankError > 0 )  /*▼情報が足りないと未登録で終わらせます。 */
        確認 &msgtxt
        &blankError = 0 
        手続き終了
    end


    /*▼名目が変更されている場合です。この場合は、廃番・備考の情報も全て更新します。 */
    if ( &sum1All )

        &タイトル = "！元のデータから変更を行いますか？"
        &表示本文 = " 【前】" + &結合名 + "\n 【後】" + &ketu　+"\n\n 変更する場合は「はい」を選択してください。　\n 新規追加登録する場合は「いいえ」を選択してください。"
        メッセージボックス &タイトル , &表示本文 , アイコン = i , ボタン指定 = 4 , 制御文字展開 = する , &Ans

        ケース開始
        ケース（ &Ans = 6 ）/*▼修正判定 */
            手続き実行 変更登録(1)
        ケース（ &Ans = 7 ）/*▼追加判定 */
            手続き実行 変更登録(2)
        ケース終了

        手続き実行　リセット（）
        手続き終了
    end

    /*▼廃番設定更新。廃番のフラグが立ってたら */
    if ( &変更boot[6] = 1 )

        &タイトル = "廃番設定が更新されています。"
        &表示本文 = "\n  IDデータに反映しますか？"
        メッセージボックス &タイトル , &表示本文 , アイコン = i , ボタン指定 = 2 , 制御文字展開 = する , &Ans

        if ( &Ans = 1 )

            行訂正 終了状態 = &CHK , [廃番] = &廃番A , [廃番設定日] = &廃番日A
            &msgtxt = "廃番設定を更新しました。"
            確認 &msgtxt
        end
    end
    /*▼備考設定のフラグが立っていたら */
    if ( &変更boot[7] = 1 )

        &タイトル = "備考が更新されています。"
        &表示本文 = "\n  IDデータに反映しますか？"
        メッセージボックス &タイトル , &表示本文 , アイコン = i , ボタン指定 = 2 , 制御文字展開 = する , &Ans

        if ( &Ans = 1 )

            行訂正 終了状態 = &CHK ,[備考] = &備考A
            &msgtxt = "備考を更新しました。"
            確認 &msgtxt
        end
    end

    手続き実行　リセット（）

手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　登録編集する関数
手続き定義開始 変更登録（ 数値　&mode ）

    var 数値  { &サイズ詳細ID　, &追加用紙ID , &Ans = 0 }
    /*▼各項目を登録用に全角・半角に直します。日付け等は修正したタイミングに */
    
    &銘柄A = #全角( &銘柄A )
    &紙色A　= #全角( &紙色A )
    &YTA　= #全角( &YTA )
    &サイズ詳細ID = (&サイズA * 100 ) + 1
    &サイズ名A　= #全角( &サイズ名A )

    /*▼モードが１なら行訂正 */
    if ( &mode = 1 )

        ジャンプ 行番号 = &選択行番号
        行訂正 終了状態 = &CHK , [銘柄] = &銘柄A , [紙色] = &紙色A , [目] = &YTA ,[サイズ名目] = &サイズ名A , [サイズ詳細ID] = &サイズ詳細ID ,\
        [サイズ] = &サイズA , [重さ] = &重さA , [廃番] = &廃番A , [廃番設定日] = &廃番日A , [登録日] = &登録日A , [備考] = &備考A , [登録日] = #日時値
　
        &タイトル = "Done"
        &表示本文 = "ID:" + #文字列( &用紙ID ) +" "+ &ketu + " の修正が完了しました。"
        メッセージボックス &タイトル , &表示本文 , アイコン = i , ボタン指定 = 1 , 制御文字展開 = する , &Ans
    end
    
    /*▼モードが2なら追加 */
    if ( &mode = 2 )

        解除
        &追加用紙ID = #総件数 + 1
        行追加 終了状態 =　　 &CHK , [銘柄] = &銘柄A , [紙色] = &紙色A , [目] = &YTA ,[サイズ名目] = &サイズ名A , [サイズ詳細ID] = &サイズ詳細ID ,\
        [サイズ] = &サイズA , [重さ] = &重さA , [廃番] = &廃番A , [廃番設定日] = &廃番日A , [登録日] = &登録日A , [備考] = &備考A , [ID] = &追加用紙ID ,\
        [登録日] = #日時値

        &タイトル = "Done"
        &表示本文 = "ID:" + #文字列( &追加用紙ID ) +" "+ &ketu + " の新規登録が完了しました。"
        メッセージボックス &タイトル , &表示本文 , アイコン = i , ボタン指定 = 1 , 制御文字展開 = する , &Ans
    end
手続き定義終了
