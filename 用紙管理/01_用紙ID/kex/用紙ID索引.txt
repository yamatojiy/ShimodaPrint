*■***********************************************************
*■*** 
*■*** 用紙IDを登録するフォームです。
*■*** 　引用が可能にします。　重複確認を先にします。　最後登録させます。
*■*** 
*■***********************************************************
/*▼汎用 */
var 数値  { &CHK ,　&CHK2　,　&CHK3 }
var 文字列{ &msgtxt }
var 文字列{ &タイトル , &表示本文 , &mode = "共有更新" }
var 文字列{ &Str }
var 数値  { &Num }

/*▼戻り用変数 */
var 数値  { &Num = "" }

/*▼案件用変数 */
var 文字列{ &銘柄S , &サブ銘柄S , &YTS , &サイズ名 }
var 数値  { &重さS , &サイズn , &用紙ID  = 0 }
var 文字列　　{ &廃番CHK = "" }
var 日時 { &廃番日 = "" }
var 文字列{ &備考 = "" }

/*▼ファイルパス */
var 文字列{ &用紙ID検索wfxPath = #データパス名 }
var 文字列{ &用紙IDTbx        =  &用紙ID検索wfxPath     +  "用紙ID.tbx" }

/*▼画像データ */
var 文字列{ &iconPng1        = &用紙ID検索wfxPath       + "png\seo.png" }

/*▼ライブラリ */
var 数値  { &OPnum = 0  , &openF = 0 }
var 文字列 { &mode = "共有更新" }
var 文字列  { &Lib = &用紙ID検索wfxPath + "..\..\System\Lib\Lib_ライブラリ桐処理.cmx" }
ライブラリ &Lib

*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　索引か登録かどちらかなのかフラグ。　0　索引　1登録
var 数値  { &登録boot = 0 } 

*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　外部参照
*■*:
*■*:　共有ファイルから、サイズIDを使ってサイズ名称を取ってきます。
var 文字列{ &外部tbxPath = &用紙ID検索wfxPath + "..\..\System\tbx\" }
var 文字列{ &用紙サイズtbx = &外部tbxPath + "04_01_用紙サイズリスト.tbx" }

*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　/*▼モーダルフォーム */
var 文字列{ &銘柄選択modal    = &用紙ID検索wfxPath       + "\modal\【用紙ID】銘柄選択モーダル.wfx" }
var 文字列{ &サブ銘柄選択modal = &用紙ID検索wfxPath       + "\modal\【用紙ID】SUB銘柄選択モーダル.wfx" }
var 文字列{ &サイズ選択modal   = &用紙ID検索wfxPath       + "\modal\【用紙ID】サイズ選択モーダル.wfx" }
var 文字列{ &目選択modal      = &用紙ID検索wfxPath       + "\modal\【用紙ID】目_選択モーダル.wfx" }
var 文字列{ &重さ選択modal    = &用紙ID検索wfxPath        + "\modal\【用紙ID】重さ選択モーダル.wfx" }

*■*----------------------------------------------------------
*■*-----　基本処理
*■*----------------------------------------------------------
名札 MAIN

*
*■*----------------------------------------------------------
*■*----- フォーム開始終了
*■*----------------------------------------------------------
    手続き定義開始　フォーム::フォーム開始(長整数　&表番号)

    手続き定義終了
    手続き定義開始　フォーム::フォーム終了()
        
        手続き実行　リセット（）

    手続き定義終了
*■*----------------------------------------------------------
*■*----- 手続き艇
*■*----------------------------------------------------------
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　終了処理
手続き定義開始 索引終了処理（）

    var 数値  { &EndCHK = 0 }
    *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
    *■*:　ID参照先無し
    if ( &用紙ID　= 0 )

        &タイトル = "【error】登録されていない用紙IDがセットされています。"
        &表示本文 = "新しく用紙情報を登録しますか？ \n\n　「はい」　→　登録してフォームを閉じます。　\n 「いいえ」　→　そのまま終了します。 \n　「キャンセル」　→　戻って修正します。"　 
        メッセージボックス &タイトル , &表示本文 , アイコン = ? , ボタン指定 = 4 , 制御文字展開 = する , &CHK
        

        /*▼「はい」の時の動作 */
        条件 ( &CHK = 6 ) 手続き実行 新規登録( &用紙ID , &EndCHK ) 

        /*▼「いいえ」の時の動作 */
        条件 ( &CHK = 7 ) &EndCHK　= 0

        /*▼「キャンセル」の時の動作 */
        条件 ( &CHK = 2 ) &EndCHK　= 1

    end

    *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
    *■*:　作業終了
    if( &EndCHK = 0 )

        &Num = &用紙ID
       メソッド呼び出し　@OK_button.実行()

    end 


手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　変数リセット
手続き定義開始 リセット（）

    /*▼変数をリセットします */
    &銘柄S ="" &サブ銘柄S ="" &YTS ="" &サイズ名 =""
    &重さS ="" &サイズn　="" &用紙ID =　0

    メソッド呼び出し @フォーム.変数変更()

手続き定義終了
*■*----------------------------------------------------------
*■*-----　オブジェクト専用手続き
*■*----------------------------------------------------------
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　銘柄更新
手続き定義開始　銘柄選択::ソース値更新()

    *■*:::::::::::::::::::
    *■*:　ID検索
    手続き実行 同IDチェック（）

手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　サブ銘柄
手続き定義開始　サブ銘柄選択::ソース値更新()

    *■*:::::::::::::::::::
    *■*:　ID検索
    手続き実行 同IDチェック（）

手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　サイズ選択
手続き定義開始　サイズ選択::ソース値更新()

    *■*-------------------------------------
    *■*-- 菊半などの用紙名称を取得する
    *■*-------------------------------------
    手続き実行 表オープン（　&用紙サイズtbx , &mode , &OPnum , &openF ）
    編集表 &OPnum

        検索 [作業ID] { &サイズn } , 終了状態 = &CHK         

        if ( &CHK = 1 )
        
            &サイズ名 = [名称]
        end

    条件 ( &openF = 0 ) 終了 表 &OPnum

    *■*-------------------------------------
    *■*:　ID検索
    *■*-------------------------------------
    手続き実行 同IDチェック（）

手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　目のチェック
手続き定義開始　目選択::ソース値更新()

    *■*:::::::::::::::::::
    *■*:　ID検索
    手続き実行 同IDチェック（）

手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　重さ更新
手続き定義開始　重さ選択::ソース値更新()
    
    *■*:::::::::::::::::::
    *■*:　ID検索
    手続き実行 同IDチェック（）

手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　用紙ID
手続き定義開始　用紙ID::ソース値更新()

    手続き実行 IDによる逆引きチェック（）

手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　備考情報更新
*■*:　　中にデータがあれば強制的に情報を更新するようにします。
*■*:　
手続き定義開始　備考::ソース値更新()

    var 数値  { &表num　= 0 }
    var 数値  { &廃番CHKi }

    /*▼データがあれば、行訂正する。IDが0だったら頑張らない▼数値だったら0検知しないぜ！ */
    if ( &用紙ID )

        利用者コード "ADDIDP"
        手続き実行 表オープン（&用紙IDTbx  , &mode , &OPnum , &openF ）
        編集表 &OPnum
        
            検索 [ID] { &用紙ID } , 終了状態　= &廃番CHKi

            行訂正 終了状態 = &廃番CHKi , [備考]　= &備考

            &msgtxt = "備考情報を更新しました。"
            確認　&msgtxt
        
        条件 ( &openF = 0 ) 終了 表 &OPnum
        メソッド呼び出し @フォーム.変数変更()
    end

手続き定義終了
*■*----------------------------------------------------------
*■*-----　ID検索
*■*----------------------------------------------------------
手続き定義開始 同IDチェック（）

    var 数値  { &表num　= 0 }

    手続き実行 表オープン（&用紙IDTbx  , &mode , &OPnum , &openF ）
    編集表 &OPnum

        並べ替え  { [銘柄] 昇順 }
        絞り込み [銘柄] { &銘柄S } , 終了状態 = &CHK
        絞り込み [紙色] { &サブ銘柄S } , 終了状態 = &CHK
        絞り込み [サイズ] { &サイズn } , 終了状態 = &CHK
        絞り込み [目] { &YTS } , 終了状態 = &CHK
        絞り込み [重さ] { &重さS } , 終了状態 = &CHK

        &用紙ID = [ID]
        &廃番CHK = [廃番]
     
    条件 ( &openF = 0 ) 終了 表 &OPnum
    メソッド呼び出し @フォーム.変数変更()

手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　IDによる逆引きチェック
手続き定義開始 IDによる逆引きチェック（）

    var 数値  { &表num　= 0 }

    手続き実行 表オープン（&用紙IDTbx  , &mode , &OPnum , &openF ）
    編集表 &OPnum

        検索 [ID] { &用紙ID } , 終了状態 = &CHK 
            &銘柄S = [銘柄]
            &サブ銘柄S = [紙色]
            &サイズn = [サイズ]
            &YTS = [目]
            &重さS = [重さ]
            &廃番CHK = [廃番]
     
    条件 ( &openF = 0 ) 終了 表 &OPnum
    メソッド呼び出し @フォーム.変数変更()

手続き定義終了
*■*----------------------------------------------------------
*■*-----　廃番登録
*■*----------------------------------------------------------
手続き定義開始 廃棄登録（）

    var 数値  { &表num　= 0 }
    var 数値  { &廃番CHKi }

    利用者コード "ADDIDP"
    
    手続き実行 表オープン（&用紙IDTbx  , &mode , &OPnum , &openF ）
    編集表 &OPnum

        /*▼用紙IDがきちんと入っているか確認 */
        if( &用紙ID > 0 )

            検索 [ID] { &用紙ID } , 終了状態　= &廃番CHKi

            行訂正 終了状態 = &廃番CHKi , [廃番]　= &廃番CHK　, [廃番設定日]　= #日時値 

            &msgtxt = "廃番設定を更新しました。"
            確認　&msgtxt
        
        else

            &msgtxt = "用紙IDが無いので、廃番設定できません。"
            確認　&msgtxt
        
        end

    条件 ( &openF = 0 ) 終了 表 &OPnum

手続き定義終了
*■***********************************************************
*■*** データ無しなら新規登録しちゃう
*■***　用紙IDが0なら現状を新規登録するんだぜ
*■***********************************************************
手続き定義開始 新規登録（ 参照　数値　&新規ID , 参照 数値 &完了 ）

    var 数値  { &表ID }
    var 数値  { &OPnum = 0  , &openF = 0 }
    var 文字列 { &pass = "ADDIDP" }

    利用者コード &pass
    手続き実行 表オープン（&用紙IDTbx  , &mode , &OPnum , &openF ）
    編集表 &OPnum

    /*▼加工処理 */
    &新規ID = #総件数　+ 1

    &銘柄S = #全角( &銘柄S )
    &サブ銘柄S = #全角( &サブ銘柄S )
    &YTS = #全角( &YTS )

    &完了 = 0 /*←終了判定＿0正常＿１異常　 */

    /*▼エラー処理 */
    if ( &銘柄S = "" )

        &msgtxt = "銘柄名が未定義のため登録できません。"
        確認 &msgtxt
        &完了 = 1
    end
    *■*:::::::::::::::::::::::::::::::::::::::::
    if ( &サイズn = "" )

        &msgtxt = "サイズが未定義のため登録できません。"
        確認 &msgtxt
        &完了 = 1
    end
    *■*:::::::::::::::::::::::::::::::::::::::::
    if ( &YTS = "" )

        &msgtxt = "縦・ヨコが未定義のため登録できません。"
        確認 &msgtxt
        &完了 = 1
    end
    *■*:::::::::::::::::::::::::::::::::::::::::
    if ( &重さS = "" )

        &msgtxt = "重さが未定義のため登録できません。"
        確認 &msgtxt
        &完了 = 1
    end

    if ( &完了 = 0 )


        行追加 終了状態 = &CHK , [ID] = &新規ID , [銘柄] = &銘柄S , [紙色] = &サブ銘柄S , [目] = &YTS , [サイズ] = &サイズn ,\
        [重さ] = &重さS , [登録日] = #日時値 , [サイズ名目] = &サイズ名 , [備考] = &備考


        var 文字列{ &けつごうめい }
    
        &けつごうめい = &銘柄S +"-"+&サブ銘柄S+"-"+#文字列( &サイズn )+"-"+&YTS+"-"+#文字列( &重さS ) +"として登録"

        &msgtxt = #文字列( &新規ID ) +"　： "+&けつごうめい
        確認 &msgtxt

    end

    利用者コード　""
    条件 ( &openF = 0 ) 終了 表 &OPnum

手続き定義終了