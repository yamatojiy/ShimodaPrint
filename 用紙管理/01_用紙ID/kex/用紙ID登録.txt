*■***********************************************************
*■*** 
*■*** 用紙IDを登録するフォームです。
*■*** 　引用が可能にします。　重複確認を先にします。　最後登録させます。
*■*** 
*■***********************************************************
/*▼汎用 */
var 数値  { &CHK ,　&CHK2　,　&CHK3 }
var 文字列{ &msgtxt }

/*▼戻り用変数 */
var 数値  { &Num = "" }
var 文字列{ &登録用文言 = "" }
var 数値  { &登録NG =  0 }

/*▼案件用変数 */
var 文字列{ &銘柄S , &サブ銘柄S , &YTS , &サイズ名 }
var 数値  { &重さS , &サイズn , &用紙ID = 0 }
var 文字列　　{ &廃番CHK = "" }
var 日時 { &廃番日 = "" }
var 文字列{ &備考 = "" }
    var 文字列{ &重さ名 = "" }

/*▼動作✓用配列 */
var 数値  { &bt[4] ={""} }

/*▼ファイルパス */
var 文字列{ &用紙ID検索wfxPath = #データパス名 }
var 文字列{ &用紙IDTbx        =  &用紙ID検索wfxPath     +  "用紙ID.tbx" }

/*▼画像データ */
var 文字列{ &iconPng2        = &用紙ID検索wfxPath       + "png\in.png" }


*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　索引か登録かどちらかなのかフラグ。　0　索引　1登録
var 数値  { &登録boot = 1 } 

*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　外部参照
*■*:
*■*:　共有ファイルから、サイズIDを使ってサイズ名称を取ってきます。
var 文字列{ &外部tbxPath = &用紙ID検索wfxPath + "..\..\System\tbx\" }
var 文字列{ &用紙サイズtbx = &外部tbxPath + "04_01_用紙サイズリスト.tbx" }
var 文字列{ &用紙サイズmodal = &外部tbxPath + "..\modal\General\004_用紙_04_01_用紙サイズリスト_1_(基本).wfx" }

*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　/*▼モーダルフォーム */
var 文字列{ &銘柄選択modal    = &用紙ID検索wfxPath       + "..\system\modal\【用紙ID】銘柄選択モーダル.wfx" }
var 文字列{ &サブ銘柄選択modal = &用紙ID検索wfxPath       + "..\system\modal\【用紙ID】SUB銘柄選択モーダル.wfx" }
var 文字列{ &サイズ選択modal   = &用紙ID検索wfxPath       + "..\system\modal\【用紙ID】サイズ選択モーダル.wfx" }
var 文字列{ &目選択modal      = &用紙ID検索wfxPath       + "..\system\modal\【用紙ID】目_選択モーダル.wfx" }
var 文字列{ &重さ選択modal    = &用紙ID検索wfxPath        + "..\system\modal\【用紙ID】重さ選択モーダル.wfx" }

*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　/*▼一括処理 */
var 文字列{ &廃番登録cmx     = &用紙ID検索wfxPath        + "cmx\用紙管理_廃番登録.cmx" }

*■*----------------------------------------------------------
*■*-----　基本処理
*■*----------------------------------------------------------
名札 MAIN

*
*■*----------------------------------------------------------
*■*----- フォーム専用
*■*----------------------------------------------------------
手続き定義開始　フォーム::フォーム開始(長整数　&表番号)

手続き定義終了
手続き定義開始　フォーム::フォーム終了()

    手続き実行　リセット（）

手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　テーブルID調査
*■*:　ターゲットのテーブルが開いてても開いて無くても表番号を返す関数
*■*:　引数１：ターゲットのテーブルアドレス
*■*:　引数２：新規で開くステータス（専有とか参照とか、すでに開いている場合はそのまま）
*■*:　戻り値：表のＩＤ
    手続き定義開始 テーブルID調査（ 文字列 &tbx ,文字列　&モード　, 参照　数値　&表ID ）

        *■*-------------------------------------
        *■*--  編集表番号取得（開いてても問題無し
        *■*-------------------------------------
        &表ID = #表番号取得( &tbx )
        if ( .not &表ID ) /*←未定義なら */
            
            表 &tbx , モード = &モード , 終了状態 = &CHK 
            &表ID = #表番号取得( &tbx )　　

        end

    手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　選択内容を行追加する手続き処理
手続き定義開始 新規用紙ID挿入（ 参照　数値　&用紙ID　）

    var 数値  { &AddID = 0 , &入力check = 0　}
    var 数値  { &表num　= 0 }
    var 文字列{ &msgtx = "" }

    利用者コード "ADDIDP"
    手続き実行 テーブルID調査 ( &用紙IDTbx , "専有" , &表num )
    編集表　&表num

    *■*-------------------------------------
    *■*-- 必須項目チェック
    *■*-------------------------------------
    手続き実行 入力チェック ( &銘柄S , &bt[1] )
    手続き実行 入力チェック ( &サイズ名 , &bt[2] )
    手続き実行 入力チェック ( &YTS , &bt[3] )
    手続き実行 入力チェック ( &重さ名 , &bt[4] )

    &入力check = #配列合計("bt")

    if ( &入力check > 3 )

        if ( &用紙ID )

            *■*-------------------------------------
            *■*-- 用紙IDが既に定着している場合は重複しているため、登録は行わない。
            *■*-------------------------------------
            &登録用文言　= "既に用紙IDに登録されている情報です！"
            確認 &登録用文言

        else
        
            /*▼IDを新規作成するよ */
            &AddID = #総件数 + 1

            行追加 終了状態 = &CHK ,[ID] = &AddID , [銘柄] = &銘柄S , [紙色] = &サブ銘柄S , [サイズ] = &サイズn , \
                [目] = &YTS , [重さ] = &重さS , [登録日] = #日時値　, [サイズ名目] = &サイズ名
            
            表整理 余白割合 = 0
        
            if ( &CHK = 1 )

                &msgtx = "ID:" + #文字列( &AddID ) + " :" + &銘柄S +"-"+ &サブ銘柄S +"-"+ #文字列( &サイズn ) +"-"+ &YTS +"-"+ #文字列( &重さS ) +"　を登録しました。"
                確認　&msgtx

                &用紙ID = &AddID

            end

        end

    else

        &登録用文言　= "登録に必要な情報が足りません。赤枠の情報を埋めてください。"
        確認 &登録用文言

    end

    終了 表 &表num

    メソッド呼び出し @フォーム.変数変更()

手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　変数チェック
手続き定義開始 入力チェック（ 文字列　&hen　, 参照　数値　&bt ）

    if ( &hen )
        &bt = 1
    else
        &bt = 0
    end

手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　変数リセット
手続き定義開始 リセット（）

    /*▼変数をリセットします */
    &銘柄S ="" &サブ銘柄S ="" &YTS ="" &サイズ名 =""
    &重さS ="" &サイズn　="" &用紙ID ="" &登録NG =  0

    メソッド呼び出し @フォーム.変数変更()

手続き定義終了
*■*----------------------------------------------------------
*■*-----　オブジェクト専用手続き
*■*----------------------------------------------------------
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　銘柄更新 1
手続き定義開始　銘柄選択::ソース値更新()

    *■*------------------
    *■*-- 確認手続き
    手続き実行 同IDチェック（）

手続き定義終了

*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　サブ銘柄更新
手続き定義開始　サブ銘柄選択::ソース値更新()

    *■*------------------
    *■*-- 確認手続き
    手続き実行 同IDチェック（）

手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　サイズ更新 2
手続き定義開始　サイズ選択::ソース値更新()

    var 数値  { &表num　= 0 }

    手続き実行 テーブルID調査 ( &用紙サイズtbx , "共有参照" , &表num )
    編集表　&表num

        検索 [ID] { &サイズn } , 終了状態 = &CHK         

        if ( &CHK = 1 )
        
            &サイズ名 = [サイズ名]
        end

    終了 表 &表num

    *■*------------------
    *■*-- 確認手続き
    手続き実行 同IDチェック（）


手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　目更新
手続き定義開始　目選択::ソース値更新()

    *■*------------------
    *■*-- 確認手続き
    手続き実行 同IDチェック（）
    

手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　重さ更新
手続き定義開始　重さ選択::ソース値更新()

    *■*------------------
    *■*-- 確認手続き
    手続き実行 同IDチェック（）
    &重さ名 = #文字列( &重さS )

手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　用紙ID
手続き定義開始　用紙ID::ソース値更新()

    手続き実行 IDによる逆引きチェック（）

手続き定義終了
*■***********************************************************
*■***　同じ情報になっていないかを常にチェックする
*■***********************************************************
手続き定義開始 同IDチェック（）

    var 数値  { &表num　= 0 }
    手続き実行 テーブルID調査 ( &用紙IDTbx , "専有" , &表num )
    編集表　&表num

        並べ替え  { [銘柄] 昇順 }
        絞り込み [銘柄] { &銘柄S } , 終了状態 = &CHK
        絞り込み [紙色] { &サブ銘柄S } , 終了状態 = &CHK
        絞り込み [サイズ] { &サイズn } , 終了状態 = &CHK
        絞り込み [目] { &YTS } , 終了状態 = &CHK
        絞り込み [重さ] { &重さS } , 終了状態 = &CHK

        &用紙ID = [ID]
     
    終了 表 &表num

    メソッド呼び出し @フォーム.変数変更()

手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　備考情報更新
*■*:　　中にデータがあれば強制的に情報を更新するようにします。
*■*:　
手続き定義開始　備考::ソース値更新()

    var 数値  { &表num　= 0 }
    var 数値  { &廃番CHKi }

    /*▼データがあれば、行訂正する。IDが0だったら頑張らない▼数値だったら0検知しないぜ！ */
    if ( &用紙ID )

        利用者コード "ADDIDP"
        手続き実行 テーブルID調査 ( &用紙IDTbx , "専有" , &表num )
        編集表　&表num

            検索 [ID] { &用紙ID } , 終了状態　= &廃番CHKi

            行訂正 終了状態 = &廃番CHKi , [備考]　= &備考

            &msgtxt = "備考情報を更新しました。"
            確認　&msgtxt
        
        終了 表 &表num
        メソッド呼び出し @フォーム.変数変更()
    end

手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　IDによる逆引きチェック
手続き定義開始 IDによる逆引きチェック（）

    var 数値  { &表num　= 0 }

    手続き実行 テーブルID調査 ( &用紙IDTbx , "共有参照" , &表num )
    編集表　&表num

        検索 [ID] { &用紙ID } , 終了状態 = &CHK

            if ( &CHK = 1 )
            
                &銘柄S = [銘柄]
                &サブ銘柄S = [紙色]
                &サイズn = [サイズ]
                &YTS = [目]
                &重さS = [重さ]
                &廃番CHK = [廃番]

                &重さ名 = #文字列( &重さS )
            
                &登録用文言　= "既に用紙IDに登録されている情報です！"

                var 数値  { &表num2　= 0 }
                手続き実行 テーブルID調査 ( &用紙サイズtbx , "共有参照" , &表num2 )
                編集表　&表num2
                    検索 [ID] { &サイズn } , 終了状態 = &CHK         
                    条件 ( &CHK = 1 ) &サイズ名 = [サイズ名]
                終了 表 &表num2
        
            end

    終了 表 &表num
    メソッド呼び出し @フォーム.変数変更()

手続き定義終了