*■***********************************************************
*■*** 
*■*** 用紙IDを登録するフォームです。
*■*** 　引用が可能にします。　重複確認を先にします。　最後登録させます。
*■*** 
*■***********************************************************
/*▼汎用 */
var 数値  { &CHK ,　&CHK2　,　&CHK3 }
var 文字列{ &msgtxt }

/*▼戻り用変数 */
var 数値  { &Num = "" }
var 文字列{ &登録用文言 = "" }
var 数値  { &登録NG =  0 }

/*▼案件用変数 */
var 文字列{ &銘柄S , &サブ銘柄S , &YTS , &サイズ名 }
var 数値  { &重さS , &サイズn , &用紙ID = 0 }


/*▼ファイルパス */
var 文字列{ &用紙ID検索wfxPath = #データパス名 }
var 文字列{ &用紙IDTbx        =  &用紙ID検索wfxPath     +  "用紙ID.tbx" }

/*▼画像データ */
var 文字列{ &iconPng2        = &用紙ID検索wfxPath       + "png\in.png" }


*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　索引か登録かどちらかなのかフラグ。　0　索引　1登録
var 数値  { &登録boot = 1 } 

*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　外部参照
*■*:
*■*:　共有ファイルから、サイズIDを使ってサイズ名称を取ってきます。
var 文字列{ &外部tbxPath = &用紙ID検索wfxPath + "..\..\System\tbx\" }
var 文字列{ &用紙サイズtbx = &外部tbxPath + "04_01_用紙サイズリスト.tbx" }
var 文字列{ &用紙サイズmodal = &外部tbxPath + "..\modal\General\004_用紙_04_01_用紙サイズリスト_1_(基本).wfx" }

*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　/*▼モーダルフォーム */
var 文字列{ &銘柄選択modal    = &用紙ID検索wfxPath       + "..\system\modal\【用紙ID】銘柄選択モーダル.wfx" }
var 文字列{ &サブ銘柄選択modal = &用紙ID検索wfxPath       + "..\system\modal\【用紙ID】SUB銘柄選択モーダル.wfx" }
var 文字列{ &サイズ選択modal   = &用紙ID検索wfxPath       + "..\system\modal\【用紙ID】サイズ選択モーダル.wfx" }
var 文字列{ &目選択modal      = &用紙ID検索wfxPath       + "..\system\modal\【用紙ID】目_選択モーダル.wfx" }
var 文字列{ &重さ選択modal    = &用紙ID検索wfxPath        + "..\system\modal\【用紙ID】重さ選択モーダル.wfx" }
*■*----------------------------------------------------------
*■*-----　基本処理
*■*----------------------------------------------------------
名札 MAIN

*
*■*----------------------------------------------------------
*■*----- フォーム専用
*■*----------------------------------------------------------
手続き定義開始　フォーム::フォーム開始(長整数　&表番号)

手続き定義終了
手続き定義開始　フォーム::フォーム終了()

    if ( &用紙ID )
    
        確認　"既に登録済みの内容なので、そのまま終了"
    
    else

        手続き実行 新規用紙ID挿入（）
        
    end

    手続き実行　リセット（）

手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　選択内容を行追加する手続き処理
手続き定義開始 新規用紙ID挿入（）

    var 数値  { &AddID = 0 }
    var 数値  { &表num　= 0 }
    var 文字列{ &msgtx = "" }

    利用者コード "ADDIDP"

    表 &用紙IDTbx  , モード = 専有
    &表num = #表番号取得( &用紙IDTbx )
    編集表　&表num
    　　
        /*▼IDを新規作成するよ */
        &AddID = #総件数 + 1

        行追加 終了状態 = &CHK ,[ID] = &AddID , [銘柄] = &銘柄S , [紙色] = &サブ銘柄S , [サイズ] = &サイズn , \
            [目] = &YTS , [重さ] = &重さS , [登録日] = #日時値　, [サイズ名目] = &サイズ名
    
    if ( &CHK = 1 )

        &msgtx = "ID:" + #文字列( &AddID ) + " :" + &銘柄S +"-"+ &サブ銘柄S +"-"+ #文字列( &サイズn ) +"-"+ &YTS +"-"+ #文字列( &重さS ) +"　を登録しました。"

        確認　&msgtx

    end

    終了 表 &表num

手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　変数リセット
手続き定義開始 リセット（）

    /*▼変数をリセットします */
    &銘柄S ="" &サブ銘柄S ="" &YTS ="" &サイズ名 =""
    &重さS ="" &サイズn　="" &用紙ID ="" &登録NG =  0

    メソッド呼び出し @フォーム.変数変更()

手続き定義終了
*■*----------------------------------------------------------
*■*-----　オブジェクト専用手続き
*■*----------------------------------------------------------
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　銘柄更新
手続き定義開始　銘柄選択::ソース値更新()

    *■*-------------------------------------
    *■*-- 確認手続き
    *■*-------------------------------------
    手続き実行 同IDチェック（）

手続き定義終了

*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　サブ銘柄更新
手続き定義開始　サブ銘柄選択::ソース値更新()

    *■*-------------------------------------
    *■*-- 確認手続き
    *■*-------------------------------------
    手続き実行 同IDチェック（）


手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　サイズ更新
手続き定義開始　サイズ選択::ソース値更新()

    var 数値  { &表num　= 0 }

    表 &用紙サイズtbx , モード = 共有参照
    &表num = #表番号取得( &用紙サイズtbx )

        検索 [ID] { &サイズn } , 終了状態 = &CHK         

        if ( &CHK = 1 )
        
            &サイズ名 = [サイズ名]
        end

    終了 表 &表num

    *■*-------------------------------------
    *■*-- 確認手続き
    *■*-------------------------------------
    手続き実行 同IDチェック（）

手続き定義終了

*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　目更新
手続き定義開始　目選択::ソース値更新()

    *■*-------------------------------------
    *■*-- 確認手続き
    *■*-------------------------------------
    手続き実行 同IDチェック（）

手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　重さ更新
手続き定義開始　重さ選択::ソース値更新()

    *■*-------------------------------------
    *■*-- 確認手続き
    *■*-------------------------------------
    手続き実行 同IDチェック（）

手続き定義終了
*■***********************************************************
*■***　同じ情報になっていないかを常にチェックする
*■***********************************************************
手続き定義開始 同IDチェック（）

    var 数値  { &表num　= 0 }

    表 &用紙IDTbx  , モード = 共有参照
    &表num = #表番号取得( &用紙IDTbx )

        並べ替え  { [銘柄] 昇順 }
        絞り込み [銘柄] { &銘柄S } , 終了状態 = &CHK
        絞り込み [紙色] { &サブ銘柄S } , 終了状態 = &CHK
        絞り込み [サイズ] { &サイズn } , 終了状態 = &CHK
        絞り込み [目] { &YTS } , 終了状態 = &CHK
        絞り込み [重さ] { &重さS } , 終了状態 = &CHK

        &用紙ID = [ID]
     
    終了 表 &表num

    *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
    *■*:　用紙IDに1以上の実数が入っていたらNG　、　0だったら新規登録OK
    if( &用紙ID > 0 )

        &登録用文言　= "既に用紙IDに登録されている情報です！"
        &登録NG = 1 

    else
   
        &登録用文言　= ""
        &登録NG = 0

    end

    メソッド呼び出し @フォーム.変数変更()


手続き定義終了