*■***********************************************************
*■*** 用紙IDに合わせて、全てのテーブル情報を最新に更新する。
*■***
*■***　03.入庫
*■***　04.資材断裁
*■***　06.在庫登録
*■***
*■***
*■***********************************************************

var 数値  { &CHK , &r = 1 }
var 数値  { &Task = 3 }
var 文字列 { &mode = ”専有” }
var 文字列 { &mode2 = ”共有参照” }

var 文字列{ &タイトル , &表示本文　, &msgtxt }

/*▼パス関連 */
var 文字列{ &Datapath = #一括パス名 }

/*▼用紙ID */
var 文字列{ &用紙IDtbx = &Datapath +"..\用紙ID.tbx" }
var 数値  { &用紙IDnum , &用紙IDopenF }

/*▼各作業テーブル */
var 文字列{ &TaskPath[&Task] ={""} }
&TaskPath[1] = &Datapath +"..\..\03_用紙入庫\用紙入庫.tbx"
&TaskPath[2] = &Datapath +"..\..\04_資材断裁\資材断裁.tbx"
&TaskPath[3] = &Datapath +"..\..\06_在庫登録\在庫登録.tbx"
var 数値  { &OPnum[&Task] ={""} , &openF[&Task] ={""} }

/*▼代入コマンド */
var 文字列{ &InputCMD[&Task] ={""} }
&InputCMD[1] = "行訂正 終了状態 = &CHK , [銘柄] = &ns銘柄 , [紙色] = &nsサブ銘柄 , [サイズID] = &nsサイズID , [サイズ名] = &nsサイズ名 , [目] = &ns目 , [重さ] = &ns重さ"
&InputCMD[2] = "行訂正 終了状態 = &CHK , [銘柄] = &ns銘柄 , [サブ銘柄] = &nsサブ銘柄 , [サイズID] = &nsサイズID , [サイズ名] = &nsサイズ名 , [目] = &ns目 , [重さ] = &ns重さ"
&InputCMD[3] = "行訂正 終了状態 = &CHK , [銘柄] = &ns銘柄 , [紙色] = &nsサブ銘柄 , [サイズID] = &nsサイズID , [サイズ名] = &nsサイズ名 , [目] = &ns目 , [重さ] = &ns重さ"


/*▼作業変数 */
var 文字列{ &ns銘柄 , &nsサブ銘柄 , &nsサイズ名 , &ns目 }
var 数値  { &ID , &nsサイズID ,&nsサイズ詳細ID　, &ns重さ }


*■*----------------------------------------------------------
*■*----- ライブラリ
*■*----------------------------------------------------------
var 文字列  { &Lib = &Datapath  + "..\..\..\System\Lib\Lib_ライブラリ桐処理.cmx" }
ライブラリ &Lib


名札 MAIN

    &タイトル = "確認"
    &表示本文 = " 全てのテーブルを閉じて、用紙に関するtbxを専有で開きます。\n 準備が出来たらＯＫをおしてください。"
    メッセージボックス &タイトル , &表示本文 , アイコン = ！ , ボタン指定 = 2 , 制御文字展開 = する , &CHK
    if ( &CHK = 2 )
        中止
    end

    表　/*▼全ての表を一旦閉じます */
    *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
    *■*:用紙IDtbxの表numを格納
    手続き実行 表オープン（ &用紙IDtbx , &mode2 , &用紙IDnum , &用紙IDopenF ）
    

    繰り返し（ &r ≦ &Task ）
    
        *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
        *■*:　該当ファイルを開いて
        *■*--　用紙情報を変数に格納

        手続き実行 表オープン（ &TaskPath[&r] , &mode , &OPnum[&r] , &openF[&r] ）
        編集表 &OPnum[&r]

            繰り返し（ .NOT #終端行 ）
                &ID = [用紙ID]

                *■*-------------------------------------
                *■*--　用紙IDから調べる
                *■*-------------------------------------
                編集表 &用紙IDnum

                    検索 [ID] { &ID } , 終了状態 = &CHK 
                    if ( &CHK = 1 )
                
                        &ns銘柄            = [銘柄]
                        &nsサブ銘柄         = [紙色]
                        &nsサイズID         =　[サイズ]
                        &nsサイズ詳細ID     =　[サイズ詳細ID]
                        &nsサイズ名         =　[サイズ名目]
                        &ns目               = [目]
                        &ns重さ             =　[重さ]
                    end

                *■*-------------------------------------
                *■*--　戻って格納する
                *■*-------------------------------------
                編集表 &OPnum[&r]

                    コマンド　&InputCMD[&r]


                *■*-------------------------------------
                *■*--　変数リセットして次へ
                *■*-------------------------------------
                &ns銘柄 = ""  &nsサブ銘柄 = "" &nsサイズID =　"" &nsサイズ詳細ID =　"" &nsサイズ名 =　"" &ns目 = "" &ns重さ =　""
                ジャンプ 行番号 = 次行 
            繰り返し終了 

        条件 ( &openF[&r] = 0 ) 終了 表 &OPnum[&r] 
        &r = &r + 1 
    繰り返し終了

    &msgtxt　="Done"
    確認 &msgtxt

    表

終了