*■***********************************************************
*■*** 用紙管理フォーム用のヘッダーファイルです。
*■*** 結構管理状況を設定するのが大変なのでここいらで環境を整理する。
*■***********************************************************
/*▼汎用 */
var 数値  { &CHK ,　&CHK2　,　&CHK3 , &hit , &i , &用紙管理wfxId }
var 文字列{ &msgtxt }

/*▼案件用変数 */
var 数値  { &A伝票番号　,　&A前回伝票番号 , &A部数 , &max = 10 }
var 文字列{ &A得意先 , &A品名 , &Aサイズ , &A頁数　, &A仕上時間 }
var 日時  { &A前回日付 , &A仕上日 }
var 文字列{ &工程[&max] = {""} }/*←工程情報を配列 */

/*▼ファイルパス */
var 文字列{ &用紙DataPath = #データパス名 }
var 文字列{ &用紙subTbx  = &用紙DataPath + "..\..\..\submaster.tbx"}
var 文字列{ &用紙工程管理Tbx  = &用紙DataPath + "..\..\..\工程管理\kotei_MASTER.TBX"}

var 文字列{ &用紙案件情報tbx =  &用紙DataPath + "..\tbx\用紙案件情報.tbx" }
var 文字列{ &用紙項目情報tbx =  &用紙DataPath + "..\tbx\用紙項目情報.tbx" }
*■*----------------------------------------------------------
*■*-----　基本処理
*■*----------------------------------------------------------
名札 MAIN

*
*■*----------------------------------------------------------
*■*----- 手続き
*■*----------------------------------------------------------
手続き定義開始　フォーム::フォーム開始(長整数　&表番号)

    &用紙管理wfxId = &表番号

    *■*-------------------------------------
    *■*-- オープンと同時に裏で起動させておく表
    *■*-------------------------------------



手続き定義終了
手続き定義開始　フォーム::フォーム終了()

    *■*-------------------------------------
    *■*-- 使い終わった表を閉じる
    *■*-------------------------------------



手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　案件更新
手続き定義開始　伝01h::ソース値更新()

    表 &用紙工程管理Tbx  , 表番号 = 15 , モード = 共有更新　終了状態 = &CHK
    編集表 15 

        ジャンプ 行番号 = 1
        検索 [伝票ＮＯ] { &A伝票番号 } , 終了状態 = &CHK2

        if ( &CHK2 = 1 )

            &A前回伝票番号 = [前回伝票No]
            &A得意先      = [得意先]
            &A品名        = [品名]

            *■*::::::::::::::::::::::::::::
            *■*:　仮数値確認。確定があればそっちを優先
            手続き実行 確定調査【数値】　(　[部数] , [確定部数] , &A部数 )
            手続き実行 確定調査【文字列】　(　[頁数] , [確定頁数] , &A頁数 )
            手続き実行 確定調査【文字列】　(　[サイズ] , [確定サイズ] , &Aサイズ )
            手続き実行 確定調査【日時】　(　[仕上日] , [納品日] , &A仕上日 )
            手続き実行 確定調査【文字列】　(　[仕上日１] , [納品日１] , &A仕上時間 )

            *■*-------------------------------------
            *■*--　前回様に再度検索
            *■*-------------------------------------
            ジャンプ 行番号 = 1
            検索 [伝票ＮＯ] { &A前回伝票番号 } , 終了状態 = &CHK3
            if ( &CHK3 = 1 )
                手続き実行 確定調査【日時】　(　[仕上日] , [納品日] , &A前回日付 )
            end
        
        end
         
    終了 表 15
    編集表 &用紙管理wfxId

    メソッド呼び出し @フォーム.変数変更()

手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　伝票案件登録
手続き定義開始 工程情報作成（）

    手続き実行 工程情報を読み込み（　&A伝票番号 ）
    手続き実行 項目登録（）

    再抽出 変数使用 = する , 終了状態 = &CHK
    
手続き定義終了
*■***********************************************************
*■***　汎用　手続きコード
*■***********************************************************
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　変数を識別しやすいようにするため、あえて数字にしております。
手続き定義開始 確定調査【数値】（　数値 &数字　, 数値 &確定数字 , 参照 数値　&戻り数字 ）

    if ( &確定数字 )
        &戻り数字 = &確定数字
    else
        &戻り数字　= &数字
    end

手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　変数は日付としています
手続き定義開始 確定調査【日時】（　日時 &日付Day　, 日時 &確定日付 , 参照 日時　&戻り日付 ）

    if ( &確定日付 )
        &戻り日付 = &確定日付
    else
        &戻り日付　= &日付Day
    end

手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　文字列Ver
手続き定義開始 確定調査【文字列】（　文字列 &文字　, 文字列 &確定文字 , 参照 文字列　&戻り文字 ）

    if ( &確定文字 )
        &戻り文字 = &確定文字
    else
        &戻り文字　= &文字
    end

手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　案件の変数リセット
手続き定義開始 案件関連変数リセット（）
    &A伝票番号　=""　&A前回伝票番号 ="" &A部数 =""
    &A得意先 ="" &A品名 ="" &Aサイズ ="" &A頁数　="" &A仕上時間 =""
    &A前回日付 ="" &A仕上日 =""

手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　案件情報を登録
*■*:　
手続き定義開始 案件の登録処理（）

    var 数値  { &doId }

    表 &用紙案件情報tbx  , 表番号 = 16 , モード = 共有更新

        検索 [伝票番号] {　&A伝票番号 } , 終了状態 = &hit
        if ( &hit <> 1 )

            *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
            *■*:　kakunin
            メッセージボックス "確認" , "案件登録されていません。新規登録を行いますか？" , アイコン = ? , ボタン指定 = 5 , 制御文字展開 = しない , &doId
            
            if ( &doId = 6 )
                行追加 終了状態 = &CHK , [伝票番号] = &A伝票番号 , [前回伝票番号] = &A前回伝票番号 , [前回日付] = &A前回日付 ,\
                [得意先] = &A得意先 , [品名] = &A品名 , [部数] = &A部数 , [サイズ] = &Aサイズ

            end
        end
     
    終了 表 16

手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　工程情報を読み込み変数に格納
*■*:　伝票番号を調査して、工程情報を変数に格納します。　
手続き定義開始 工程情報を読み込み（ 数値 &伝票ナンバー ）

    表 &用紙工程管理Tbx , 表番号 = 15 , モード = 共有更新

        &工程[1] = [内容１]
        &工程[2] = [内容２]
        &工程[3] = [内容３]
        &工程[4] = [内容４]
        &工程[5] = [内容５]
        &工程[6] = [内容６]
        &工程[7] = [内容７]
        &工程[8] = [内容８]
        &工程[9] = [内容９]
        &工程[10] = [内容10]
    
    終了 表 15

手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　項目情報tbx　に項目ごと案件を登録する処理
手続き定義開始 項目登録（）

    var 数値  { &r = 1 }

    表 &用紙項目情報tbx  , 表番号 = 17 , モード = 共有更新

        /*▼伝票番号にちゃんと番号が入っているか確認 */
        if (　&A伝票番号　)

            繰り返し（ &r < &max ）
            
                条件 ( &工程[&r] = "" ) 繰り返し中止

                行追加 終了状態 = &CHK , [伝票番号] = &A伝票番号 , [工程] = &工程[&r] , [工程順] = 1
            
            &r = &r + 1 
            繰り返し終了         

        end


     
    終了 表 17

手続き定義終了

