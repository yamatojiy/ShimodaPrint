*■***********************************************************
*■*** 
*■*** 用紙の入力情報をまとめる入力フォームです
*■*** 今まで入庫と納品を別フォームにしていましたが、今回１つにまとめます。
*■*** 
*■***********************************************************
    /*▼汎用 */
    var 数値  { &CHK }
    var 文字列{ &msgtxt , &タイトル　,　&表示本文 }
    var 数値  { &入庫TbxNum = 0 , &ns発注ID = 0 }

    /*▼案件用変数 */
    var 日時  { &days }
    var 文字列{ &担当者 }

    /*▼伝票関連 */
    var 数値  { &伝票ID , &ns部数 }
    var 文字列{ &ns得意先 , &ns品名 }

    /*▼用紙情報 */
    var 文字列{ &ns銘柄 , &nsサブ銘柄 , &nsサイズ名 , &ns目 }
    var 数値  { &nsサイズID , &ns重さ }

    /*▼用紙情報 */
    var 数値  { &ns単価 }
    var 文字列{ &ns単価種 }

    /*▼表開き権限 */
    var 文字列{ &共有参照 = "共有参照" }
    var 文字列{ &専有 　　　= "専有" , &mode = "共有更新" }
    var 数値  { &在庫登録表 = "" }

    /*▼担当者情報 */
    var 数値  { &社員ID }
    var 文字列{ &担当者氏名 , &役職 , &携帯番号 , &社員略称 }

    /*▼仕入先絞り込み */
    var 文字列{ &List仕入先 }

    /*▼パレット用変数 */
    var 日時  { &P入力日 }
    var 数値  { &P用紙ID , &PサイズID , &P重さ , &P枚数 }
    var 文字列{ &P銘柄 , &P紙色 , &Pサイズ名 , &P目 }

    
*■*-------------------------------------
*■*--  各パス関連
*■*-------------------------------------
    /*▼ファイルパス */
    var 文字列{ &用紙入庫DPath = #データパス名 }

    /*▼画像データ */
    var 文字列{ &iconPng = &用紙入庫DPath + "png\cut.png" }

    *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
    *■*:　モーダルフォーム

    /*▼モーダルフォーム用変数 */
    var 数値  { &Num }
    var 文字列{ &Str }
    var 日時  { &Day }

    /*▼カレンダー */
    var 文字列{ &nSカレンダーmodal = &用紙入庫DPath + "..\..\System\modal\Calendar\INF_DatePicker.wfx" }

    /*▼電卓 */
    var 文字列{ &nS電卓modal = &用紙入庫DPath + "..\..\System\modal\Calculator\INF_電卓.wfx" }

    /*▼用紙仕入先選択 */
    var 文字列{ &nS仕入会社modal = &用紙入庫DPath + "..\..\System\modal\General\004_用紙_08_01_製紙会社リスト_1.wfx" }

    /*▼単価種リスト */
    var 文字列{ &nS単価種modal = &用紙入庫DPath + "..\..\System\modal\General\004_用紙_02_08_単価種_1.wfx" }

    /*▼社員リスト */
    var 文字列{ &nS社員リストmodal = &用紙入庫DPath + "..\..\System\modal\General\004_用紙_03_01_シモダ社員情報_1_(用紙発注者).wfx" }

    /*▼用紙索引 */
    var 文字列{ &S用紙IDmodal = &用紙入庫DPath + "..\01_用紙ID\用紙ID索引フォーム.wfx"  }

    /*▼用紙登録 */
    var 文字列{ &S用紙登録modal = &用紙入庫DPath + "..\01_用紙ID\用紙ID登録フォーム.wfx"  }

    *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
    *■*:　外部参照
    *■*:
    *■*:　共有ファイルから、サイズIDを使ってサイズ名称を取ってきます。

    /*▼用紙ID */
    var 文字列{ &用紙IDTbx = &用紙入庫DPath + "..\01_用紙ID\用紙ID.tbx" }

    /*▼subMasta */
    var 文字列{ &subMastaTbx = &用紙入庫DPath + "..\..\submaster.tbx"}

    /*▼用紙サイズ */
    var 文字列{ &用紙サイズTbx = &用紙入庫DPath + "..\..\System\tbx\04_01_用紙サイズリスト.tbx" }

    /*▼用紙サイズ */
    var 文字列{ &仕入先Tbx = &用紙入庫DPath + "..\..\System\tbx\08_01_製紙会社リスト.tbx" }

    /*▼用紙サイズ */
    var 文字列{ &社員情報Tbx = &用紙入庫DPath + "..\..\System\tbx\03_01_シモダ社員情報.tbx" }

    /*▼発注書様参照 */
    var 文字列{ &用紙発注書tbx = &用紙入庫DPath + "rpx\用紙発注書.tbx"  }
    var 文字列{ &用紙発注書プリントrpx = &用紙入庫DPath + "rpx\用紙発注書.rpx"  }

*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　画像データ参照
*■*:
*■*:　共有から画像データを参照してもってきます
    /*▼SHIMODAロゴ */
    var 文字列{ &会社ロゴpng = &用紙入庫DPath + "..\..\System\logo\SHIMODA\Logo1(背景白).png" }
*■*----------------------------------------------------------
*■*-----　基本処理
*■*----------------------------------------------------------
名札 MAIN

*
手続き定義開始　フォーム::フォーム開始(長整数　&表番号)

    &在庫登録表 = &表番号
    手続き実行　InPut在庫情報( 1 )

手続き定義終了
*■*----------------------------------------------------------
*■*-----　各オブジェクトイベント
*■*----------------------------------------------------------
*■*----------------------------------------------------------
    *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
    *■*:　用紙ID　ソース更新
    手続き定義開始　用紙ID::ソース値更新()

        手続き実行 用紙ID情報格納　( &P用紙ID , &P銘柄 , &P紙色  , &PサイズID  , &Pサイズ名 , &P目 , &P重さ , &ns単価種　, &ns単価　)
        メソッド呼び出し @フォーム.変数変更()

    手続き定義終了
*■*----------------------------------------------------------
*■*----- 各手続き
*■*----------------------------------------------------------
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　テーブルID調査
*■*:　ターゲットのテーブルが開いてても開いて無くても表番号を返す関数
*■*:　引数１：ターゲットのテーブルアドレス
*■*:　引数２：新規で開くステータス（専有とか参照とか、すでに開いている場合はそのまま）
*■*:　戻り値：表のＩＤ
    手続き定義開始 テーブルID調査（ 文字列 &tbx ,文字列　&モード　, 参照　数値　&表ID ）

        *■*-------------------------------------
        *■*--  編集表番号取得（開いてても問題無し
        *■*-------------------------------------
        &表ID = #表番号取得( &tbx )
        if ( .not &表ID ) /*←未定義なら */
            
            表 &tbx , モード = &モード , 終了状態 = &CHK 
            &表ID = #表番号取得( &tbx )　　

        end
    手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　用紙IDから用紙詳細情報を変数に格納する
*■*:　引数１：ターゲットの用紙ID
*■*:　戻値１：参照：銘柄
*■*:　戻値２：参照：サブ銘柄
*■*:　戻値３：参照：サイズＩＤ
*■*:　戻値４：参照：サイズ名目
*■*:　戻値５：参照：YT(横目縦目)
*■*:　戻値６：参照：重さ
    手続き定義開始 用紙ID情報格納（ 数値　&ID ,　参照 文字列 &銘柄 , 参照 文字列 &サブ銘柄 , 参照 数値 &サイズID , 参照 文字列 &サイズ名 , 参照 文字列 &YT , 参照 数値 &重さ , 参照　文字列　&単価種　,　参照　数値　&単価　）

        var 数値  { &表ID = 0 }
        手続き実行 テーブルID調査( &用紙IDTbx , &mode , &表ID )
        編集表　&表ID

        検索 [ID] { &ID } , 終了状態 = &CHK 

            if (&CHK = 1)

                &銘柄       = [銘柄]
                &サブ銘柄    = [紙色]
                &サイズID    = [サイズ]
                &サイズ名   = [サイズ名目]
                &YT         = [目]
                &重さ       = [重さ]

            end
        
        終了 表 &表ID

    手続き定義終了
*■***********************************************************
*■*** 変数リセット
*■***********************************************************
    手続き定義開始 変数リセット（）
        &P入力日 ="" &P用紙ID ="" &PサイズID ="" &P重さ ="" &P枚数 =""
        &P銘柄 ="" &P紙色 ="" &Pサイズ名 ="" &P目 =""
    手続き定義終了
*■***********************************************************
*■*** 変数インプット
*■***********************************************************
    手続き定義開始 InPut在庫情報（ 数値　&行 ）

        *■*:::::::::::::::::::::::::::::
        *■*:　行情報が入っている
        if (　&行 <> 0 )
        
            手続き実行 変数リセット（）

            *■*:　その行にジャンプして必要情報を変数に入れる
            ジャンプ 行番号 = &行

            /*▼変数を入れる */
            &P入力日 =　[入力日] &P用紙ID = [用紙ID] &PサイズID = [サイズID] &P重さ = [重さ] &P枚数 = [枚数]
            &P銘柄 = [銘柄] &P紙色 = [紙色] &Pサイズ名 = [サイズ名] &P目 = [目]

        end
        
        /*▼変数情報を更新する */
        メソッド呼び出し @フォーム.変数変更()

    手続き定義終了
*■***********************************************************
*■*** レコードを移動するたびに行情報を更新する
*■***********************************************************
    手続き定義開始　フォーム::レコード移動(長整数　&行番号，長整数　&総件数，長整数　&明細番号)
        
        手続き実行　InPut在庫情報( #行番号 )

    手続き定義終了
*■***********************************************************
*■***　更新チェック
*■***　
*■***　更新情報が登録可能か、error変数を返す
*■***********************************************************
    手続き定義開始 更新チェック（　文字列　&Mode ,  参照　数値　&error番号 , 参照　文字列　&errorテキスト ）

        /*▼初期化 */ 
        &error番号 = 0
        &errorテキスト = ""

        /*▼error情報 */
        if (　&P用紙ID = #未定義　)
            &error番号 = 1
            &errorテキスト = "用紙IDの登録が無い紙情報です。"
        end
        if (　&P枚数 = 0　)
            &error番号 = 2
            &errorテキスト = "枚数の入力がありません。"
        end
        if (　&P枚数 = #未定義　)
            &error番号 = 3
            &errorテキスト = "枚数の入力がありません。"
        end
        *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
        *■*::　新規の場合は、用紙IDが重複していないかチェック
        if (　&Mode = "新規"　)

            検索 [用紙ID] { &P用紙ID } , 終了状態 = &CHK 
            if ( &CHK = 1  )
            
                &error番号 = 4
                &errorテキスト = #文字列( #行番号 ) + "行目： その用紙IDは既に登録されています。"

            end
        end
        *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
        *■*::　追加の場合は、今入れようとしている用紙IDと同じかチェック
        if (　&Mode = "追加"　)

            if ( &P用紙ID <> [用紙ID]  )
            
                &タイトル = "用紙IDが変更されています。"
                &表示本文　= "　元の紙情報が変わってしまいます。\n　このまま登録して良いですか？"
                メッセージボックス &タイトル , &表示本文 , アイコン = ? , ボタン指定 = 2 , 制御文字展開 = する , &CHK

                条件 ( &CHK = 2 ) 手続き終了

                検索 [用紙ID] { &P用紙ID } , 終了状態 = &CHK 
                if ( &CHK = 1  )
            
                    &error番号 = 4
                    &errorテキスト = #文字列( #行番号 ) + "行目： その用紙IDは既に登録されています。"

                end

            end
        end

    手続き定義終了
*■***********************************************************
*■***　新規作成　登録
*■***　
*■***　今変数に乗っている情報を新規情報とし、新しく登録を行います。
*■***********************************************************
    手続き定義開始 新規登録（）

        /*▼表示レコードの情報がリセットされる前に登録用情報をコピーしておく */
        var 数値  { &eCHK }
        var 数値  { &P2用紙ID = &P用紙ID , &P2サイズID = &PサイズID , &P2重さ = &P重さ , &P2枚数 = &P枚数 }
        var 文字列{ &P2銘柄 = &P銘柄 , &P2紙色 = &P紙色 , &P2サイズ名 = &Pサイズ名 , &P2目 = &P目 }
    
        /*▼新規登録実行して問題無いかチェックする */
        手続き実行　更新チェック( "新規" , &eCHK , &msgtxt )
        if ( 0 < &eCHK )
        
            確認 &msgtxt
            手続き終了

        end

        /*▼問題無いってことで登録する */
        編集表 &在庫登録表
        行追加 終了状態 = &CHK , [入力日] = #日時値 , [用紙ID] = &P2用紙ID , [サイズID] = &P2サイズID , [重さ] = &P2重さ ,\
        [枚数] = &P2枚数 , [銘柄] = &P2銘柄 , [紙色] = &P2紙色 , [サイズ名] = &P2サイズ名 , [目] = &P2目

    手続き定義終了
*■***********************************************************
*■***　データ修正　登録
*■***　
*■***　修正出来たら登録しちゃいましょう。IDが変わってたら一応警告を出しましょう。
*■***********************************************************
    手続き定義開始 データ修正丸（）

        var 数値  { &eCHK }
        var 数値  { &P2用紙ID = &P用紙ID , &P2サイズID = &PサイズID , &P2重さ = &P重さ , &P2枚数 = &P枚数 }
        var 文字列{ &P2銘柄 = &P銘柄 , &P2紙色 = &P紙色 , &P2サイズ名 = &Pサイズ名 , &P2目 = &P目 }
    
        /*▼新規登録実行して問題無いかチェックする */
        手続き実行　更新チェック( "追加" , &eCHK , &msgtxt )
        if ( 0 < &eCHK )
            確認 &msgtxt
            手続き終了
        end

        /*▼問題無いってことで登録する */
        編集表 &在庫登録表
        行訂正 終了状態 = &CHK , [入力日] = #日時値 , [用紙ID] = &P2用紙ID , [サイズID] = &P2サイズID , [重さ] = &P2重さ ,\
        [枚数] = &P2枚数 , [銘柄] = &P2銘柄 , [紙色] = &P2紙色 , [サイズ名] = &P2サイズ名 , [目] = &P2目

    手続き定義終了
