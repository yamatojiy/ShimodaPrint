*■***********************************************************
*■**
*■**　在庫リストの情報を正とし、用紙IDの情報を修正する一括処理です。
*■**
*■**　非常に危ない仕組みなので、使う場合はパスワード必須にする。
*■**　在庫リストを見て　IDリスト見て、　修正箇所を１箇所ずつ指摘するようにします。
*■**
*■**　「必ず在庫リストを開いた状態で起動してね」
*■**
*■***********************************************************

var 数値  { &CHK }
var 文字列{ &msgtxt }

var 文字列{ &dataPath = #データパス名 }
var 文字列{ &用紙IDtbx = &dataPath + "..\01_用紙ID\用紙ID.tbx" }

/*▼作業変数 */
var 文字列{ &銘柄 , &紙色 , &YT , &サイズ名 , &タイトル , &表示本文 }
var 数値  { &サイズID , &重さ ,　&用紙ID }
var 文字列{ &結合名 }

/*▼ライブラリ */
var 文字列{ &Lib = &dataPath　+ "..\..\System\Lib\Lib_ライブラリ桐処理.cmx" }
ライブラリ &Lib


*■*----------------------------------------------------------
*■*----- ここから処理
*■*----------------------------------------------------------
名札 MAIN

    var 数値 { &作業num = #IS表 }
    var 数値  { &OPnum = 0  , &openF = 0 }
    var 文字列 { &mode = "共有更新" , &pass = "ADDIDP" }

    /*▼比較用 */
    var 数値  { &結合名調査 }
    var 数値  { &銘柄調査 , &紙色調査 , &YT調査 , &サイズ調査　}

    利用者コード &pass
    手続き実行 表オープン（ &用紙IDtbx , &mode , &OPnum , &openF ）
    編集表 &OPnum
    

    編集表　&作業num
    *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
    *■*:　在庫テーブルを参照してIDと詳細データを変数へ
    繰り返し（ .NOT #終端行 ）
    
    
        &用紙ID = [用紙ID]

        &銘柄 = [銘柄（正）]
        &紙色 = [紙色（正）]
        &サイズID = [サイズ（正）]
        &YT = [目（正）]
        &重さ =[重さ（正）]
        &結合名 = [結合名]


        *■*::::::::::::::::::::::::::::::::::
        *■*:　用紙IDで調査開始
        編集表 &OPnum
        検索 [ID] { &用紙ID } , 終了状態 = &CHK

        /*▼結合名を見比べて、違いがあれば、詳細を見て、違った箇所がわかれば修正していいか聞く */

        *■*-------------------------------------
        *■*-- Hitしたら情報修正
        *■*-------------------------------------
        if ( &CHK = 1 )

            *■*::::::::::::::::::::::::::::::::::
            *■*:　１　銘柄を確認
            if ( &銘柄 <> [銘柄] )
            
                &タイトル =　"「銘柄」　の文字列に問題を検知しました。"
                &表示本文　=　"\n【修】" + &銘柄 +"\n【旧】" + [銘柄] +"\n 用紙IDの差し替えをおこないますか？"

                メッセージボックス &タイトル , &表示本文 , アイコン = ! , ボタン指定 = 2 , 制御文字展開 = する , &CHK

                if (&CHK = 1 )
                
                    行訂正 終了状態 = &CHK , [銘柄] = &銘柄 

                end
            end
            *■*::::::::::::::::::::::::::::::::::
            *■*:　２　紙色を確認
            
            if ( &紙色 <> [紙色] )
            
                &タイトル 　　=　"「紙色」　の文字列に問題を検知しました。"
                &表示本文　=　"\n【修】" + &紙色 +"\n【旧】" + [紙色] +"\n 用紙IDの差し替えをおこないますか？"

                メッセージボックス &タイトル , &表示本文 , アイコン = ! , ボタン指定 = 2 , 制御文字展開 = する , &CHK

                if (&CHK = 1 )
                
                    行訂正 終了状態 = &CHK , [紙色] = &紙色 

                end
            end
            *■*::::::::::::::::::::::::::::::::::
            *■*:　３　目を確認（先に目をチェックします。文字列だから）
            if ( &YT <> [目] )
            
                &タイトル 　　=　"「目」　の文字列に問題を検知しました。"
                &表示本文　=　"\n【修】" + &YT +"\n【旧】" + [目] +"\n 用紙IDの差し替えをおこないますか？"

                メッセージボックス &タイトル , &表示本文 , アイコン = ! , ボタン指定 = 2 , 制御文字展開 = する , &CHK

                if (&CHK = 1 )
                
                    行訂正 終了状態 = &CHK , [目] = &YT 

                end
            end
            *■*::::::::::::::::::::::::::::::::::
            *■*:　４　サイズを確認（数字なので直接チェック）
            if ( &サイズID　<> [サイズ] )
            
                &タイトル 　　=　"「サイズ」　の文字列に問題を検知しました。"
                &表示本文　=　"\n【修】" + #文字列( &サイズID ) +"\n【旧】" + #文字列( [サイズ] ) +"\n 用紙IDの差し替えをおこないますか？"

                メッセージボックス &タイトル , &表示本文 , アイコン = ! , ボタン指定 = 2 , 制御文字展開 = する , &CHK

                if (&CHK = 1 )
                
                    行訂正 終了状態 = &CHK , [サイズ] = &サイズID

                end
            end
            *■*::::::::::::::::::::::::::::::::::
            *■*:　5　重さを確認（先に目をチェックします。文字列だから）
            if ( &重さ　<> [重さ] )
            
                &タイトル =　"「重さ」　の文字列に問題を検知しました。"
                &表示本文　=　"\n【修】" +#文字列( &重さ )  +"\n【旧】" + #文字列( [重さ] ) +"\n 用紙IDの差し替えをおこないますか？"

                メッセージボックス &タイトル , &表示本文 , アイコン = ! , ボタン指定 = 2 , 制御文字展開 = する , &CHK

                if (&CHK = 1 )
                
                    行訂正 終了状態 = &CHK , [重さ] = &重さ

                end
            end
        end

        /*▼リセット */
        &銘柄 ="" &紙色 ="" &YT ="" &サイズ名　=""
        &サイズID ="" &重さ =""

        編集表　&作業num
        ジャンプ 行番号 = 次行 
    繰り返し終了 

終了
