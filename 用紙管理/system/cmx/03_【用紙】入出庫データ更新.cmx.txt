*■*****************************************************************************
*■*****　用紙の在庫状況などを見れるようにするため、一括処理に収める。
*■*****　入庫状況の更新や、ID割り振りなどが結構めんどくさいけど頑張る。
*■*****************************************************************************
/*▼基本変数　*/
var 数値 {　&ok , &NumCount = 1 , &i , &v , &s , &z }

/*▼ファイルコピー用パス */
var 文字列 { &NewOdListTxCopy    = &STbxPath + "01_新入庫情報管理(COPY).tbx"}  /*←新入庫管理用[&NewOdListTx]（保存先）パス　*/
var 文字列 { &consumptionTxCopy  = &STbxPath + "02_用紙(COPY).tbx"}           /*←用紙TBXの保存先、用紙TBXとは場所が違うので注意　*/
var 文字列 { &paperIdListTxCopy  = &STbxPath + "03_用紙ID(COPY).tbx"}           /*←用紙TBXの保存先、用紙TBXとは場所が違うので注意　*/
var 文字列 { &StockTxCopy        = &STbxPath + "07_用紙在庫(COPY).tbx"}           /*←在庫用の保存先データ　*/

/*▼印字するテキストファイル　*/
var 文字列 { &NewOderText = &SIniPath + "NewOderText.txt" }    /*←入庫用テキストファイル　*/
var 文字列 { &ConsumpText = &SIniPath + "ConsumpText.txt" }    /*←出庫用テキストファイル　*/
var 文字列 { &StockText   = &SIniPath + "StockText.txt" }      /*←在庫用テキストファイル　*/

/*▼規定時期の日付を設定　*/
var 日時 { &tMonth = d"2023/01/01" } /*←今年からの判定にしてみる　*/
var 文字列{ &銘柄全 , &紙色全 ,  &用紙全 ,  &サイズ半 ,  &目全 , 　&重さ半 }

*■*****************************************************************************
*■*****************　ここから変数定義　　　　   　
*■*****************************************************************************
名札　MAIN

    /*▼在庫用処理を行います。　*/
    手続き実行　在庫データをテキスト化

    *■*    新入庫管理　を加工してテキストデータにします。
    *■*    ・納品予定　から　納品完了になったデータは日付が入庫日に代わって完了となるようにする。
    手続き実行　入庫データをテキスト化

    *■*    用紙TBXをテキストデータにします。
    *■*    ・用紙IDを配列化する
    *■*    ・結合名を作ってIDを割り振る。そのあとにテキストデータにする。
    *■*     マッチするのだけIDを入れる。
    *■*    ※入力が不安定な用紙TBXからは用紙IDを作成しない。　→　入力でヒューマンエラーなどが発生している可能性があるため。これは用紙TBX側でふさぐ
    手続き実行　出庫データをテキスト化

    *■*    一つのTBXに書き出す。
    *■*    ・VIXでも良いが、処理が重くなる可能性があるので、TBXにする。 
    *■*    ・在庫、入庫、出庫の順に書き出したあと、並び変えてリストを見やすくする
    手続き実行　新用紙管理へ出力


    確認 "データの更新が完了しました"


終了
*■*----------------------------------------------------------------------------
*■*----------　　　　　   新用紙管理.tbx に書き出し　
*■*----------------------------------------------------------------------------
名札　新用紙管理へ出力()

    グループ選択解除
    行削除　*　
    読み込み　テキスト　, &StockText    ,区切り　=　"↑"    /*←在庫　*/
    読み込み　テキスト　, &NewOderText  ,区切り　=　"↑"    /*←入庫　*/
    読み込み　テキスト　, &ConsumpText　 ,区切り　=　"↑"    /*←出庫　*/
    グループ選択

手続き終了
*■*----------------------------------------------------------------------------
*■*----------　　　出庫 to Text
*■*----------------------------------------------------------------------------
名札　出庫データをテキスト化()

    /*▼用紙IDテーブルを開いて格納する　*/
    表　&paperIdListTx ,  モード = 参照
    書出し　表　&paperIdListTxCopy

    var 数値   { &ID最大数 = #総件数 }
    var 文字列 { &ID結合名[&ID最大数] }
    var 数値   { &ID[&ID最大数] }
    &v = 1

    繰り返し( .NOT　＃終端行 )

        &ID結合名[&v] = [結合名]
        &ID[&v]    　 = [ID]

        ジャンプ 行番号 = 次行
        &v = &v + 1
    
    繰り返し終了

    終了  表  編集対象表

    /*▼IDと照合しながら用紙テーブルを埋めるぜ　*/
    利用者コード "gyoumu"
    表　&consumptionTx , , モード = 共有参照
    書出し　表　&consumptionTxCopy

    /*▼一応未定義があると困るので絞り込むと同時に規定時期からのデータを取り込む　*/
    絞り込み [用紙名] {#定義}
    絞り込み　[日付]　{ &tMonth ≦}　/*←規定以降の絞り込み */
    絞り込み [サイズ]　{ 0 < }      /*←サイズが0である封筒・名刺の情報は用紙リストに載せない　*/

    /*▼用紙結合名を作ってIDがあるか調べます。　*/
    var 文字列 { &idNumChk }

    印字開始　&ConsumpText , 終了状態 = &ok
    繰り返し (.NOT #終端行)
        
        /*▼入庫の情報と統一するために、全角半角の仕様に合わせる　*/
        &用紙全 = #全角( [用紙名])
        &サイズ半 = #半角( #文字列( [サイズ] ) )
        &目全 = #全角( [目])
        &重さ半= #半角( #文字列( [重さ] ) )

        /*▼結合名を作成する　*/
        &idNumChk = #連結( &用紙全 + "-", &サイズ半 + "-" , &目全 + "-" , &重さ半)
        &s = 1
        
        /*▼出庫件数分印字処理を行う　*/
        繰り返し (&s = 1 , &ID最大数 )

            if (&idNumChk = &ID結合名[&s] ) /*←結合名と合うものがあれば印字する　*/

                *■*  「項目」　    [日付]←　  [担当]　　　[伝票No]　　　　[品名]　　　　[用途] 　　　[用紙ID]　　　 [用紙名]　   [規格]　　　 [サイズ]　  　　　　　[目]　　　　[重さ]　　　　[枚数]
                *■*  ※入庫　入庫予定　出庫
                印字 "消費","↑",[日付],"↑",[担当者],"↑",[伝票ＮＯ],"↑",[品名],"↑","","↑",&ID[&s]　,"↑",&用紙全 ,"↑","","↑",&サイズ半 ,"↑",&目全 ,"↑",&重さ半 ,"↑",[枚数]*-1

                繰り返し中止
            
            end
            /*▼最終行まで行った場合はＩＤ無しとのことで登録する　*/
            if(&ID最大数 < &s + 1  )
                印字 "消費","↑",[日付],"↑",[担当者],"↑",[伝票ＮＯ],"↑",[品名],"↑","","↑","","↑",&用紙全 ,"↑","","↑",&サイズ半 ,"↑",&目全 ,"↑",&重さ半 ,"↑",[枚数]*-1

                繰り返し中止
            end
            &s = &s　+ 1
        繰り返し終了

        ジャンプ　行番号　=　次行
    繰り返し終了

    印字終了
    終了  表  編集対象表

手続き終了
*■*----------------------------------------------------------------------------
*■*----------　　　入庫 to Text
*■*----------------------------------------------------------------------------
名札 入庫データをテキスト化()
    
    /*▼まずはシートをコピーします　*/
    表　&NewOdListTx , , モード = 共有更新
    書出し　表　&NewOdListTxCopy
    
    /*▼一応未定義があると困るので絞り込む　*/
    絞り込み [用紙名] {#定義}

    /*▼記載情報をすべてテキスト化する　*/
    印字開始 &NewOderText , 終了状態 = &ok
       
        繰り返し (.NOT #終端行　)

            /*▼用紙名の規格を合わせる　*/
            &用紙全 = #全角( [用紙名])
            &サイズ半 = #半角( #文字列( [サイズ] ) )
            &目全 = #全角( [目])
            &重さ半= #半角( #文字列( [重さ] ) )
        
            if ([入庫完了]= "◎")/*←入庫完了データが入っている場合は入庫完了データとして印字する　*/
                印字　"入庫","↑",[受領日],"↑","","↑",[伝票No],"↑",[品名],"↑",[印刷項目],"↑",[用紙ID],"↑",&用紙全　,"↑",[規格],"↑",&サイズ半　,"↑",&目全　,"↑",&重さ半　,"↑",[枚数]
            else
                印字　"入庫予定","↑",[入庫予定日付],"↑","","↑",[伝票No],"↑",[品名],"↑",[印刷項目],"↑",[用紙ID],"↑",&用紙全　,"↑",[規格],"↑",&サイズ半　,"↑",&目全　,"↑",&重さ半　,"↑",[枚数]

            end
        
            ジャンプ 行番号 = 次行
        繰り返し終了
    印字終了
    終了  表  編集対象表

手続き終了
*■*----------------------------------------------------------------------------
*■*----------　　　在庫 to Text
*■*----------　　　こちらは在庫処理になるので間違えないように
*■*----------------------------------------------------------------------------
名札 在庫データをテキスト化()
    
    /*▼まずはシートをコピーします　*/
    表　&StockTx , , モード = 共有更新
    書出し　表　&StockTxCopy
    
    /*▼一応未定義があると困るので絞り込む　*/
    絞り込み [用紙名] {#定義}

    /*▼記載情報をすべてテキスト化する　*/
    印字開始 &StockText , 終了状態 = &ok
       
        繰り返し (.NOT #終端行　)

            /*▼用紙名の規格を合わせる　*/
                &用紙全 = #全角( [用紙名])
                &サイズ半 = #半角( #文字列( [サイズ] ) )
                &目全 = #全角( [目])
                &重さ半= #半角( #文字列( [重さ] ) )
        
            *■*一から作り直す
            *■*  「項目」　        [日付]←        [担当]       [伝票No]　　　　 [品名]　　　　[用途] 　　　[用紙ID]　　　 [用紙名]　　   [規格]   [サイズ]　　　　　[目]　　　　[重さ]　　　　[枚数]
            *■*       ※入庫　入庫予定　出庫
            印字　"在庫"    ,"↑",[入力日]   ,"↑",""     ,"↑",""     ,"↑",""     ,"↑",""    ,"↑",[用紙ID],"↑",&用紙全　,"↑",[規格],"↑",&サイズ半　,"↑",&目全　,"↑",&重さ半　,"↑",[枚数]
        
            ジャンプ 行番号 = 次行

        繰り返し終了

    印字終了
    終了  表  編集対象表

手続き終了