*■*****************************************************************************
*■*****************　　　　　   　在庫用用紙ID登録一括処理です　　　　　　　　　　************
*■*****************　　　　　   　そんな使わないはずなので、もう新規で一括処理を追加する。
*■*****************************************************************************
/*▼基本変数　*/
var 数値 {　&ok , &NumCount = 1 , &i , &v , &s , &z }

/*▼ファイルコピー用パス。このファイルだけで完結するので、変数名はそのまま行きます */
var 文字列 { &PeparInBasePath = &StockTx }
var 文字列 { &PeparInCopyPath = &STbxPath + "07_用紙在庫.tbx(COPY)"}
var 文字列 { &PeparIdListPath = &STbxPath + "用紙ID.tbx"}

/*▼用紙名のコピーファイルテキスト*/
var 文字列 { &peparAddListI = &SIniPath + "StockAddList.txt"}

var 文字列{ &SuppliersName[5] = {""} }
&SuppliersName[1] = "児島"
&SuppliersName[2] = "網中"
&SuppliersName[3] = "レイメイ"
&SuppliersName[4] = "紙弘"

/*▼格納用変数　*/
var 数値 { &idListNum }　/*←IDの数を格納します　*/


*■*****************************************************************************
*■*****************　ここから変数定義　　　　   　
*■*****************************************************************************
名札　MAIN
    /*▼表A（元）を表B（先）に書き出します　*/
    手続き実行　作業ファイルコピー

    /*▼IDリストの情報を格納します　*/
    手続き実行 用紙IDテーブル情報取得

    /*▼コピーしたテーブルを開いて用紙IDが無いリストを吐き出します　*/
    手続き実行 追加ＩＤリスト作成
    /*▼吐き出したリストを用紙IDリストに新規登録します　*/
    手続き実行　用紙リスト更新
    /*▼元の表を更新して終わる　*/

    メッセージボックス　"未定義の用紙情報を用紙IDリストに追加しました",\
        "編集メニュー　＞　置換（R）　＞　再計算（E）　を押すと、\n　未定義の用紙にIDが付与されます。", \
        アイコン　= i ,\
        ボタン指定 = 1, \
        制御文字展開 = する ,\
        &ok

終了
*■*----------------------------------------------------------------------------------------------
*■*---　　　　　   　用紙IDの情報を格納します。
*■*---          データはグローバル変数で宣言すること。
*■*----------------------------------------------------------------------------------------------
名札　用紙IDテーブル情報取得

    表　 &PeparIdListPath , モード = 共有更新

        &idListNum =  #総件数

    終了  表  編集対象表

手続き終了
*■*----------------------------------------------------------------------------------------------
*■*---　　　　　   　用紙リストに追加して更新
*■*----------------------------------------------------------------------------------------------
名札　用紙リスト更新

    表　 &PeparIdListPath , モード = 共有更新

    読み込み　テキスト , &peparAddListI  , 区切り = "↑"

    終了  表  編集対象表

手続き終了
*■*----------------------------------------------------------------------------------------------
*■*---　　　　　   　追加するテキストリストを用意する
*■*----------------------------------------------------------------------------------------------
名札　追加ＩＤリスト作成
    
    表　&PeparInCopyPath,,モード = 共有更新

    /*▼IDが無いものを絞り込み　*/
    絞り込み　[用紙ID]　{#未定義}
    絞り込み　単一化 = {[結合名]} /*←被り除去　*/

    /*▼テキストへ印字開始宣言　*/
    印字開始　&peparAddListI ，終了状態　= &ok

        /*▼絞り込んだ内容を印字　*/
        繰り返し (.NOT #終端行)

            &idListNum　= &idListNum + 1
        
            印字  [銘柄（全角）], "↑" ,[紙色（全角）], "↑" ,[サイズ], "↑", [目], "↑",[重さ], "↑" , #日時値 , "↑" , &idListNum , "↑" , ""
            ジャンプ　行番号 = 次行

        繰り返し終了

    印字終了

    終了  表  編集対象表

手続き終了
*■*----------------------------------------------------------------------------------------------
*■*---　　　　　   　誰かが編集中かもしれないので、用紙データをコピーします
*■*----------------------------------------------------------------------------------------------
名札　作業ファイルコピー
    /*▼表A（元）を表B（先）に書き出します。　*/
    表　&PeparInBasePath , , モード = 共有更新
        書出し　表　&PeparInCopyPath ,
    *■-　　終了  表  編集対象表
    
手続き終了
