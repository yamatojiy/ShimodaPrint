*■***********************************************************
*■*** 
*■*** 用紙IDを登録するフォームです。
*■*** 　引用が可能にします。　重複確認を先にします。　最後登録させます。
*■*** 
*■***********************************************************
/*▼汎用 */
var 数値  { &CHK , &表Num }
var 文字列{ &msgtxt }
var 数値  { &断裁TbxNum = 0 }
var 文字列{ &専有 = "専有" , &共有参照 = "共有参照"　}

/*▼案件用変数 */
var 日時  { &days }

/*▼設定項目ID */
var 文字列  { &G発注ID }
var 日時  { &G発注日 }
var 数値  { &G担当者ID }
var 文字列  { &G担当者名 }
var 数値  { &G伝票ID }
var 数値  { &G前回ID }
var 文字列  { &G得意先 }
var 文字列  { &G品名 }
var 数値  { &G伝票部数 }
var 文字列  { &G伝票サイズ }
var 日時  { &G作業日 }
var 文字列  { &G迄時間 }
var 文字列  { &G機械名 }
var 数値  { &Gロス要因1顧客 }
var 数値  { &Gロス要因2自部署 }
var 数値  { &Gロス要因3他部署 }
var 数値  { &Gロス要因4開発破損 }
var 数値  { &G用紙ID }
var 文字列  { &G銘柄 }
var 文字列  { &G紙色 }
var 数値  { &Gサイズ作業ID }
var 数値  { &Gサイズ名称ID }
var 文字列  { &Gサイズ名 }
var 文字列  { &GTY }
var 数値  { &G重さ }
var 数値  { &G断裁サイズ作業ID }
var 数値  { &G断裁サイズ名称ID }
var 文字列  { &G断裁サイズ名 }
var 文字列  { &G断裁縦横 }
var 数値  { &G断裁枚数定数 }
var 数値  { &G断裁枚数予備 }
var 数値  { &G断裁枚数合計 }
var 文字列  { &G断裁不要 }
var 文字列  { &G断裁備考 }
var 文字列  { &G備考 }
var 数値  { &G備考画像 }


*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　１行ごとの使用変数
    /*▼ファイルパス */
    var 文字列{ &DPath = #データパス名 }

    /*▼画像データ */
    var 文字列{ &iconPng1        = &DPath + "png\in.png" }


*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　モーダルフォーム

    /*▼モーダルフォーム用変数 */
    var 数値  { &Num }
    var 文字列{ &Str }
    var 日時  { &Day }

    /*▼用紙索引 */
    var 文字列{ &GS用紙IDmodal = &DPath + "..\01_用紙ID\用紙ID索引フォーム.wfx"  }
    var 文字列{ &GS用紙IDTbx = &DPath + "..\01_用紙ID\用紙ID.tbx"  }

    /*▼用紙登録 */
    var 文字列{ &GS用紙登録modal = &DPath + "..\01_用紙ID\用紙ID登録フォーム.wfx"  }

    /*▼用紙サイズ */
    var 文字列{ &GS用紙サイズmodal = &DPath + "..\..\System\modal\General\004_用紙_04_01_用紙サイズリスト_1_(基本).wfx" }
    var 文字列{ &GS用紙縦横サイズmodal = &DPath + "..\..\System\modal\General\004_用紙_04_01_用紙サイズリスト_3_(縦横).wfx" }
    var 文字列{ &GS用紙全紙サイズmodal = &DPath + "..\..\System\modal\General\004_用紙_04_01_用紙サイズリスト_4_(全紙).wfx" }


    /*▼用紙サイズ */
    var 文字列{ &GSタイム設定modal = &DPath + "..\..\System\modal\General\001_基本_01_01_タイムリスト_1_(ハーフなし).wfx" }

    /*▼用紙情報選択 */
    var 文字列{ &GS用紙銘柄設定modal = &DPath + "..\..\System\modal\General\004_用紙_04_03_銘柄リスト_1.wfx" }
    var 文字列{ &GS用紙SUB銘柄設定modal = &DPath + "..\..\System\modal\General\004_用紙_04_04_SUB銘柄リスト_1.wfx" }
    var 文字列{ &GS用紙TY設定modal = &DPath + "..\..\System\modal\General\004_用紙_04_05_目リスト_1.wfx" }
    var 文字列{ &GS用紙重さ設定modal = &DPath + "..\..\System\modal\General\004_用紙_04_06_重さリスト_1.wfx" }

    /*▼カレンダー */
    var 文字列{ &GSカレンダーmodal = &DPath + "..\..\System\modal\Calendar\INF_DatePicker.wfx" }

    /*▼従業員 */
    var 文字列{ &GS社員リストmodal = &DPath + "..\..\System\modal\General\03_01_社員情報【S】.wfx" }
    var 文字列{ &GS社員リストtbx   = &DPath + "..\..\System\tbx\03_01_シモダ社員情報.tbx" }

*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　外部参照
*■*:
*■*:　共有ファイルから、サイズIDを使ってサイズ名称を取ってきます。
    /*▼用紙ID */
    var 文字列{ &用紙IDTbx = &DPath + "..\01_用紙ID\用紙ID.tbx" }

    /*▼用紙サイズ */
    var 文字列{ &用紙サイズTbx = &DPath + "..\..\System\tbx\04_01_用紙サイズリスト.tbx" }

    /*▼subMasta */
    var 文字列{ &subMastaTbx = &DPath + "..\..\submaster.tbx"}

    /*▼データ保存先 */
    var 文字列{ &資材断裁tbx = &DPath + "資材断裁.tbx" }

    /*▼プリントアウトデータ */
    var 文字列{ &用紙出庫伝票rpx = &DPath +"tbx\出庫伝票.rpx" }

*■*----------------------------------------------------------
*■*-----　基本処理
*■*----------------------------------------------------------
名札 MAIN

*
*■*----------------------------------------------------------
*■*-----　各オブジェクトイベント
*■*----------------------------------------------------------
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　フォーム開始
手続き定義開始　フォーム::フォーム開始(長整数　&表番号)

    /*▼呼び出した時の表番号を変数に保存する */
    &表Num = &表番号
    手続き実行 変数リセット（）
手続き定義終了
*■*----------------------------------------------------------
*■*----- 各手続き
*■*----------------------------------------------------------
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　テーブルID調査
*■*:　ターゲットのテーブルが開いてても開いて無くても表番号を返す関数
*■*:　引数１：ターゲットのテーブルアドレス
*■*:　引数２：新規で開くステータス（専有とか参照とか、すでに開いている場合はそのまま）
*■*:　戻り値：表のＩＤ
*■*:　戻り値：既にオープンされている場合は１、新規でオープンなら０
手続き定義開始 テーブルID調査（ 文字列 &tbx ,文字列　&モード　, 参照　数値　&表ID , 参照　数値　&openF ）

        *■*-------------------------------------
        *■*--  編集表番号取得（開いてても問題無し
        *■*-------------------------------------
        &表ID = #表番号取得( &tbx )
        if ( .not &表ID ) /*←未定義なら */
            
            表 &tbx , モード = &モード , 終了状態 = &CHK 
            &表ID = #表番号取得( &tbx )　　

        &openF = 0
    else
        &openF = 1
    end
手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　確定情報返答
*■*:　予定の変数と確定の変数を受け取って、確定があれば確定を、無ければ予定を返す
*■*:　戻り値：数値：（返す数値）
*■*:　戻り値：数値：フラグ。確定なら１　予定なら０
*■*:　引数１：数値：予定情報
*■*:　引数２：数値：確定情報
手続き定義開始　確定情報返答（ 参照　数値　&AnsNum , 参照　数値　&AnsBoot , 数値　&予定値　, 数値 &確定値 ）

    /*▼未定義じゃなければ確定 */
    if ( &確定値 <> #未定義 )
        &AnsNum  = &確定値
        &AnsBoot = 1
    else

        &AnsNum  = &予定値
        &AnsBoot = 0
    end

手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　変数リセット
*■*:　定義されている変数を全てリセットする
手続き定義開始 変数リセット（）

    &G発注ID = "" &G発注日 = #日時値  &G担当者ID = "" &G担当者名 = "" &G伝票ID = "" &G前回ID = "" &G得意先 = "" &G品名 = "" &G作業日 = "" &G機械名 = "" 
    &Gロス要因1顧客 = 0 &Gロス要因2自部署 = 0 &Gロス要因3他部署 = 0 &Gロス要因4開発破損 = 0 &G用紙ID = "" &G銘柄 = "" &G紙色 = "" &Gサイズ名称ID = "" 
    &GTY = "" &G重さ = "" &G断裁サイズ作業ID = "" &G断裁サイズ名称ID = "" &G断裁サイズ名 = "" &G断裁縦横 = "" &G断裁枚数定数 = 0 &G断裁枚数予備 = 0
    &G断裁枚数合計 = 0 &G備考 = "" &Gサイズ名 = ""　&G伝票部数 = ""　&G伝票サイズ = ""　&G迄時間 = ""　&G断裁不要 = ""　&Gサイズ作業ID = ""　&G断裁備考 = ""
    &G備考画像 = 0

    メソッド呼び出し @フォーム.変数変更()
手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　入力日なんて大体今なんだから、今の時間を入れてあげようという心意気
手続き定義開始　1テキスト_発注日::フォーカス取得(文字列　&喪失オブジェクト名)

    &G発注日 = #日時値
    メソッド呼び出し @フォーム.変数変更()

手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　１：社員IDを更新すると、略称も自動で切り替わる関数です。　
手続き定義開始　テキスト社員ID::ソース値更新(　)

    var 数値  { &表Num , &OPboot }
    手続き実行 テーブルID調査( &GS社員リストtbx　, &共有参照　, &表Num , &OPboot )

    編集表 &表Num
    検索 [社員ID] { &G担当者ID } , 終了状態 = &CHK 
    
    if (&CHK = 1 )
    
        &G担当者名 = [略称]
    end 

    条件 ( &OPboot = 0 ) 終了　表　&表Num
    メソッド呼び出し @フォーム.変数変更()
手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　２：社員略称を更新すると、IDも自動で切り替わる関数です。　
手続き定義開始　テキスト社員略称::ソース値更新(　)

    var 数値  { &表Num , &OPboot }
    手続き実行 テーブルID調査( &GS社員リストtbx　, &共有参照　, &表Num , &OPboot )

    編集表 &表Num
    検索 [略称] { &G担当者名 } , 終了状態 = &CHK 
    
    if (&CHK = 1 )
    
        &G担当者ID = [社員ID]
    end 

    条件 ( &OPboot = 0 ) 終了　表　&表Num
    メソッド呼び出し @フォーム.変数変更()
手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　伝票Noを入力すると、サブマスタから必要情報をGetしにいきます。
手続き定義開始　伝票ID::ソース値更新()

    var 数値  { &表num = 0 , &open = 0 , &AnsBoot }

    手続き実行 テーブルID調査( &subMastaTbx , &共有参照 , &表num , &open )

    編集表　&表num

    検索 [伝票ＮＯ] { &G伝票ID } , 終了状態 = &CHK 

        if (&CHK = 1)
        
            手続き実行 確定情報返答( &G伝票部数 , &AnsBoot , [部数] , [確定部数] )
            &G前回ID =　#数値 ( [前回伝票] ) 
            &G得意先 = [得意先]
            &G品名 = [品名]
            &G伝票サイズ = [サイズ]
        end
    条件 ( &open = 0 )  終了 表 &表num    
    メソッド呼び出し @フォーム.変数変更()
手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　用紙IDを更新したら、詳細情報を再取得する関数
手続き定義開始　用紙ID_1::ソース値更新()

    var 数値  { &表num = 0 , &open = 0 , &AnsBoot }
    手続き実行 テーブルID調査( &用紙IDTbx , &共有参照 , &表num , &open )

    編集表　&表num
    検索 [ID] { &G用紙ID } , 終了状態 = &CHK

    if ( &CHK = 1 )
        &G銘柄 = [銘柄]
        &G紙色 = [紙色]
        &Gサイズ名称ID = [サイズ]
        &Gサイズ名 = [サイズ名目]
        &GTY = [目]
        &G重さ = [重さ]
    end

    条件 ( &open = 0 )  終了 表 &表num    
    メソッド呼び出し @フォーム.変数変更()
手続き定義終了

手続き定義開始　銘柄t::ソース値更新()
    手続き実行　用紙ID調査（）
    メソッド呼び出し @フォーム.変数変更()
手続き定義終了
手続き定義開始　t紙色::ソース値更新()
    手続き実行　用紙ID調査（）
    メソッド呼び出し @フォーム.変数変更()
手続き定義終了
手続き定義開始　t目::ソース値更新()
    手続き実行　用紙ID調査（）
    メソッド呼び出し @フォーム.変数変更()
手続き定義終了
手続き定義開始　t重さ::ソース値更新()
    手続き実行　用紙ID調査（）
    メソッド呼び出し @フォーム.変数変更()
手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　発注用紙サイズを入力する際の
手続き定義開始　サイズID_4::ソース値更新()

    var 数値  { &表num = 0 , &open = 0 , &AnsBoot }
    手続き実行 テーブルID調査( &用紙サイズTbx , &共有参照 , &表num , &open )

    編集表　&表num
    検索 [名称ID] { &Gサイズ作業ID } , 終了状態 = &CHK

    if ( &CHK = 1 )

        &Gサイズ名 = [名称]
        &Gサイズ名称ID = [作業ID]
    end

    条件 ( &open = 0 )  終了 表 &表num

    手続き実行　用紙ID調査（）

    メソッド呼び出し @フォーム.変数変更()
手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　断裁サイズのモーダルフォームを閉じる時の処理
手続き定義開始　断裁サイズ::ソース値更新()

    var 数値  { &表num = 0 , &open = 0 , &AnsBoot }
    手続き実行 テーブルID調査( &用紙サイズTbx , &共有参照 , &表num , &open )

    編集表　&表num
    検索 [名称ID] { &G断裁サイズ作業ID } , 終了状態 = &CHK

    if ( &CHK = 1 )

        &G断裁サイズ名称ID = [作業ID]
        &G断裁サイズ名 = [名称]
        &G断裁縦横 = [縦横サイズ名]
    end

    条件 ( &open = 0 )  終了 表 &表num    
    メソッド呼び出し @フォーム.変数変更()
手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　用紙数　定数か予備を更新したら、合計の数値を変える
手続き定義開始　用紙数::ソース値更新()

    &G断裁枚数合計 = &G断裁枚数定数 + &G断裁枚数予備

    メソッド呼び出し @フォーム.変数変更()
手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　テーブル登録
*■*:　変数に入っている情報をテーブルに登録します。
*■*:　赤枠が埋まってないと登録しないようにします。
*■*:　発注IDがあれば上書きし、無ければ新規作成を行います。
手続き定義開始 出庫情報登録（）

    *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
    *■*:　実質エラー処理
    var 数値  { &OKboot = 0 } /*←エラー✓用、0なら問題無し、１以上なら属したエラーコード */
    条件 ( &G発注日 = "" ) &OKboot = 1　 &msgtxt = "発注日が未記入です！"
    条件 ( &G担当者ID　= "" ) &OKboot = 2　&msgtxt = "社員IDが未記入です！"
    条件 ( &G作業日　= "" ) &OKboot = 2　&msgtxt = "作業日が未記入です！"
    条件 ( &G迄時間　= "" ) &OKboot = 2　&msgtxt = "作業時間が未記入です！"
    条件 ( &G機械名　= "" ) &OKboot = 2　&msgtxt = "機械名が未記入です！"
    条件 ( &G断裁枚数合計　= 0 ) &OKboot = 3　&msgtxt = "用紙枚数の合計が未記入です！"

    *■*-------------------------------------
    *■*-- 日時チェック
    var 日時  { &CHKDay }
    var 文字列{ &TimeStr = #部分列 ( [迄時間] , 0 , 2 )} /*←これで時間だけの数値が入るはず */
    &CHKDay = #日時値生成( #年( &G作業日 ) , #月( &G作業日 ) , #日( &G作業日 ) , &TimeStr )

    /*▼現在より過去に設定しようとするとエラーになる */
    if ( &CHKDay < #日時値 )

        &OKboot = 10　&msgtxt = "現時刻より過去の作業日が指定されているため登録できません"
    
    end

    if ( &OKboot > 0 )

        var 文字列{ &表示本文 }
        &表示本文 = "todayTime :" +#日時文字列( #日時値 , 9, 3, 2 ) + " <>  CHKTime：" +#日時文字列(  &CHKDay , 9, 3, 2 ) 
        メッセージボックス &msgtxt , &表示本文 , アイコン = ! , ボタン指定 = 2 , 制御文字展開 = しない , &CHK
        手続き終了

    end
    *■*===========================================================
    *■*:　発注IDの確認を行います。
    var 数値  { &addType = 0 , &countNum , &Row } /*←0ならご新規、１なら修正 */

        検索 [発注ID] { &G発注ID } , 終了状態 = &CHK 
        if ( &CHK = 1 ) /*←1なら検索にヒットしたという事。なので上書き処理 */

            &Row = #行番号

            行訂正 終了状態 = &CHK ,　[発注ID] = &G発注ID , [発注日] = &G発注日 , [担当者ID] = &G担当者ID , [担当者名] = &G担当者名 ,\
                [伝票ID] = &G伝票ID , [前回ID] = &G前回ID , [得意先] = &G得意先 , [品名] = &G品名 , [伝票部数] = &G伝票部数 ,\
                [伝票サイズ] = &G伝票サイズ , [作業日] = &G作業日 , [機械名] = &G機械名 , [ロス要因1：顧客] = &Gロス要因1顧客 ,\
                [ロス要因2：自部署] = &Gロス要因2自部署 , [ロス要因3：他部署] = &Gロス要因3他部署 , [ロス要因4：開発・破損] = &Gロス要因4開発破損 ,\
                [用紙ID] = &G用紙ID , [銘柄] = &G銘柄 , [紙色] = &G紙色 , [サイズ] = &Gサイズ名称ID , [サイズ名] = &Gサイズ名 , [TY] = &GTY ,\
                [重さ] = &G重さ , [断裁サイズ作業ID] = &G断裁サイズ作業ID , [断裁サイズ名称ID] = &G断裁サイズ名称ID , [断裁サイズ名] = &G断裁サイズ名 ,\
                [断裁縦横] = &G断裁縦横 , [断裁枚数（定数）] = &G断裁枚数定数 , [断裁枚数（予備）] = &G断裁枚数予備 , \
                [断裁枚数合計] = &G断裁枚数合計 , [備考] = &G備考 , [迄時間] = &G迄時間 , [断裁不要] = &G断裁不要 , [サイズ作業ID] = &Gサイズ作業ID ,\
                [断裁備考] = &G断裁備考 ,[備考画像] = &G備考画像
            
            ジャンプ 行番号 = &Row
            &msgtxt = &G発注ID + " :の情報を更新しました。"

        else

            絞り込み [伝票ID] { &G伝票ID } , 終了状態 = &CHK

                条件 ( &G伝票ID = "" ) &G伝票ID = 0
                &countNum = #総件数 + 1
                &G発注ID = #文字列( &G伝票ID , 7 ) +"-"+ #文字列( &countNum　, 3 )

            解除 1


            行追加 終了状態 = &CHK ,　[発注ID] = &G発注ID , [発注日] = &G発注日 , [担当者ID] = &G担当者ID , [担当者名] = &G担当者名 ,\
                [伝票ID] = &G伝票ID , [前回ID] = &G前回ID , [得意先] = &G得意先 , [品名] = &G品名 , [伝票部数] = &G伝票部数 ,\
                [伝票サイズ] = &G伝票サイズ , [作業日] = &G作業日 , [機械名] = &G機械名 , [ロス要因1：顧客] = &Gロス要因1顧客 ,\
                [ロス要因2：自部署] = &Gロス要因2自部署 , [ロス要因3：他部署] = &Gロス要因3他部署 , [ロス要因4：開発・破損] = &Gロス要因4開発破損 ,\
                [用紙ID] = &G用紙ID , [銘柄] = &G銘柄 , [紙色] = &G紙色 , [サイズ] = &Gサイズ名称ID , [サイズ名] = &Gサイズ名 , [TY] = &GTY ,\
                [重さ] = &G重さ , [断裁サイズ作業ID] = &G断裁サイズ作業ID , [断裁サイズ名称ID] = &G断裁サイズ名称ID , [断裁サイズ名] = &G断裁サイズ名 ,\
                [断裁縦横] = &G断裁縦横 , [断裁枚数（定数）] = &G断裁枚数定数 , [断裁枚数（予備）] = &G断裁枚数予備 , \
                [断裁枚数合計] = &G断裁枚数合計 , [備考] = &G備考 , [迄時間] = &G迄時間 , [断裁不要] = &G断裁不要 , [サイズ作業ID] = &Gサイズ作業ID ,\
                [断裁備考] = &G断裁備考 , [備考画像] = &G備考画像
            
            ジャンプ 行番号 = 最終
            &msgtxt = &G発注ID + " :として、新規伝票情報を追加しました。"

        end

        確認 &msgtxt　/*←完了表示 */
手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　前回伝票番号の絞り込み
*■*:　前回伝票番号を、今回の伝票番号で絞り込んで、前回の引用をやり易くするための関数です
手続き定義開始 引用検索（）

    var 数値  { &cut , &count }

    解除 *

    絞り込み [伝票ID] { &G前回ID } , 終了状態 = &CHK

    *■*===========================================================
    *■*:　0件だったら解除    
    if ( #総件数 = 0 )
    
        解除 1
        &msgtxt = "前回情報で登録されたデータはありませんでした。"
    else
    *■*===========================================================
    *■*:　１件でもあったら絞り込むよ    
        &count = #総件数
        &msgtxt = #文字列( &count ) + " 件の該当データを絞り込みました。 "
    end

    確認 &msgtxt 

手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　選択行引用ボタン
*■*:　選択している行の情報を変数に呼び出します
*■*: 引数：boot: 0:用紙情報だけ引用します
*■*:            1:全て引用します
手続き定義開始 選択引用（ 数値　&boot ）

    if ( 0 < &boot )
        &G発注ID = [発注ID]　&G発注日 = [発注日] &G担当者ID = [担当者ID] &G担当者名 = [担当者名] &G伝票ID = [伝票ID] &G前回ID = [前回ID]　&G迄時間 = [迄時間]
        &G得意先 = [得意先] &G品名 = [品名] &G伝票部数 = [伝票部数] &G伝票サイズ = [伝票サイズ] &G作業日 = [作業日] &G機械名 = [機械名] 
    else
        
        /*▼用紙だけ引用の場合は、発注IDを持ってこないように初期化する */
        &G発注ID = ""
    end

    &Gロス要因1顧客 = [ロス要因1：顧客] 
    &Gロス要因2自部署 = [ロス要因2：自部署] &Gロス要因3他部署 = [ロス要因3：他部署] &Gロス要因4開発破損 = [ロス要因4：開発・破損] &G用紙ID = [用紙ID] &G銘柄 = [銘柄]
    &G紙色 = [紙色] &Gサイズ名称ID = [サイズ] &Gサイズ名 = [サイズ名] &GTY = [TY] &G重さ = [重さ] &G断裁サイズ作業ID = [断裁サイズ作業ID] &G断裁サイズ名称ID = [断裁サイズ名称ID]
    &G断裁サイズ名 = [断裁サイズ名] &G断裁縦横 = [断裁縦横] &G断裁枚数定数 = [断裁枚数（定数）] &G断裁枚数予備 = [断裁枚数（予備）] &G断裁枚数合計 = [断裁枚数合計]
    &G備考 = [備考] &G断裁不要 = [断裁不要]  &Gサイズ作業ID = [サイズ作業ID]　&G断裁備考 = [断裁備考] &G備考画像 = [備考画像]

    メソッド呼び出し @フォーム.変数変更()
手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　選択している行を呼び出して、レポート化します。
*■*:　
手続き定義開始 選択行レポート化（）

    if ( [発注ID] = "" )

        &msgtxt = "発注IDが無い登録情報は出力できません"
        確認 &msgtxt 
        手続き終了
    end

    /*▼大丈夫そうならその行を絞り込んでレポート出力 */
    絞り込み 行数 = 1
        レポート印刷 &用紙出庫伝票rpx , 会話 = しない , プレビュー = する , 終了状態 = &CHK
    解除　1
手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　用紙その用紙IDがあるのか簡易的に調査する手続きです
*■*:　
手続き定義開始 用紙ID調査（）

    var 数値  { &表num = 0 , &open = 0 , &AnsBoot }
    手続き実行 テーブルID調査( &GS用紙IDTbx , &共有参照 , &表num , &open )

    編集表　&表num

        並べ替え  { [銘柄] 昇順 }
        絞り込み [銘柄] { &G銘柄 } , 終了状態 = &CHK
        絞り込み [紙色] { &G紙色 } , 終了状態 = &CHK
        絞り込み [サイズ] { &Gサイズ名称ID } , 終了状態 = &CHK
        絞り込み [目] { &GTY } , 終了状態 = &CHK
        絞り込み [重さ] { &G重さ } , 終了状態 = &CHK

        &G用紙ID = [ID]
    
    条件 ( &G用紙ID = 0 ) &G用紙ID =""
    条件 ( &open = 0 )  終了 表 &表num    
    メソッド呼び出し @フォーム.変数変更()
手続き定義終了