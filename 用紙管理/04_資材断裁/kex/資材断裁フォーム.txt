*■***********************************************************
*■*** 
*■*** 資材断裁するためのkexです。
*■*** 　フォームに必要な設定と、登録処理などがあります。
*■*** 
*■***********************************************************
/*▼汎用 */
var 数値  { &CHK }
var 文字列{ &msgtxt }
var 数値  { &断裁TbxNum = 0 }

/*▼案件用変数 */
var 日時  { &days }
var 文字列{ &担当者 }
var 数値  { &担当ID }

/*▼アウトプット用変数 */
var 日時  { &OUT日付 }
var 文字列  { &OUT担当者 }
var 数値  { &OUT伝票番号 }
var 文字列  { &OUT得意先 }
var 文字列  { &OUT品名 }
var 文字列  { &OUTサイズ }
var 数値  { &OUT部数 }
var 数値  { &OUT用紙ID }
var 文字列  { &OUT銘柄 }
var 文字列  { &OUTサブ銘柄 }
var 数値  { &OUTサイズID }
var 文字列  { &OUTサイズ名 }
var 文字列  { &OUT目 }
var 数値  { &OUT重さ }
var 数値  { &OUT消費枚数 }

var 数値  { &OUT断裁後枚数定数 }
var 数値  { &OUT断裁後枚数予備 }
var 文字列  { &OUT備考 }
var 数値  { &OUT断裁後作業ID }
var 数値  { &OUT断裁名称ID }
var 文字列  { &OUT断裁サイズ名 }
var 文字列{ &OUT断裁縦横 }
var 数値  { &OUT作業時間 }
var 数値  { &OUT倍率 }
var 数値  { &OUT単価 }
var 文字列{ &OUT単価種 }
var 数値  { &OUT金額 }
var 数値  { &OUT全紙予備 }
var 数値  { &OUT全紙定数 }

*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　１行ごとの使用変数
var 文字列{ &S得意先 , &S品名 , &Sサイズ }
var 文字列{ &S銘柄 , &Sサブ銘柄 , &Sサイズ名 , &S目 }
var 数値  { &S部数 , &SサイズID , &S重さ }
var 数値  { &hoge }

var 文字列{ &S断裁サイズ }

/*▼ファイルパス */
var 文字列{ &資材断裁DPath = #データパス名 }

/*▼画像データ */
var 文字列{ &iconPng1        = &資材断裁DPath + "png\cut.png" }


*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　モーダルフォーム

/*▼モーダルフォーム用変数 */
var 数値  { &Num }
var 文字列{ &Str }
var 日時  { &Day }

/*▼用紙索引 */
var 文字列{ &S用紙IDmodal = &資材断裁DPath + "..\01_用紙ID\用紙ID索引フォーム.wfx"  }

/*▼用紙登録 */
var 文字列{ &S用紙登録modal = &資材断裁DPath + "..\01_用紙ID\用紙ID登録フォーム.wfx"  }

/*▼用紙サイズ */
var 文字列{ &S用紙サイズmodal = &資材断裁DPath + "..\..\System\modal\General\004_用紙_04_01_用紙サイズリスト_1_(基本).wfx" }

/*▼カレンダー */
var 文字列{ &Sカレンダーmodal = &資材断裁DPath + "..\..\System\modal\Calendar\INF_DatePicker.wfx" }

/*▼従業員 */
var 文字列{ &S社員リストmodal = &資材断裁DPath + "..\..\System\modal\General\03_01_社員情報【S】.wfx" }
var 文字列{ &N社員リストmodal = &資材断裁DPath + "..\..\System\modal\General\03_01_社員情報【N】.wfx" }
var 文字列{ &社員tbx = &資材断裁DPath + "..\..\System\tbx\03_01_シモダ社員情報.tbx" }


*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　外部参照
*■*:
*■*:　共有ファイルから、サイズIDを使ってサイズ名称を取ってきます。
/*▼用紙ID */
var 文字列{ &用紙IDTbx = &資材断裁DPath + "..\01_用紙ID\用紙ID.tbx" }

/*▼用紙サイズ */
var 文字列{ &用紙サイズTbx = &資材断裁DPath + "..\..\System\tbx\04_01_用紙サイズリスト.tbx" }

/*▼subMasta */
var 文字列{ &subMastaTbx = &資材断裁DPath + "..\..\submaster.tbx"}

/*▼データ保存先 */
var 文字列{ &資材断裁tbx = &資材断裁DPath + "資材断裁.tbx" }

*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　印刷日報に情報を書き出す用変数
var 文字列{ &Lib = &資材断裁DPath + "..\..\system\Lib\Lib_ライブラリ桐処理.cmx"}
ライブラリ &Lib

/*▼日報情報 */
var 文字列{ &印刷日報path = &資材断裁DPath + "..\..\日報\日報印刷.tbx" }
var 文字列{ &印刷日報pass = "innipou" }/*←入力権限のみ */

/*▼用紙情報 */
var 文字列{ &用紙TBXpath = &資材断裁DPath + "..\..\用紙管理\用紙.tbx" }
var 文字列{ &用紙TBXpass = "gyoumu" }/*←入力権限のみ */

*■*----------------------------------------------------------
*■*-----　基本処理
*■*----------------------------------------------------------
名札 MAIN

*
*■*----------------------------------------------------------
*■*-----　各オブジェクトイベント
*■*----------------------------------------------------------
    *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::
    *■*:　フォーム開始
    手続き定義開始　フォーム::フォーム開始(長整数　&表番号)

        /*▼呼び出した時の表番号を変数に保存する */
        &断裁TbxNum = &表番号
    
    手続き定義終了
    *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::
    *■*:　伝票番号から案件情報引用
    手続き定義開始　ラベル伝票番号::ソース値更新()

        
        手続き実行 伝票番号引用( [伝票番号] )

    手続き定義終了
    *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::
    *■*:　用紙IDから詳細
    手続き定義開始　テキスト用紙ID::ソース値更新()

        手続き実行 用紙ID引用( [用紙ID] )

    手続き定義終了
    *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::
    *■*:　倍率自動計算
    *■*:　倍率の項目に数値をいれたら、断裁後の枚数を自動的に計算します。
    手続き定義開始　倍率::ソース値更新()

        var 数値  { &chk1 , &定数倍 , &予備倍 }

        &定数倍 = [全紙（定数）] * [断裁倍率]
        &予備倍 = [全紙（予備）] * [断裁倍率]

        項目値代入 [断裁後枚数（定数）] = &定数倍  , [断裁後枚数（予備）] = &予備倍 

    手続き定義終了
    *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::
    *■*:　自動計算
    *■*:　合計値を自動計算します。
    手続き定義開始　全紙入力::ソース値更新()

        var 数値  { &chk1 , &合計枚数 }

        &合計枚数 = [全紙（定数）] + [全紙（予備）]
        項目値代入 [消費枚数] = &合計枚数

    手続き定義終了
    *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::
    *■*:　IDを入れると略称名も入れるようにしやす
    *■*:　
    手続き定義開始　担当ID入力::ソース値更新()

    var 数値  { &OPnum = 0  , &openF = 0 }
    var 文字列 { &mode = "共有更新" }

    手続き実行 表オープン（ &社員tbx , &mode , &OPnum , &openF ）
    編集表 &OPnum

        検索 [社員ID] { &担当ID } , 終了状態 = &CHK
        条件 ( &CHK = 1 ) 　&担当者 = [略称]
    
    条件 ( &openF = 0 ) 終了 表 &OPnum
    手続き定義終了
    *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::
    *■*:　IDを入れると略称名も入れるようにしやす
    *■*:　
    手続き定義開始　担当略称入力::ソース値更新()

    var 数値  { &OPnum = 0  , &openF = 0  }
    var 文字列 { &mode = "共有更新" }

    手続き実行 表オープン（ &社員tbx , &mode , &OPnum , &openF ）
    編集表 &OPnum

        検索 [略称] { &担当者 } , 終了状態 = &CHK
        条件 ( &CHK = 1 ) 　&担当ID = [社員ID]

    条件 ( &openF = 0 ) 終了 表 &OPnum
    手続き定義終了
    *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::
    *■*:　行追加コマンド　日付けなどの設定した変数を代入した行追加を行います。
    *■*:　
    手続き定義開始　行追加コマンド()

        /*▼エラー✓ */
        var 数値  { &er = 1 }
        
        /*▼日付けの入力が無い場合は処理しない。 */
        if ( &days = "" )
            &er = 0
            確認 "日付けを入力してください！"
        end
        /*▼担当者の情報が無い場合は処理しない。 */
        if ( &担当者 = "" .or &担当ID　= "" )
            &er = 0
            確認 "担当情報を埋めてください！"
        end

        /*▼問題無い場合は行追加する */
        if ( &er )
            行追加 終了状態 = &CHK , [日付] = &days , [担当者] = &担当者
        end

    手続き定義終了
*■*----------------------------------------------------------
*■*----- 各手続き
*■*----------------------------------------------------------
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　テーブルID調査
*■*:　ターゲットのテーブルが開いてても開いて無くても表番号を返す関数
*■*:　引数１：ターゲットのテーブルアドレス
*■*:　引数２：新規で開くステータス（専有とか参照とか、すでに開いている場合はそのまま）
*■*:　戻り値：表のＩＤ
*■*:　戻り値：既にオープンされている場合は１、新規でオープンなら０
   手続き定義開始 テーブルID調査（ 文字列 &tbx ,文字列　&モード　, 参照　数値　&表ID , 参照　数値　&openF ）
   
           *■*-------------------------------------
           *■*--  編集表番号取得（開いてても問題無し
           *■*-------------------------------------
           &表ID = #表番号取得( &tbx )
           if ( .not &表ID ) /*←未定義なら */
               
               表 &tbx , モード = &モード , 終了状態 = &CHK 
               &表ID = #表番号取得( &tbx )　　

            &openF = 0
        else
            &openF = 1
        end

手続き定義終了

*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　伝票情報取得処理
手続き定義開始 伝票番号引用（ 数値 &伝票番号 ）

    var 数値  { &表num = 0 , &open = 0 }

    手続き実行 テーブルID調査( &subMastaTbx , "共有参照" , &表num , &open )

    編集表　&表num

    検索 [伝票ＮＯ] { &伝票番号 } , 終了状態 = &CHK 

        if (&CHK = 1)
        
            &S部数 = [部数]
            &S得意先 = [得意先]
            &S品名 = [品名]
            &Sサイズ = [サイズ]
    
        end
    
    条件 ( &open = 0 )  終了 表 &表num
    
    *■*-------------------------------------
    *■*-- 本体の方に反映
    *■*-------------------------------------
    編集表 &断裁TbxNum
    メソッド呼び出し @フォーム.更新モード設定(0)

    /*▼伝票番号があれば訂正を行い、無ければ飛ばす */
    if ( &伝票番号 )
        行訂正 終了状態 = &CHK , [得意先] = &S得意先 , [品名] = &S品名 , [部数] = &S部数 , [サイズ] = &Sサイズ
    end

手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　伝票情報取得処理
手続き定義開始 伝票番号リセット（ ）

    項目値代入  [伝票番号] ="" , [得意先] ="" , [品名] ="" , [部数] ="" , [サイズ] =""

手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　用紙情報取得処理
手続き定義開始 用紙ID引用（ 数値 &用紙ID ）

    var 数値  { &表num = 0 , &open = 0 }
    手続き実行 テーブルID調査 ( &用紙IDTbx , "共有参照" , &表num , &open )

    編集表　&表num

    検索 [ID] { &用紙ID } , 終了状態 = &CHK 

        if (&CHK = 1)
        
            &S銘柄 = [銘柄]
            &Sサブ銘柄 = [紙色]
            &SサイズID = [サイズ]
            &Sサイズ名　= [サイズ名目]
            &S目     = [目]
            &S重さ    = [重さ]
       
        end
    
    条件 ( &open = 0 )  終了 表 &表num

    *■*-------------------------------------
    *■*-- 本体の方に反映
    *■*-------------------------------------
    編集表 &断裁TbxNum
    メソッド呼び出し @フォーム.更新モード設定(0)
    if (　&用紙ID　)
        行訂正 終了状態 = &CHK , [銘柄]= &S銘柄 , [サブ銘柄] = &Sサブ銘柄 , [サイズID] = &SサイズID ,\
            [サイズ名] = &Sサイズ名 , [目] = &S目 , [重さ] = &S重さ
    end

手続き定義終了

*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　断裁サイズのモーダルフォームを閉じる時の処理
*■*:　断裁サイズのIDが　0000　で返ってくる予定なので、その中から作業IDを抜き出すようにしたい
手続き定義開始　断裁サイズ_名称ID::ソース値更新()

    手続き実行 用紙サイズ引用名称ID( [断裁後名称ID] )

手続き定義終了
*■*:　用紙サイズ引用　（用紙サイズIDを受け取り、名称を返す）
手続き定義開始 用紙サイズ引用名称ID（ 数値 &名称ID ）

    var 数値  { &表num = 0 , &open = 0 }
    手続き実行 テーブルID調査 ( &用紙サイズTbx , "共有参照" , &表num , &open )
    編集表　&表num

        var 文字列{ &名称 , &縦横名称 }
        var 数値  { &作業ID }

    検索 [名称ID] { &名称ID } , 終了状態 = &CHK 

        if (&CHK = 1)
        
            &名称　= [名称]
            &作業ID = [作業ID]
            &縦横名称    = [縦横サイズ名]

        end
    
    条件 ( &open = 0 )  終了 表 &表num

    *■*-------------------------------------
    *■*-- 本体の方に反映
    *■*-------------------------------------
    編集表 &断裁TbxNum
    メソッド呼び出し @フォーム.更新モード設定(0)
    if (　&名称ID　)
        行訂正 終了状態 = &CHK , [断裁サイズ名] = &名称 , [断裁後サイズ作業ID] = &作業ID , [断裁縦横] = &縦横名称
    end

手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　断裁サイズのモーダルフォームを閉じる時の処理
*■*:　断裁サイズのIDが　0000　で返ってくる予定なので、その中から作業IDを抜き出すようにしたい
手続き定義開始　断裁サイズ_作業ID::ソース値更新()

    手続き実行 用紙サイズ引用作業ID（ [断裁後サイズ作業ID] ）

手続き定義終了
*■*:　用紙サイズ引用　（作業IDだったらざっくり返す）
手続き定義開始 用紙サイズ引用作業ID（ 数値 &作業ID ）

    var 数値  { &表num = 0 , &open = 0 }
    手続き実行 テーブルID調査 ( &用紙サイズTbx , "共有参照" , &表num , &open )
    編集表　&表num

        var 文字列{ &名称 , &縦横名称 }
        var 数値  { &名称ID }

    検索 [作業ID] { &作業ID } , 終了状態 = &CHK 

        if (&CHK = 1)
        
            &名称　= [名称]
            &名称ID = [名称ID]
            &縦横名称   = [縦横サイズ名]

        end

    条件 ( &open = 0 )  終了 表 &表num

    *■*-------------------------------------
    *■*-- 本体の方に反映
    *■*-------------------------------------
    編集表 &断裁TbxNum
    メソッド呼び出し @フォーム.更新モード設定(0)
    if (　&作業ID　)
        行訂正 終了状態 = &CHK , [断裁サイズ名] = &名称 , [断裁後名称ID] = &名称ID , [断裁縦横] = &縦横名称
    end

手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　資材日報登録処理
手続き定義開始 資材日報登録処理（）

    var 数値  { &表num = 0 , &open = 0 }
    var 数値  { &OUTNG = 0 }

    手続き実行 テーブルID調査 ( &資材断裁tbx , "共有更新" , &表num , &open )

    var 数値  { &OPnum = 0  , &openF = 0 , &OPnum2 = 0  , &openF2 = 0}
    var 文字列 { &mode = "共有更新" }
    
    利用者コード &印刷日報pass
    手続き実行 表オープン（ &印刷日報path , &mode , &OPnum ,　&openF ）

    利用者コード &用紙TBXpass
    手続き実行 表オープン（ &用紙TBXpath , &mode , &OPnum2 ,　&openF2 ）

    *■*-------------------------------------
    *■*-- 作業前検査
    *■*-------------------------------------
    編集表 &断裁TbxNum
    ジャンプ 行番号 = 1

    if ( &days ="" )
        &OUTNG = 1
        &msgtxt = "日付が入力されていません！"
        確認 &msgtxt    
        手続き終了
    end

    if ( &担当者 ="" )
        &OUTNG = 1
        &msgtxt = "担当者が入力されていません！"
        確認 &msgtxt    
        手続き終了
    end
    
    繰り返し（ .NOT #終端行 ）

        if ([用紙ID] = 0 .or [用紙ID] = ""　)
        
            &OUTNG = 1
            &msgtxt = #文字列( #行番号 ) +"行目の 用紙ID が空欄です！"
            確認 &msgtxt
            手続き終了

        end
        if ( [消費枚数] = 0 .or [消費枚数] = ""　)
        
            &OUTNG = 1
            &msgtxt = #文字列( #行番号 ) +"行目の 消費枚数 が空欄です！"
            確認 &msgtxt
            手続き終了

        end
        *■*:::::::::::::::::::::【　作業時間の判定追加　】:::::::::::::::::::::::::::
        if (　[作業時間（分）] = 0　)
        
            &OUTNG = 1
            &msgtxt = #文字列( #行番号 ) +"行目の 作業時間が空欄です！"
            確認 &msgtxt
            手続き終了

        end
        ジャンプ 行番号 = 次行 
    繰り返し終了 

    /*▼検査情報をクリアしたら登録処理しましょう */
    if ( &OUTNG = 0 )

        *■*-------------------------------------
        *■*-- １行ずつ変数に入れて本体に張り付ける
        *■*-------------------------------------


        編集表 &断裁TbxNum
        ジャンプ 行番号 = 1

        繰り返し（ .NOT #終端行 ）

            手続き実行 OUT変数リセット（）

            手続き実行 OUT変数格納（）

            手続き実行 入庫価格調査（ &OUT伝票番号 , &OUT用紙ID , &OUT単価 , &OUT金額 ）

            編集表 &表num

                手続き実行 OUT行追加保存（）
                手続き実行　印刷日報追加（）

                手続き実行　用紙tbx追加（）
            
            編集表 &断裁TbxNum
        
            ジャンプ 行番号 = 次行 
        繰り返し終了

        &msgtxt = "作業完了"
        確認 &msgtxt

        グループ選択解除
        行削除 * , 圧縮
        グループ選択

        条件 ( &open = 0 )  終了 表 &表num
        条件 ( &openF　= 0 ) 終了 表 &OPnum
        条件 ( &openF2　= 0 ) 終了 表 &OPnum2
    end

手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　コメント
手続き定義開始 OUT変数リセット（）

    &OUT日付 = ""　&OUT担当者 = ""　&OUT伝票番号 = ""　&OUT得意先 = ""　&OUT品名 = ""　&OUTサイズ = ""　&OUT部数 = ""　&OUT用紙ID = ""　&OUT銘柄 = ""　
    &OUTサブ銘柄 = ""　&OUTサイズID = ""　&OUTサイズ名 = ""　&OUT目 = ""　&OUT重さ = ""　&OUT消費枚数 = ""　&OUT断裁後作業ID = ""　&OUT断裁サイズ名 = ""
    &OUT断裁後枚数定数 = ""　&OUT断裁後枚数予備 = ""　&OUT備考 = ""　&OUT断裁名称ID ="" &OUT断裁縦横 = "" &OUT作業時間 = ""
    &OUT単価 = "" &OUT金額 = "" &OUT単価種 = ""

手続き定義終了
手続き定義開始 OUT変数格納（）

    &OUT日付 = &days 
    &OUT担当者 = &担当者
    &OUT伝票番号 = [伝票番号]
    &OUT得意先 = [得意先]
    &OUT品名 = [品名]
    &OUTサイズ = [サイズ]
    &OUT部数 = [部数]
    &OUT用紙ID = [用紙ID]
    &OUT銘柄 = [銘柄]
    &OUTサブ銘柄 = [サブ銘柄]
    &OUTサイズID = [サイズID]
    &OUTサイズ名 = [サイズ名]
    &OUT目 = [目]
    &OUT重さ = [重さ]
    &OUT消費枚数 = [消費枚数]
    &OUT全紙定数 = [全紙（定数）]
    &OUT全紙予備 = [全紙（予備）]
    &OUT断裁後作業ID = [断裁後サイズ作業ID]
    &OUT断裁サイズ名 = [断裁サイズ名]
    &OUT断裁後枚数定数 = [断裁後枚数（定数）]
    &OUT断裁後枚数予備 = [断裁後枚数（予備）]
    &OUT備考 = [備考]
    &OUT断裁名称ID = [断裁後名称ID]
    &OUT断裁縦横 = [断裁縦横]
    &OUT作業時間 = [作業時間（分）]
    &OUT倍率 = [断裁倍率]

手続き定義終了
手続き定義開始 OUT行追加保存（）

    行追加 終了状態 = &CHK , \
        [日付] = &OUT日付 , [担当者] = &OUT担当者 , [伝票番号] = &OUT伝票番号 , [得意先] = &OUT得意先 , [品名] = &OUT品名 , [サイズ] = &OUTサイズ , [部数] = &OUT部数 , \
        [用紙ID] = &OUT用紙ID , [銘柄] = &OUT銘柄 , [サブ銘柄] = &OUTサブ銘柄 , [サイズID] = &OUTサイズID , [サイズ名] = &OUTサイズ名 , [目] = &OUT目 , [重さ] = &OUT重さ , \
        [消費枚数] = &OUT消費枚数 , [断裁後サイズ作業ID] = &OUT断裁後作業ID , [断裁サイズ名] = &OUT断裁サイズ名 , [断裁後枚数（定数）] = &OUT断裁後枚数定数 , \
        [断裁後枚数（予備）] = &OUT断裁後枚数予備 , [備考] = &OUT備考 , [断裁後名称ID] = &OUT断裁名称ID , [断裁縦横] = &OUT断裁縦横 , [作業時間（分）] = &OUT作業時間 

手続き定義終了
*■*----------------------------------------------------------
*■*----- 印刷日報にも書き出す処理です。
*■*-----　ちょっと力技で何度も開いて閉じてを繰り返すが、１日２分ぐらいの処理なので良し。
*■*-----　いつか直す。
*■*----------------------------------------------------------
手続き定義開始 印刷日報追加（）

    編集表 &印刷日報path
        
        行追加 終了状態 = &CHK , [日付] = &OUT日付 , [係] = 6 , [ＣＤ] = 11 ,  [担当者] = &OUT担当者 ,\
        [伝票ＮＯ] = &OUT伝票番号 , [得意先] = &OUT得意先 , [品名] = &OUT品名 , [ミス] = 0 , [内容] = 320 , [時間] = &OUT作業時間

手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　用紙情報を格納する
*■*:　なんども繰り返すが､面倒なので合せ技1本でよしとする
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
手続き定義開始 用紙tbx追加（ ）

    編集表 &用紙TBXpath

    行追加 終了状態 = &CHK , [日付] = &OUT日付 ,  [ＣＤ] = 11 ,  [担当者] = &OUT担当者 ,\
    [伝票ＮＯ] = &OUT伝票番号 , [得意先] = &OUT得意先 , [品名] = &OUT品名 , [ミス] = 0 ,\
    [サイズ] = &OUTサイズID , [目] = &OUT目 , [重さ] = &OUT重さ , [実数] = &OUT全紙定数 , [予備数] = &OUT全紙予備  ,\
    [単価] = &OUT単価

    &msgtxt = "用紙情報を 用紙.tbxに反映させました。"
    *確認 &msgtxt

手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　入庫価格情報 を調査
*■*:　   用紙入庫から該当する単価を入手し､価格まで確認して返す｡
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
手続き定義開始 入庫価格調査（ 数値 &伝票番号 , 数値 &用紙ID , 参照 数値 &単価 , 参照 数値 &金額 ）

    var 数値  { &入庫OPnum = 0  , &入庫openF = 0 , &入庫枚数 = 0 }
    var 文字列{ &入庫mode = ”共有参照” }
    var 文字列{ &入庫tbx = &資材断裁DPath + "..\03_用紙入庫\用紙入庫.tbx" }
    
    手続き実行 表オープン（ &入庫tbx , &入庫mode , &入庫OPnum , &入庫openF ）
    編集表 &入庫OPnum

    絞り込み [伝票ID] { &伝票番号 } , 終了状態 = &CHK
    絞り込み [用紙ID] { &用紙ID } , 終了状態 = &CHK

    if ( 0 < #総件数 )
    
        &OUT単価種 = [単価種]
        &単価 = [入庫単価]
        &金額 = [金額]
        &入庫枚数 = [枚数]

        &単価 = #切り上げ( &金額 / &入庫枚数 , 2 ) 

        &msgtxt = #文字列( &金額 )+":::枚数" + #文字列( &入庫枚数 )
        確認 &msgtxt 

    else
    
        &msgtxt = ”該当する単価情報はありませんでした｡”
        確認 &msgtxt 
    
    end

    解除 *
    条件 ( &入庫openF = 0 ) 終了 表 &入庫OPnum

手続き定義終了