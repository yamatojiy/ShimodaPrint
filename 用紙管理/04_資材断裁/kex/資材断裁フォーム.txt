*■***********************************************************
*■*** 
*■*** 用紙IDを登録するフォームです。
*■*** 　引用が可能にします。　重複確認を先にします。　最後登録させます。
*■*** 
*■***********************************************************
/*▼汎用 */
var 数値  { &CHK }
var 文字列{ &msgtxt }
var 数値  { &断裁TbxNum = 0 }
var 数値  { &Num }

/*▼案件用変数 */
var 日時  { &days }
var 文字列{ &担当者 }



*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　１行ごとの使用変数
var 文字列{ &S得意先 , &S品名 , &Sサイズ }
var 文字列{ &S銘柄 , &Sサブ銘柄 , &Sサイズ名 , &S目 }
var 数値  { &S部数 , &SサイズID , &S重さ }

var 文字列{ &S断裁サイズ }

/*▼ファイルパス */
var 文字列{ &資材断裁DPath = #データパス名 }

/*▼画像データ */
var 文字列{ &iconPng1        = &資材断裁DPath + "png\cut.png" }


*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　モーダルフォーム
/*▼用紙ID */
var 文字列{ &S用紙IDmodal = &資材断裁DPath + "..\01_用紙ID\用紙ID索引フォーム.wfx"  }
/*▼用紙サイズ */
var 文字列{ &S用紙サイズmodal = &資材断裁DPath + "..\system\modal\【用紙ID】サイズ選択モーダル.wfx"  }

*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　外部参照
*■*:
*■*:　共有ファイルから、サイズIDを使ってサイズ名称を取ってきます。
/*▼用紙ID */
var 文字列{ &用紙IDTbx = &資材断裁DPath + "..\01_用紙ID\用紙ID.tbx" }


/*▼subMasta */
var 文字列{ &subMastaTbx = &資材断裁DPath + "..\..\submaster.tbx"}


*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　/*▼モーダルフォーム */

*■*----------------------------------------------------------
*■*-----　基本処理
*■*----------------------------------------------------------
名札 MAIN

*
*■*----------------------------------------------------------
*■*-----　各オブジェクトイベント
*■*----------------------------------------------------------
    *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::
    *■*:　フォーム開始
    手続き定義開始　フォーム::フォーム開始(長整数　&表番号)

        /*▼呼び出した時の表番号を変数に保存する */
        &断裁TbxNum = &表番号

    手続き定義終了
    *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::
    *■*:　伝票番号から案件情報引用
    手続き定義開始　ラベル伝票番号::ソース値更新()

        手続き実行 伝票番号引用( [伝票番号] )

    手続き定義終了
    *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::
    *■*:　用紙IDから詳細
    手続き定義開始　テキスト用紙ID::ソース値更新()

        手続き実行 用紙ID引用( [用紙ID] )

    手続き定義終了
*■*----------------------------------------------------------
*■*----- 各手続き
*■*----------------------------------------------------------
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　伝票情報取得処理
手続き定義開始 伝票番号引用（ 数値 &伝票番号 ）

    var 数値  { &表num = 0 }

    表 &subMastaTbx , モード = 参照
    &表num = #表番号取得( &subMastaTbx )
    編集表　&表num

    検索 [伝票ＮＯ] { &伝票番号 } , 終了状態 = &CHK 

        if (&CHK = 1)
        
            &S部数 = [部数]
            &S得意先 = [得意先]
            &S品名 = [品名]
            &Sサイズ = [サイズ]
       
        else
        
            &msgtxt = "入力された伝票番号はヒットしませんでした。"
            確認 &msgtxt

        end
    
    終了 表 &表num

    *■*-------------------------------------
    *■*-- 本体の方に反映
    *■*-------------------------------------
    編集表 &断裁TbxNum
    メソッド呼び出し @フォーム.更新モード設定(0)
    行訂正 終了状態 = &CHK , [得意先] = &S得意先 , [品名] = &S品名 , [部数] = &S部数 , [サイズ] = &Sサイズ

手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　用紙情報取得処理
手続き定義開始 用紙ID引用（ 数値 &用紙ID ）

    var 数値  { &表num = 0 }

    表 &用紙IDTbx , モード = 参照
    &表num = #表番号取得( &用紙IDTbx )
    編集表　&表num

    検索 [ID] { &用紙ID } , 終了状態 = &CHK 

        if (&CHK = 1)
        
            &S銘柄 = [銘柄]
            &Sサブ銘柄 = [紙色]
            &SサイズID = [サイズ]
            &Sサイズ名　= [サイズ名目]
            &S目     = [目]
            &S重さ    = [重さ]
       
        else
        
            &msgtxt = "入力された用紙IDにerror：2110"
            確認 &msgtxt

        end
    
    終了 表 &表num

    *■*-------------------------------------
    *■*-- 本体の方に反映
    *■*-------------------------------------
    編集表 &断裁TbxNum
    メソッド呼び出し @フォーム.更新モード設定(0)
    行訂正 終了状態 = &CHK , [銘柄]= &S銘柄 , [サブ銘柄] = &Sサブ銘柄 , [サイズID] = &SサイズID ,\
        [サイズ名] = &Sサイズ名 , [目] = &S目 , [重さ] = &S重さ


手続き定義終了