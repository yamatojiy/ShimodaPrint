*■**********************************************************
*■* : 伝票詳細取得処理 手続き実行処理
*■**********************************************************

/*▼基本となる変数はkexの方で宣言する事にする? */

*■* 基本変数
var 数値  { &CHK }
var 文字列{ &msgtxt }
var 数値  { &定義用ID }

*■* ファイル定義
var 文字列{ &DP = #一括パス名 }

*■* 伝票情報用変数（01伝票収支調査.tbx）
var 文字列{ &smastaDP = &DP + "..\..\..\Smasta.tbx" }
var 文字列{ &smastaPass = "fumio" }
var 文字列{ &基本tbx = &DP + "..\tbx\01伝票収支調査.tbx" }
var 数値  { &smastaN , &基本tbxID }

var 数値  { &伝票ID = 1036428 , &部数 , &頁数 , &前回伝票ID , &実行前回伝票ID  }
var 文字列{ &得意先 , &品名 , &営業所 , &担当営業 , &サイズ , &区分 }
var 日時  { &仕上日 }
var 通貨  { &組版積算 , &製版積算 , &印刷積算 , &仕上積算 , &用紙積算 , &組外積算 , &製外積算 , &印外積算 , &仕外積算 , &全外積算 , &送料 , &決定金額 }

*■* 伝票情報用変数（02用紙調査.tbx）
var 文字列{ &用紙DP = &DP + "..\..\..\..\用紙管理\03_用紙入庫\用紙入庫.tbx" }
var 文字列{ &用紙tbx = &DP + "..\tbx\02用紙調査.tbx" }
var 数値  { &用紙DPID , &用紙tbxID }

var 数値  { &用紙ID , &用紙サイズID , &用紙重さ , &用紙枚数 , &発注ID }
var 文字列{ &用紙銘柄 , &用紙紙色 , &用紙サイズ名 , &用紙目 }
var 通貨  { &用紙金額 , &用紙入庫単価 }

*■* 外注情報用変数（03外注調査.tbx）
var 文字列{ &外注DP = &DP + "..\..\..\..\外注管理\外注一覧.tbx" }
var 文字列{ &外注tbx = &DP + "..\tbx\03外注調査.tbx" }
var 文字列{ &外注Pass = "soto" }
var 数値  { &外注DPID , &外注tbxID }

var 数値  { &外注ID , &外注頁 , &外注枚数 , &外注ミス }
var 文字列{ &外注区分 , &外注先 , &外注内容 }
var 通貨  { &外注金額 }

*■* 日報情報用変数（04作業時間.tbx） 
var 文字列{ &日報1組版DP = &DP + "..\..\..\..\日報\日報組版.tbx" }
var 文字列{ &日報2製版DP = &DP + "..\..\..\..\日報\日報製版.tbx" }
var 文字列{ &日報3印刷DP = &DP + "..\..\..\..\日報\日報印刷.tbx" }
var 文字列{ &日報4仕上DP = &DP + "..\..\..\..\日報\日報仕上.tbx" }
var 文字列{ &日報5管理DP = &DP + "..\..\..\..\日報\日報管理.tbx" }
var 数値  { &組版DPID , &製版DPID , &印刷DPIP , &仕上DPIP , &管理DPIP }

var 文字列{ &作業時間tbx = &DP + "..\tbx\04作業時間.tbx" }
var 数値  { &作業時間tbxID }
var 文字列{ &日報Pass = "gyoumu" }
var 文字列{ &作業コードtbx = &DP + "..\tbx\◆作業コード.tbx" }
var 数値  { &作業コードID }

var 数値  { &作業区分ID , &作業内容ID , &ミス , &行ID }
var 文字列{ &作業区分名 , &作業内容名 , &担当者 }
var 数値  { &作業時間 }

*■* 材料情報用変数（05材料調査.tbx）
材料テーブル
材料変数



*■**********************************************************
*■* : MAIN処理
*■**********************************************************

名札 MAIN

    手続き実行 伝票基本情報取得( &伝票ID )
    &実行前回伝票ID = &前回伝票ID                   /*←前々回伝票に上書きされないように､実行用に分ける */
    手続き実行 伝票基本情報取得( &実行前回伝票ID )

    手続き実行 用紙情報取得( &伝票ID )
    手続き実行 用紙情報取得( &実行前回伝票ID )

    手続き実行 外注情報取得( &伝票ID )
    手続き実行 外注情報取得( &実行前回伝票ID )

    手続き実行 日報情報取得処理( &伝票ID )
    手続き実行 日報情報取得処理( &実行前回伝票ID )

終了

*■**********************************************************
*■* : 伝票基本情報取得()：Smastaから伝票情報を取得して基本tbxに記録
*■**********************************************************
手続き定義開始 伝票基本情報取得( 数値 &伝票id )

    var 数値  { &OpenFlag = 0 , &行状態 = 0 }

    *■* エラーチェック（伝票IDが未入力または0）
    if ( &伝票id = 0 .or &伝票id = #未定義 )
        手続き終了
    end

    *■* 表オープン（Smasta）
        &smastaN = #表番号取得( &smastaDP )
        if ( .not &smastaN )
            利用者コード "fumio"
            表 &smastaDP , モード = 共有参照 , 終了状態 = &CHK
            &smastaN = #表番号取得( &smastaDP )
        end

        編集表 &smastaN

    *■* 検索処理（伝票ID）
        検索 [伝票ＮＯ] { &伝票id } , 終了状態 = &CHK
        if ( &CHK = 0 )
            メッセージボックス "伝票が存在しません" , #文字列( &伝票ID , 0 )
            終了 表 &smastaN
            終了
        end
    
    *■* 区分判定（伝票IDから）
    var 数値 { &桁6 , &桁7 }
    var 文字列 { &区分 = "" }

    &桁7 = #数値( #部分列( #文字列( &伝票id , 0 ) , 1 , 1 ) )  /* 7桁目：1文字目 */
    &桁6 = #数値( #部分列( #文字列( &伝票id , 0 ) , 2 , 1 ) )  /* 6桁目：2文字目 */

    if ( &桁7 = 9 )
        if ( &桁6 = 1 )
            &区分 = "枚葉"
        else if ( &桁6 = 2 )
            &区分 = "冊子"
        else if ( &桁6 = 3 )
            &区分 = "新聞"
        else if ( &桁6 = 4 )
            &区分 = "複写"
        end
    else
        if ( &桁7 = 1 )
            &区分 = "枚葉"
        else if ( &桁7 = 2 )
            &区分 = "冊子"
        else if ( &桁7 = 3 )
            &区分 = "新聞"
        else if ( &桁7 = 4 )
            &区分 = "複写"
        end
    end

    *■* 項目取得（確定優先含む）
        &得意先 = [得意先]
        &品名 = [品名]
        &営業所 = [営業所]
        &担当営業 = [担当営業]

    *■* 部数取得（確定部数があれば優先）
        if ( [確定部数] <> #未定義 )
            &部数 = [確定部数]
        else
            &部数 = [部数]
        end

    *■* 頁数取得（確定頁数があれば優先）
        if ( [確定頁数] <> #未定義 )
            &頁数 = #数値( [確定頁数] )
        else
            &頁数 = #数値( [頁数] )
        end

    *■* 仕上日取得（確定仕上日があれば優先）
        if ( [確定仕上日] <> #未定義 )
            &仕上日 = [確定仕上日]
        else
            &仕上日 = [仕上日]
        end

        &サイズ = [サイズ]

    *■* 積算取得
        &組版積算 = [組版積算]
        &製版積算 = [製版積算]
        &印刷積算 = [印刷積算]
        &仕上積算 = [仕上積算]
        &用紙積算 = [用紙積算]
        &組外積算 = [組外積算]
        &製外積算 = [製外積算]
        &印外積算 = [印外積算]
        &仕外積算 = [仕外積算]
        &全外積算 = [全外積算]
        &送料 = [送料]
        &決定金額 = [決定金額]

    *■* 前回伝票取得（文字列→数値）
        &前回伝票ID = #数値([前回伝票])

        終了 表 &smastaN

    *■* 表オープン（基本tbx）
        &基本tbxID = #表番号取得( &基本tbx )
        if ( .not &基本tbxID )
            利用者コード ""
            表 &基本tbx , モード = 専有 , 終了状態 = &CHK
            &基本tbxID = #表番号取得( &基本tbx )
        end

    編集表 &基本tbxID

    *■* 検索（伝票ID）
    検索 [伝票ID] { &伝票id } , 終了状態 = &CHK

    if ( &CHK = 1 )
        *■* 行訂正（既存行を上書き）
        行訂正 終了状態 = &CHK , \
            [得意先] = &得意先 , \
            [区分] = &区分 , \
            [品名] = &品名 , \
            [営業所] = &営業所 , \
            [担当営業] = &担当営業 , \
            [部数] = &部数 , \
            [サイズ] = &サイズ , \
            [頁数] = &頁数 , \
            [仕上日] = &仕上日 , \
            [組版積算] = &組版積算 , \
            [製版積算] = &製版積算 , \
            [印刷積算] = &印刷積算 , \
            [仕上積算] = &仕上積算 , \
            [用紙積算] = &用紙積算 , \
            [組外積算] = &組外積算 , \
            [製外積算] = &製外積算 , \
            [印外積算] = &印外積算 , \
            [仕外積算] = &仕外積算 , \
            [全外積算] = &全外積算 , \
            [送料] = &送料 , \
            [決定金額] = &決定金額 , \
            [前回伝票ID] = &前回伝票ID

    else
        *■* 行追加（新規登録）
        行追加 終了状態 = &CHK , \
            [伝票ID] = &伝票id , \
            [区分] = &区分 , \
            [得意先] = &得意先 , \
            [品名] = &品名 , \
            [営業所] = &営業所 , \
            [担当営業] = &担当営業 , \
            [部数] = &部数 , \
            [サイズ] = &サイズ , \
            [頁数] = &頁数 , \
            [仕上日] = &仕上日 , \
            [組版積算] = &組版積算 , \
            [製版積算] = &製版積算 , \
            [印刷積算] = &印刷積算 , \
            [仕上積算] = &仕上積算 , \
            [用紙積算] = &用紙積算 , \
            [組外積算] = &組外積算 , \
            [製外積算] = &製外積算 , \
            [印外積算] = &印外積算 , \
            [仕外積算] = &仕外積算 , \
            [全外積算] = &全外積算 , \
            [送料] = &送料 , \
            [決定金額] = &決定金額 , \
            [前回伝票ID] = &前回伝票ID

        if ( &CHK <> 1 )
            メッセージボックス "行追加に失敗しました" , "状態=" + #文字列( &CHK , 0 )
        end
    end

    終了 表 &基本tbxID

手続き定義終了
*■**********************************************************
*■* : 用紙情報取得（指定伝票IDの用紙行をDPから読み取り、用紙tbxに記録）
*■**********************************************************
手続き定義開始 用紙情報取得( 数値 &伝票id )

    &定義用ID = &伝票id

    *■* エラーチェック（伝票IDが未入力または0）
    if ( &伝票id = 0 .or &伝票id = #未定義 )
        手続き終了
    end

    *■* 表オープン（用紙DP）
        &用紙DPID = #表番号取得( &用紙DP )
        if ( .not &用紙DPID )
            表 &用紙DP , モード = 共有参照 , 終了状態 = &CHK
            &用紙DPID = #表番号取得( &用紙DP )
        end
        編集表 &用紙DPID

    *■* 絞り込み（指定伝票ID）
        絞り込み [伝票ID] { &伝票id }

    *■* 表オープン（用紙tbx）
        &用紙tbxID = #表番号取得( &用紙tbx )
        if ( .not &用紙tbxID )
            表 &用紙tbx , モード = 専有 , 終了状態 = &CHK
            &用紙tbxID = #表番号取得( &用紙tbx )
        end
        編集表 &用紙tbxID
        検索条件登録 条件名 = "伝票発注一致", \
        { [伝票ID] = &定義用ID , [発注ID] = &発注ID }
       
    *■* 繰り返し（絞り込まれた全用紙行を転記）
        編集表 &用紙DPID
        繰り返し ( .not #終端行 )

            &用紙ID = [用紙ID]
            &発注ID = [発注ID]
            &用紙銘柄 = [銘柄]
            &用紙紙色 = [紙色]
            &用紙サイズID = [サイズID]
            &用紙サイズ名 = [サイズ名]
            &用紙目 = [目]
            &用紙重さ = [重さ]
            &用紙枚数 = [枚数]
            &用紙金額 = [金額]
            &用紙入庫単価 = [入庫単価]

    *■* 行訂正または追加（伝票ID単独で検索）
        編集表 &用紙tbxID
        絞り込み 条件名 = "伝票発注一致"
        if ( #総件数 = 1 )
            行訂正 終了状態 = &CHK , \
                [用紙ID] = &用紙ID ,\
                [発注ID] = &発注ID ,\
                [用紙銘柄] = &用紙銘柄 , \
                [用紙紙色] = &用紙紙色 , \
                [用紙サイズID] = &用紙サイズID , \
                [用紙サイズ名] = &用紙サイズ名 , \
                [用紙目] = &用紙目 , \
                [用紙重さ] = &用紙重さ , \
                [用紙枚数] = &用紙枚数 , \
                [用紙金額] = &用紙金額 , \
                [用紙入庫単価] = &用紙入庫単価
            絞り込み解除 1        
        else
            絞り込み解除 1
            行追加 終了状態 = &CHK , \
                [伝票ID] = &伝票id , \
                [用紙ID] = &用紙ID , \
                [発注ID] = &発注ID ,\
                [用紙銘柄] = &用紙銘柄 , \
                [用紙紙色] = &用紙紙色 , \
                [用紙サイズID] = &用紙サイズID , \
                [用紙サイズ名] = &用紙サイズ名 , \
                [用紙目] = &用紙目 , \
                [用紙重さ] = &用紙重さ , \
                [用紙枚数] = &用紙枚数 , \
                [用紙金額] = &用紙金額 , \
                [用紙入庫単価] = &用紙入庫単価
        end
        編集表 &用紙DPID
        ジャンプ 行番号 = 次行

        繰り返し終了

    終了 表 &用紙DPID
    終了 表 &用紙tbxID

手続き定義終了
*■**********************************************************
*■* : 外注情報取得：横持ち外注DPから縦データ形式に展開し外注tbxへ記録
*■**********************************************************
手続き定義開始 外注情報取得( 数値 &伝票id )

    *■* エラーチェック（伝票IDが未入力または0）
    if ( &伝票id = 0 .or &伝票id = #未定義 )
        手続き終了
    end

    &定義用ID = &伝票id

    *■* 表オープン（外注DP）
        &外注DPID = #表番号取得( &外注DP )
        if ( .not &外注DPID )
            利用者コード &外注Pass
            表 &外注DP , モード = 共有参照 , 終了状態 = &CHK
            &外注DPID = #表番号取得( &外注DP )
        end
        編集表 &外注DPID

    *■* 表オープン（外注tbx）
        &外注tbxID = #表番号取得( &外注tbx )
        if ( .not &外注tbxID )
            表 &外注tbx , モード = 専有 , 終了状態 = &CHK
            &外注tbxID = #表番号取得( &外注tbx )
        end
        編集表 &外注tbxID
        検索条件登録 条件名 = "外注情報一致", \
        { [伝票ID] = &定義用ID , [外注ID] = &外注ID }

    *■* 外注区分１～９の繰り返し処理

    var 数値 { &回数 = 1 }
    編集表 &外注DPID
    絞り込み [伝票ＮＯ] { &伝票id } , 終了状態 = &CHK

    繰り返し &回数 = 1 , 9

        *■* 初期化
        &外注ID = ""  &外注頁 = ""  &外注枚数 = ""  &外注ミス = ""
        &外注区分 = ""  &外注先 = ""  &外注内容 = ""
        &外注金額 = ""

        var 文字列 { &番号 = #全角(#文字列( &回数 ) ) }
        var 通貨  { &予定M = "" , &決定M = "" }
        
            *■* 変数代入
            コマンド "&外注区分 = [区分" + &番号 + "]"
            コマンド "&外注先 = [外注先" + &番号 + "]"
            コマンド "&外注内容 = [内容" + &番号 + "]"
            コマンド "&外注頁 = #数値( [頁数" + &番号 + "] )"
            コマンド "&外注枚数 = #数値( [枚数" + &番号 + "] )"
            コマンド "&外注ミス = #数値( [ミス" + &番号 + "] )"
            コマンド "&予定M = [予定金額" + &番号 + "]"
            コマンド "&決定M = [決定金額" + &番号 + "]"

            &外注ID = &回数

            *■* 空の区分はスキップ
            if ( &外注区分 = "" )
                繰り返し中止
            end

            if ( &決定M <> #未定義 )
                &外注金額 = &決定M
            else
                &外注金額 = &予定M
            end

        *■* 行追加（伝票ID + 外注IDで固有とする構成）
        編集表 &外注tbxID
        絞り込み 条件名 = "外注情報一致"
        if ( #総件数 = 1 )
            行訂正 終了状態 = &CHK , \
                [外注ID] = &外注ID , \
                [外注区分] = &外注区分 , \
                [外注先] = &外注先 , \
                [外注内容] = &外注内容 , \
                [外注頁] = &外注頁 , \
                [外注枚数] = &外注枚数 , \
                [外注金額] = &外注金額 , \
                [外注ミス] = &外注ミス
            絞り込み解除 1     
        else
            絞り込み解除 1     
            行追加 終了状態 = &CHK , \
                [伝票ID] = &伝票id , \
                [外注ID] = &外注ID , \
                [外注区分] = &外注区分 , \
                [外注先] = &外注先 , \
                [外注内容] = &外注内容 , \
                [外注頁] = &外注頁 , \
                [外注枚数] = &外注枚数 , \
                [外注金額] = &外注金額 , \
                [外注ミス] = &外注ミス
        end
        編集表 &外注DPID
    繰り返し終了

    終了 表 &外注DPID
    終了 表 &外注tbxID

手続き定義終了
*■**********************************************************
*■* : 日報情報取得：5種の日報から作業情報を抽出し作業時間tbxに格納
*■**********************************************************
手続き定義開始 日報情報取得処理（ 数値 &伝票id ）

    *■* エラーチェック（伝票IDが未入力または0）
    if ( &伝票id = 0 .or &伝票id = #未定義 )
        手続き終了
    end

    &定義用ID = &伝票id

    *■* 表オープン：作業時間tbx
    &作業時間tbxID = #表番号取得( &作業時間tbx )
    if ( .not &作業時間tbxID )
        表 &作業時間tbx , モード = 専有 , 終了状態 = &CHK
        &作業時間tbxID = #表番号取得( &作業時間tbx )
    end
    編集表 &作業時間tbxID
    *■* 重複防止：検索条件登録（[行][伝票ID][担当者]）
    検索条件登録 条件名 = "日報情報一致", \
        { [行ID] = &行ID , [伝票ID] = &定義用ID , [担当者] = &担当者 }

    *■* 表オープン：作業コードtbx
    &作業コードID = #表番号取得( &作業コードtbx )
    if ( .not &作業コードID )
        表 &作業コードtbx , モード = 共有参照 , 終了状態 = &CHK
        &作業コードID = #表番号取得( &作業コードtbx )
    end
    編集表 &作業コードID

    *■* 各日報処理（組版〜管理）
    手続き実行 日報処理本体( &日報1組版DP , &組版DPID , &伝票id )
    手続き実行 日報処理本体( &日報2製版DP , &製版DPID , &伝票id )
    手続き実行 日報処理本体( &日報3印刷DP , &印刷DPIP , &伝票id )
    手続き実行 日報処理本体( &日報4仕上DP , &仕上DPIP , &伝票id )
    手続き実行 日報処理本体( &日報5管理DP , &管理DPIP , &伝票id )

    *■* 終了処理
    終了 表 &作業コードID
    終了 表 &作業時間tbxID

手続き定義終了 
*■**********************************************************
*■* : 共通処理：1日報表の中から作業を抽出し作業時間表へ記録
*■**********************************************************
手続き定義開始 日報処理本体( 文字列 &日報path , 参照 数値 &日報ID , 数値 &伝票id )

    &定義用ID = &伝票id

    *■* 日報表オープン（gyoumuコードで参照）
    &日報ID = #表番号取得( &日報path )
    if ( .not &日報ID )
        利用者コード &日報Pass
        表 &日報path , モード = 共有参照 , 終了状態 = &CHK
        &日報ID = #表番号取得( &日報path )
    end
    編集表 &日報ID

    *■* 絞り込み：伝票IDで対象行を限定
    絞り込み [伝票ＮＯ] { &伝票id } , 終了状態 = &CHK

    *■* 行ごとの処理
    繰り返し ( .not #終端行 )

        &行ID = #行番号
        &作業内容ID = [内容]
        &ミス = [ミス]
        &担当者 = [担当者]
        &作業時間 = [時間]

        *■* 作業内容IDがある場合のみコード参照
        if ( &作業内容ID <> #未定義 )
            編集表 &作業コードID
            絞り込み [作業内容ID] { &作業内容ID }
            if ( #総件数 = 1 )
                &作業区分ID = [作業区分ID]
                &作業区分名 = [作業区分名]
                &作業内容名 = [作業内容名]
            end
            絞り込み解除 1
        end

        *■* 登録先編集表へ移動
        編集表 &作業時間tbxID
        絞り込み 条件名 = "日報情報一致"

        if ( #総件数 = 1 )
            行訂正 終了状態 = &CHK , \
                [作業区分ID] = &作業区分ID , \
                [作業区分名] = &作業区分名 , \
                [作業内容ID] = &作業内容ID , \
                [作業内容名] = &作業内容名 , \
                [ミス] = &ミス , \
                [作業時間] = &作業時間
        else
            行追加 終了状態 = &CHK , \
                [行ID] = &行ID , \
                [伝票ID] = &伝票id , \
                [担当者] = &担当者 , \
                [作業区分ID] = &作業区分ID , \
                [作業区分名] = &作業区分名 , \
                [作業内容ID] = &作業内容ID , \
                [作業内容名] = &作業内容名 , \
                [ミス] = &ミス , \
                [作業時間] = &作業時間
        end
        絞り込み解除 1

        編集表 &日報ID
        ジャンプ 行番号 = 次行

    繰り返し終了

    終了 表 &日報ID

手続き定義終了



















