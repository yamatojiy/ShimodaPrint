*■***********************************************************
*■*** 【cmx】Smasta入力作業.cmx
*■***
*■*** 各伝票番号を判別して登録作業を行うCMXです
*■***
*■***
*■***********************************************************

/*▼汎用変数 */
var 文字列{ &msgtxt }
var 数値  { &TaskTbxNum }
var 数値  { &印刷区分n }

/*▼Smastaに関するテーブル情報 */
var 文字列{ &SmastaFaileName = "Smasta.tbx" }
var 文字列{ &SmastaAddress = #データパス名 + "..\" }
var 文字列{ &SmastaTbx = &SmastaAddress + &SmastaFaileName }

var 文字列{ &SmastaPass = "fumio" }

*■*----------------------------------------------------------
*■*-----　基本処理
*■*----------------------------------------------------------
名札 MAIN

    var 数値  { &Smasta表num }

    /*▼Smastaが開いてなければ開きます。 */
    &Smasta表num =　#表番号取得( &SmastaTbx )
    if ( &Smasta表num ="" )
        利用者コード &SmastaPass
        表 &SmastaTbx , モード = 共有更新
        &Smasta表num = #表番号取得( &SmastaTbx )
    end

    編集表 &Input表ID
        ジャンプ 行番号 = 1
        *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
        *■*:　繰り返し処理
        繰り返し（ .NOT #終端行 ）
            手続き実行 変数リセット()

            手続き実行 変数格納 ()
            
            編集表 &Smasta表num
                手続き実行 Smasta格納（）
            
            編集表 &Input表ID
        
            ジャンプ 行番号 = 次行
        
        繰り返し終了

    &msgtxt = "Smasta　登録完了！ "
    確認 &msgtxt
 
終了
*■*----------------------------------------------------------
*■*-----　手続き定義
*■*----------------------------------------------------------
手続き定義開始 Smasta格納（）

    検索 [伝票ＮＯ] {　&伝票ID } , 終了状態 = &CHK 
    if ( &CHK = 1 )
        &ansCode = "既にこの番号はSmastaに登録されています！ :" + #文字列( &伝票ID )
        &AnsSTR = "NG"

        編集表 &Input表ID

            行訂正 終了状態 = &CHK , [結果] = &AnsSTR , [詳細] = &ansCode

        編集表 &Smasta表num
    
    else
        手続き実行 Smasta格納行追加（）
    end

    終了 表 10

手続き定義終了
*■*----------------------------------------------------------
*■*----- 変数処理
*■*----------------------------------------------------------
手続き定義開始 変数リセット（）
    &日付 ="" &仕上日 =""
    &区分 ="" &社員ID ="" &支店ID ="" &伝票ID ="" &得意先ID  ="" &部数 =""
    &担当営業名 ="" &支店名 ="" &得意先名 ="" &品名 ="" &サイズ ="" &頁数 ="" &備考 ="" &前回伝票IDs =""
    &組版積算 ="" &製版積算 ="" &印刷積算 ="" &仕上積算 ="" &小計 ="" &粗利 ="" &用紙積算 ="" &送料 ="" &外注 =""
    &AnsSTR ="" &ansCode =""
手続き定義終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　格納用
手続き定義開始 変数格納（）

    編集表 &Input表ID
        &日付 = [日付]
        &区分 = [区分]
        &社員ID = [社員ID]
        &担当営業名 = [担当営業名]
        &支店ID = [支店ID]
        &支店名 = [支店名]
        &伝票ID = [伝票ID]
        &得意先ID = [得意先ID]
        &得意先名 = [得意先名]
        &品名 = [品名]
        &部数 = [部数]
        &サイズ = [サイズ]
        &頁数 = [頁数]
        &仕上日 = [仕上日]
        &組版積算 = [組版積算]
        &製版積算 = [製版積算]
        &印刷積算 = [印刷積算]
        &仕上積算 = [仕上積算]
        &粗利 = [粗利]
        &用紙積算 = [用紙積算]
        &送料 = [送料]
        &備考 = [備考]
        &前回伝票IDs = [前回伝票IDs]

    編集表 &Smasta表num

手続き定義終了
*■*----------------------------------------------------------
*■*----- Smastaに変数情報を追加する
*■*----------------------------------------------------------
手続き定義開始 Smasta格納行追加（）

    行追加 終了状態 = &CHK , [日付] = &日付　, [区分] = &区分 , [営業] = &社員ID , [担当営業] = &担当営業名 , [所] = &支店ID ,\
        [営業所] = &支店名 , [伝票ＮＯ] = &伝票ID , [得意先ＣＤ] = &得意先ID , [得意先] = &得意先名 , [品名] = &品名 ,[部数] = &部数 ,\
        [サイズ] = &サイズ , [頁数] = &頁数 , [仕上日] = &仕上日 , [組版積算] = &組版積算 , [製版積算] = &製版積算 ,[印刷積算] = &印刷積算 ,\
        [仕上積算] = &仕上積算 , [積算粗利] = &粗利 , [用紙積算] = &用紙積算 , [送料] = &送料 , [備考] = &備考 , [前回伝票] = &前回伝票IDs ,\
        [入力日] = #日時値

手続き定義終了