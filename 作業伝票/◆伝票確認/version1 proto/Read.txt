*■***********************************************************
*■*** 伝票番号から、必要情報を全て読み取ってくる一括処理です
*■***********************************************************
/*▼参照営業テーブル */
var 数値  { &伝票Num , &区分Num }
var 文字列{ &営業所s }
var 文字列{ &営業tbx }
var 文字列{ &msgtxt }


/*▼アドレス */
var 文字列{ &SmastaTbx }

/*▼本社 */
var 文字列{ &本社枚葉tbx ="X:\作業伝票\本社-伝票\伝票枚葉.tbx" }
var 文字列{ &本社冊子tbx ="X:\作業伝票\本社-伝票\伝票冊子.tbx" }
var 文字列{ &本社新聞tbx ="X:\作業伝票\本社-伝票\伝票新聞.tbx" }
var 文字列{ &本社複写tbx ="X:\作業伝票\本社-伝票\伝票複写.tbx" }

/*▼支店 */
var 文字列{ &支店枚葉tbx ="X:\作業伝票\熊本支店-伝票\支店-伝票枚葉.tbx"}
var 文字列{ &支店冊子tbx ="X:\作業伝票\熊本支店-伝票\支店-伝票冊子.tbx"}
var 文字列{ &支店新聞tbx ="X:\作業伝票\熊本支店-伝票\支店-伝票新聞.tbx"}
var 文字列{ &支店複写tbx ="X:\作業伝票\熊本支店-伝票\支店-伝票複写.tbx"}

/*▼九コン */
var 文字列{ &九コン枚葉tbx }
var 文字列{ &九コン冊子tbx }
var 文字列{ &九コン新聞tbx }
var 文字列{ &九コン複写tbx }

*■*----------------------------------------------------------
*■*-----　関数
*■*----------------------------------------------------------
名札 MAIN

    /*▼伝票番号から使用するテーブルを見つける */
    手続き実行 伝票番号調査（）

    /*▼テーブルから伝票番号を検索する */
    手続き実行 営業伝票情報格納（　&営業tbx ）

    /*▼新しく行を追加して保存する */
    手続き実行 行追加する（）




終了
*■*----------------------------------------------------------
*■*-----　伝票番号調査
*■*-　伝票番号を分解して、必要な情報を引っこ抜く。
*■*----------------------------------------------------------
手続き定義開始 伝票番号調査（）

    var 数値  { &分岐ID }
    var 文字列{ &分岐str , &IDstr }

    &分岐str = #文字列( &検索伝票No )
    &分岐str = #文字列反転( &分岐str )
    &IDstr = #右側文字列( &分岐str , 1 )

    &分岐ID = #数値 ( &IDstr )

    /*▼初期化 */
    &営業tbx = ""

    /*▼ケース調査 */
    ケース開始
        ケース（ &分岐ID　= 1 ）
            &営業tbx = &本社枚葉tbx
        ケース（ &分岐ID　= 2 ）
            &営業tbx = &本社冊子tbx
        ケース（ &分岐ID　= 3 ）
            &営業tbx = &本社新聞tbx
        ケース（ &分岐ID　= 4 ）
            &営業tbx = &本社複写tbx
        ケース（ &分岐ID　= 5 ）
            &営業tbx = &支店枚葉tbx
        ケース（ &分岐ID　= 6 ）
            &営業tbx = &支店冊子tbx
        ケース（ &分岐ID　= 7 ）
            &営業tbx = &支店新聞tbx
        ケース（ &分岐ID　= 8 ）
            &営業tbx = &支店複写tbx
     ケース終了   

手続き定義終了
*■*----------------------------------------------------------
*■*-----　受け取ったテーブル情報を開いて、必要データを抜き取る。
*■*----------------------------------------------------------
手続き定義開始 営業伝票情報格納（文字列 &営業情報tbx ）

    if (&営業情報tbx ="" )
        確認 "表情報が空欄のため、処理を中止します。"
    end

    表 &営業情報tbx  , 表番号 = 15 , モード = 共有更新

        検索 [No.] { &検索伝票No } , 終了状態 = &CHK
        /*▼ちゃんと該当するなら処理する */
        if (&CHK = 1 )

            &伝票No数 = [No.]
            &作業日日 = [入力日]
            &品名文 = [品名]
            &得意先文 = [得意先]
            &部数数 = [確定枚数・部数]
            &組版代数 = [組版]
            &Scan・PS代数 = [Scan・PS]
            &印刷数 = [印刷]
            &製本数 = [製本]
            &小計数 = [小計]
            &粗利数 = [粗利]
            &用紙数 = [用紙]
            &外注数 = [外注]
            &送料数 = [送料]
            &合計数 = [合計]
            &単価数 = [単価]
            &売上総額数 = [売上総額]
            &前回伝票No数 = [前回伝票No]

        else
            &msgtxt ="該当の伝票番号がヒットしませんでした。処理を飛ばします"
            確認 &msgtxt
        end
        *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
        *■*:　続いて前回伝票番号をそのまま検索し、変数に納めます。
        検索 [No.] { &前回伝票No数 }　, 終了状態 = &CHK 
        /*▼ちゃんと該当するなら処理する */
        if (&CHK = 1 )

            &前作業日日 = [入力日]
            &前部数数 = [確定枚数・部数]
            &前組版代数 = [組版]
            &前Scan・PS代数 = [Scan・PS]
            &前印刷代数 = [印刷]
            &前製本代数 = [製本]
            &前小計代数 = [小計]
            &前粗利代数 = [粗利]
            &前用紙代数 = [用紙]
            &前外注代数 = [外注]
            &前送料代数 = [送料]
            &前合計数 = [合計]
            &前売上総額数 = [売上総額]

        else
            &msgtxt ="前回伝票番号が存在しませんでした。"
            確認 &msgtxt
        end

    終了 表 15
手続き定義終了
*■*----------------------------------------------------------
*■*-----　データ挿入処理
*■*----------------------------------------------------------
手続き定義開始 行追加する（）

    編集表 &表No
    /*▼ここで行を追加する */
    行追加 終了状態 = &CHK ,　 [伝票No] = &伝票No数	,\
        [作業日] = &作業日日	,\
        [品名] = &品名文	,\
        [得意先] = &得意先文	,\
        [部数] = &部数数	,\
        [ページ数] = &ページ数数	,\
        [組版代] = &組版代数	,\
        [Scan・PS代] = &Scan・PS代数	,\
        [印刷] = &印刷数	,\
        [製本] = &製本数	,\
        [小計] = &小計数	,\
        [粗利] = &粗利数	,\
        [用紙] = &用紙数	,\
        [外注] = &外注数	,\
        [送料] = &送料数	,\
        [合計] = &合計数	,\
        [単価] = &単価数	,\
        [売上総額] = &売上総額数	,\
        [前回伝票No] = &前回伝票No数	,\
        [前作業日] = &前作業日日	,\
        [前部数] = &前部数数	,\
        [前ページ数] = &前ページ数数	,\
        [前組版代] = &前組版代数	,\
        [前Scan・PS代] = &前Scan・PS代数	,\
        [前印刷代] = &前印刷代数	,\
        [前製本代] = &前製本代数	,\
        [前小計代] = &前小計代数	,\
        [前粗利代] = &前粗利代数	,\
        [前用紙代] = &前用紙代数	,\
        [前外注代] = &前外注代数	,\
        [前送料代] = &前送料代数	,\
        [前合計] = &前合計数	,\
        [前売上総額] = &前売上総額数

    ジャンプ 行番号 = 最終

手続き定義終了




