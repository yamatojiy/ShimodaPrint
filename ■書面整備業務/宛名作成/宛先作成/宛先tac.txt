*■**********************************************************
*■* : タックシール出力フォーム 宛先用TAC.kex
*■* : 登録・呼び出し・出力処理に対応（RPX印刷含む）
*■**********************************************************
名札 MAIN

/*▼表定義（フォームに紐づくメインの編集表）*/
var 数値 { &TAC表 = 0 }

/*▼フォーム入力欄と対応する変数郡（登録・呼出で使用）*/
var 数値 { &番号 }
var 文字列 { &使用 , &郵便番号 , &住所1 , &住所2 , &会社名 , &支店 , &営業所 }
var 文字列 { &部署 , &役職 , &名前 , &語尾 , &補足 }

/*▼出力処理用の各種ファイルパス（相対パス指定）*/
var 文字列 { &フォームDPath = #一括パス名 } /*←このkexファイルが存在するパスを取得*/
var 文字列 { &出力TBX = &フォームDPath + "Print宛名用TAC.tbx" } /*←出力先の一時テーブル*/
var 文字列 { &出力RPX = &フォームDPath + "Print宛名用TAC.rpx" } /*←印刷用RPXファイル*/
var 文字列 { &出力TXT = &フォームDPath + "宛名出力.txt" } /*←中間ファイルとして使うテキストファイル*/

/*▼その他制御用変数*/
var 数値 { &CHK , &オフセット = 0 } /*←出力時の先頭空行数*/
var 文字列 { &msgtxt , &タイトル } /*←メッセージ表示とモーダルタイトル用*/

*■**********************************************************
*■* : フォーム開始時の初期処理
*■**********************************************************
手続き定義開始 フォーム::フォーム開始(長整数 &表番号)

    &TAC表 = &表番号 /*←フォームに紐づく編集表を受け取る*/
    手続き実行 変数初期化() /*←変数の初期化を実行*/

手続き定義終了

*■**********************************************************
*■* : 指定行の値を変数に代入（呼び出し処理）
*■**********************************************************
手続き定義開始 呼び出し処理（ 数値 &行番号 ）

    ジャンプ 行番号 = &行番号

    &番号 = [番号]
    &使用 = [使用]
    &郵便番号 = [郵便番号]
    &住所1 = [住所1]
    &住所2 = [住所2]
    &会社名 = [会社名]
    &支店 = [支店]
    &営業所 = [営業所]
    &部署 = [部署]
    &役職 = [役職]
    &名前 = [名前]
    &語尾 = [語尾]
    &補足 = [補足]

    メソッド呼び出し @フォーム.変数変更()

手続き定義終了

*■**********************************************************
*■* : 登録訂正処理（変数 → 行訂正）
*■**********************************************************
手続き定義開始 登録訂正()

    /*▼主要9項目がすべて未定義なら確認*/
    if ( &郵便番号 = #未定義 .and &住所1 = #未定義 .and &住所2 = #未定義 .and &会社名 = #未定義 .and &支店 = #未定義 .and &営業所 = #未定義 .and &部署 = #未定義 .and &役職 = #未定義 .and &名前 = #未定義 )
        &タイトル = "確認"
        メッセージボックス &タイトル , "主要な宛名情報がすべて未入力です。本当に訂正しますか？", アイコン = ? , ボタン指定 = 5 , 制御文字展開 = する , &CHK
        if ( &CHK <> 6 )
            手続き終了
        end
    end

    行訂正 終了状態 = &CHK ,\
        [番号] = &番号 , [使用] = &使用 , [郵便番号] = &郵便番号 ,\
        [住所1] = &住所1 , [住所2] = &住所2 , [会社名] = &会社名 ,\
        [支店] = &支店 , [営業所] = &営業所 , [部署] = &部署 ,\
        [役職] = &役職 , [名前] = &名前 , [語尾] = &語尾 , [補足] = &補足

    確認 "訂正しました。"

手続き定義終了

*■**********************************************************
*■* : 登録追加処理（変数 → 行追加）
*■**********************************************************
手続き定義開始 登録追加()

    /*▼主要9項目がすべて未定義なら確認*/
    if ( &郵便番号 = #未定義 .and &住所1 = #未定義 .and &住所2 = #未定義 .and &会社名 = #未定義 .and &支店 = #未定義 .and &営業所 = #未定義 .and &部署 = #未定義 .and &役職 = #未定義 .and &名前 = #未定義 )
        &タイトル = "確認"
        メッセージボックス &タイトル , "主要な宛名情報がすべて未入力です。本当に追加しますか？", アイコン = ? , ボタン指定 = 5 , 制御文字展開 = する , &CHK
        if ( &CHK <> 6 )
            手続き終了
        end
    end

    行追加 終了状態 = &CHK ,\
        [番号] = &番号 , [使用] = &使用 , [郵便番号] = &郵便番号 ,\
        [住所1] = &住所1 , [住所2] = &住所2 , [会社名] = &会社名 ,\
        [支店] = &支店 , [営業所] = &営業所 , [部署] = &部署 ,\
        [役職] = &役職 , [名前] = &名前 , [語尾] = &語尾 , [補足] = &補足

    確認 "新規追加しました。"

手続き定義終了

*■**********************************************************
*■* : 出力CHK除去（[使用] を未定義に）
*■**********************************************************
手続き定義開始 出力CHK除去()

    置換 終了状態 = &CHK , [使用] = #未定義
    &msgtxt = "使用情報をクリアしました。"
    確認 &msgtxt

手続き定義終了

*■**********************************************************
*■* : 変数初期化処理（全ての変数を未定義に）
*■**********************************************************
手続き定義開始 変数初期化()

    &番号 = #未定義
    &使用 = #未定義
    &郵便番号 = #未定義
    &住所1 = #未定義
    &住所2 = #未定義
    &会社名 = #未定義
    &支店 = #未定義
    &営業所 = #未定義
    &部署 = #未定義
    &役職 = #未定義
    &名前 = #未定義
    &語尾 = #未定義
    &補足 = #未定義

    メソッド呼び出し @フォーム.変数変更()
    &msgtxt = "変数を初期化しました。"
    確認 &msgtxt

手続き定義終了

*■**********************************************************
*■* : プリントアウト処理（RPX出力）
*■**********************************************************
手続き定義開始 プリントアウト実行()

    var 数値 { &出力表ID = 0 , &i = 0 }

    編集表 &TAC表

    絞り込み [使用] { <> #未定義 } , 終了状態 = &CHK

    印字開始 &出力TXT , 終了状態 = &CHK

    /*▼オフセット分 空行を先頭出力*/
    繰り返し ( &i < &オフセット )
        印字 _9 , _9 , _9 , _9 , _9 , _9 , _9 , _9 , _9 , _9 , _9
        &i = &i + 1
    繰り返し終了

    /*▼実データ印字（タブ区切り）*/
    繰り返し ( .NOT #終端行 )
        印字 [郵便番号] , _9 , [住所1] , _9 , [住所2] , _9 , [会社名] , _9 ,\
             [支店] , _9 , [営業所] , _9 , [部署] , _9 , [役職] , _9 ,\
             [名前] , _9 , [語尾] , _9 , [補足]
        ジャンプ 行番号 = 次行
    繰り返し終了

    印字終了
    絞り込み解除

    表 &出力TBX , モード = 専有 , 終了状態 = &CHK
    行削除 * , 終了状態 = &CHK

    読み込み テキスト , &出力TXT , 区切り = "09" , 項目名 = する , 終了状態 = &CHK

    レポート印刷 &出力RPX , 会話 = しない , プレビュー = する , 終了状態 = &CHK

手続き定義終了
