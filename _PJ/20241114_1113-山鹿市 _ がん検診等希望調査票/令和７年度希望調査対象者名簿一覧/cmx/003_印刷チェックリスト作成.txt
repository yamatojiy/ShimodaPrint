*■***********************************************************
*■*** テーブルデータからチェックリスト用のデータを作成します。
*■***********************************************************

/*▼ファイル */
var 文字列{ &datapath = #データパス名 + "..\02_本番データ\" }
var 文字列{ &ALLtbx = &datapath + "令和７年度希望調査対象者名簿一覧.tbx" }
var 数値  { &Allopen , &ALLtbxNum }

var 文字列{ &CHKpath = &datapath + "チェックリスト\" }
var 文字列{ &outReportTxt = &CHKpath + "printCHKlist.txt" }
var 文字列{ &CHKtbx = &CHKpath + "印刷CHKリスト.tbx" }
var 数値  { &CHKopen , &CHKbxNum }


var 数値  { &区分ID = 0 , &PDFNum , &r = 1 , &max , &CHK }

var 文字列{ &Snumcode , &Sname , &Enumcode , &Ename , &↓ = "↓" }
*■*----------------------------------------------------------
*■*-----
*■*----------------------------------------------------------
名札 MAIN

    *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
    *■*:　情報をテキストに書き出す
    手続き実行 表オープン（ &ALLtbx , "専有" , &ALLtbxNum , &Allopen ）
    編集表　&ALLtbxNum

    項目集計 [printJob]
    &max = &最大値　＋ 1

    印字開始 &outReportTxt , 終了状態 = &CHK

        繰り返し（ &r < &max ）

            絞り込み [printJob] { &r } , 終了状態 = &CHK
            
                ジャンプ 行番号 = 1
                    
                    手続き実行 文字列の頭４つ以降をコメにする（ &Sname , [氏名] ）
                    &Snumcode = [numcode]
                    

                ジャンプ 行番号 = 最終

                    &Enumcode = [numcode]
                    手続き実行 文字列の頭４つ以降をコメにする（ &Ename , [氏名] ）

                印字 [printJob] , &↓ , &Snumcode , &↓ , &Sname , &↓ , &Enumcode , &↓ , &Ename 
            解除 1

        &r = &r + 1 
        繰り返し終了 
    印字終了

    *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
    *■*:　読み込み
    手続き実行 表オープン（ &CHKtbx , "専有" , &CHKbxNum , &CHKopen ）
    編集表 &CHKbxNum

    行削除 * , 圧縮
    読み込み テキスト , &outReportTxt  , 区切り = &↓


    *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
    *■*:　終了処理
    条件 ( &CHKopen = 0 ) 終了 表 &CHKbxNum
    条件 ( &Allopen = 0 ) 終了 表 &ALLtbxNum

    /*▼完了通知 */
    var 文字列{ &msgtxt }
    &msgtxt = "Done"
    確認 &msgtxt

終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　テーブルID調査
*■*:　ターゲットのテーブルが開いてても開いて無くても表番号を返す関数
手続き定義開始 文字列の頭４つ以降をコメにする（ 参照　文字列 &rename , 文字列　&name ）

    var 文字列{ &加工文字列 , &加工文字列2　}

    &加工文字列 = #文字列反転( &name )
    &加工文字列2 = #右側文字列( &加工文字列 , 5 )
    &rename = #文字列反転( &加工文字列2 )　+"***"

手続き定義終了

*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　テーブルID調査
*■*:　ターゲットのテーブルが開いてても開いて無くても表番号を返す関数
*■*:　引数１：ターゲットのテーブルアドレス
*■*:　引数２：新規で開くステータス（専有とか参照とか、すでに開いている場合はそのまま）
*■*:　戻り値：表のＩＤ
*■*:　戻り値：既にオープンされている場合は１、新規でオープンなら０
手続き定義開始 表オープン（ 文字列 &tbx ,文字列　&モード　, 参照　数値　&表ID , 参照　数値　&openF ）

        *■*-------------------------------------
        *■*--  編集表番号取得（開いてても問題無し
        *■*-------------------------------------
        &表ID = #表番号取得( &tbx )
        if ( .not &表ID ) /*←未定義なら */
            
            表 &tbx , モード = &モード , 終了状態 = &CHK 
            &表ID = #表番号取得( &tbx )　　

        &openF = 0
    else
        &openF = 1
    end

手続き定義終了