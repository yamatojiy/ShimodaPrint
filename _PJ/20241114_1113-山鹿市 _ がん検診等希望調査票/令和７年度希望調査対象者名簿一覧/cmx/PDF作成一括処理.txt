*■***********************************************************
*■*** IDを振り分ける
*■***********************************************************

*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　IDリストを開いて対応する

var 文字列{ &datapath = #データパス名 + "..\02_本番データ\" }
var 文字列{ &ALLtbx = &datapath + "令和７年度希望調査対象者名簿一覧.tbx" }

var 数値  { &Allopen , &ALLtbxNum }

var 数値  { &IDnum , &CHK , &作業count }
var 文字列{ &StringNum }

var 文字列{ &outReportTxt = #データパス名 + "outReport.txt" }

var 数値  { &マイルストーンID }
var 数値  { &JOBID }

/*▼PDFに関する置き場情報 */
var 文字列{ &PDFファイル名 }
var 文字列{ &PDFアドレス = &datapath +"PDF原稿出力\" } 
var 文字列{ &rpx = &datapath + "【本番】レポートデータ_v1（背景あり）.rpx" }


/*▼PDF管理番号用変数 */
var 数値  { &boxNum  }
var 数値  { &区分ID = 0 , &PDFNum , &r = 1 , &max = 338 }
var 数値  { &切り分け値 = 0 , &残り値 = 0 }


*■*----------------------------------------------------------
*■*----- JOBIDぶんPDF作成のプロンプトを回す
*■*----- IDを付けるさいにパックIDなどを用いて
*■*----------------------------------------------------------
名札 MAIN

    手続き実行 表オープン（ &ALLtbx , "専有" , &ALLtbxNum , &Allopen ）

    項目集計 [box]
    &max = &最大値　＋ 1

    /*▼下準備 */
    &作業count = 1
    &区分ID = [区分ID]
    &PDFNum = 1

    ジャンプ 行番号 = 1
    繰り返し（ &r < &max ）

        絞り込み [printJob] { &r } , 終了状態 = &CHK

        /*▼0件だったら終了 */
        条件 ( #総件数 = 0 ) 終了

            /*▼総件数が450件以上なら処理する */
            if ( #総件数 = 900 )

                &区分ID = [区分ID]
            
                &PDFファイル名 = &PDFアドレス +  #文字列( &区分ID , 2 )  +"_yamaga" +"-"+ #文字列( &PDFNum , 3 ) +".pdf"

                /*▼印字 */
                レポート印刷 &rpx , 文書ファイル = &PDFファイル名 , 終了状態 = &CHK

            /*▼違うという事は450件満たないという事 */
            else

                &区分ID = [区分ID]
                &切り分け値 = #切り上げ( #総件数 / 2  , 0 )
                &残り値    = #総件数 - &切り分け値

                *■*::::::::::::::::::::::::::::::::
                *■*:　１
                *■*:　まずは切り分け前の数値を作る
                絞り込み 行数 = &切り分け値

                    &PDFファイル名 = &PDFアドレス +  #文字列( &区分ID , 2 )  +"_yamaga" +"-"+ #文字列( &PDFNum , 3 ) +"_01.pdf"
                    /*▼印字 */
                    レポート印刷 &rpx , 文書ファイル = &PDFファイル名 , 終了状態 = &CHK

                解除 1                
                *■*::::::::::::::::::::::::::::::::
                *■*:　２
                *■*:　残りの分を作成
                &切り分け値 = &切り分け値 + 1
                ジャンプ 行番号 = &切り分け値
                絞り込み 行数 = &残り値

                    &PDFファイル名 = &PDFアドレス +  #文字列( &区分ID , 2 )  +"_yamaga" +"-"+ #文字列( &PDFNum , 3 ) +"_02.pdf"
                    /*▼印字 */
                    レポート印刷 &rpx , 文書ファイル = &PDFファイル名 , 終了状態 = &CHK

                解除 1

                /*▼次回用に初期化 */
                &切り分け値 = 0 , &残り値 = 0

           end

        解除 1
        *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
        *■*:　更新処理
        &PDFNum = &PDFNum + 1

        &r = &r + 1 

    繰り返し終了 

    条件 ( &Allopen = 0 ) 終了 表 &ALLtbxNum

    /*▼完了通知 */
    var 文字列{ &msgtxt }
    &msgtxt = "Done"
    確認 &msgtxt

終了

*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　テーブルID調査
*■*:　ターゲットのテーブルが開いてても開いて無くても表番号を返す関数
*■*:　引数１：ターゲットのテーブルアドレス
*■*:　引数２：新規で開くステータス（専有とか参照とか、すでに開いている場合はそのまま）
*■*:　戻り値：表のＩＤ
*■*:　戻り値：既にオープンされている場合は１、新規でオープンなら０
手続き定義開始 表オープン（ 文字列 &tbx ,文字列　&モード　, 参照　数値　&表ID , 参照　数値　&openF ）

        *■*-------------------------------------
        *■*--  編集表番号取得（開いてても問題無し
        *■*-------------------------------------
        &表ID = #表番号取得( &tbx )
        if ( .not &表ID ) /*←未定義なら */
            
            表 &tbx , モード = &モード , 終了状態 = &CHK 
            &表ID = #表番号取得( &tbx )　　

        &openF = 0
    else
        &openF = 1
    end

手続き定義終了