*■***********************************************************
*■*** タックシール作製TBX
*■*** 
*■*** 
*■***********************************************************

/*▼ファイル */
var 文字列{ &datapath = #データパス名 + "..\02_本番データ\" }
var 文字列{ &ALLtbx = &datapath + "令和７年度希望調査対象者名簿一覧.tbx" }
var 数値  { &Allopen , &ALLtbxNum }

var 文字列{ &CHKpath = &datapath + "タックシール\" }
var 文字列{ &outReportTxt = &CHKpath + "Taclist.txt" }
var 文字列{ &CHKtbx = &CHKpath + "タックシール.tbx" }
var 数値  { &CHKopen , &CHKbxNum }


var 数値  { &区分ID = 0 , &PDFNum , &r = 1 ,&r2 = 1 , &max ,&Pmax , &CHK }
var 数値  { &box , &pack , &TACcount , &outCount }
var 数値  { &sID , &eID }
var 文字列{ &Snumcode , &Sname , &Enumcode , &Ename , &↓ = "↓" }

var 文字列{ &OBJct = "&区分ID  , &↓ , &box , &↓ ," }
var 文字列{ &out1 = "&Snumcode , &↓ ," }
var 文字列{ &out2 = "&Enumcode , &↓ , &Port , &↓ , &TACcount , &↓ , &outCount " }
var 文字列{ &txtmaster }

var 文字列{ &Port = "" }
var 文字列{ &郵便局[4] = {""} }
    &郵便局[1] = "鹿本郵便局分　〒861-03"
    &郵便局[2] = "菊鹿郵便局分　〒861-04"
    &郵便局[3] = "山鹿郵便局分　〒861-05"
    &郵便局[4] = "鹿北郵便局分　〒861-06"

*■*---------------------------------------------------------- 
*■*-----
*■*----------------------------------------------------------
名札 MAIN

    *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
    *■*:　情報をテキストに書き出す
    手続き実行 表オープン（ &ALLtbx , "専有" , &ALLtbxNum , &Allopen ）
    編集表　&ALLtbxNum

    項目集計 [box]
    &max = &最大値　＋ 1

    印字開始 &outReportTxt , 終了状態 = &CHK

        繰り返し（ &r < &max ）

            絞り込み [box] { &r } , 終了状態 = &CHK

                &TACcount = #総件数

                *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
                *■*:　ここからデータ集計
                ジャンプ 行番号 = 1
            
                &Snumcode = [numcode]
                &区分ID = [区分ID]
                &box = [box]

                絞り込み [除去] { <>#未定義 } , 終了状態 = &CHK
                    &outCount = #総件数
                解除 1

                ジャンプ 行番号 = 最終
                &Enumcode = [numcode]

                ケース開始
                    ケース（ &区分ID = 1 ）
                        &Port = &郵便局[1]
                    
                    ケース（ &区分ID = 2 ）
                        &Port = &郵便局[2]
                
                    ケース（ &区分ID = 3 ）
                        &Port = &郵便局[3]

                    ケース（ &区分ID = 4 ）
                        &Port = &郵便局[4]
                    
                    ケース　その他
                        &Port = "error"

                ケース終了
                *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
                *■*:　ここから印字
                &txtmaster = "印字 "+ &OBJct + &out1 + &out2
                コマンド　&txtmaster

        
            解除 1

        &r = &r + 1
        繰り返し終了 
    印字終了

    *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
    *■*:　読み込み
    手続き実行 表オープン（ &CHKtbx , "専有" , &CHKbxNum , &CHKopen ）
    編集表 &CHKbxNum

    行削除 * , 圧縮
    読み込み テキスト , &outReportTxt  , 区切り = &↓


    *■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
    *■*:　終了処理
    条件 ( &CHKopen = 0 ) 終了 表 &CHKbxNum
    条件 ( &Allopen = 0 ) 終了 表 &ALLtbxNum

    /*▼完了通知 */
    var 文字列{ &msgtxt }
    &msgtxt = "Done"
    確認 &msgtxt

終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　テーブルID調査
*■*:　ターゲットのテーブルが開いてても開いて無くても表番号を返す関数
手続き定義開始 文字列の頭４つ以降をコメにする（ 参照　文字列 &rename , 文字列　&name ）

    var 文字列{ &加工文字列 , &加工文字列2　}

    &加工文字列 = #文字列反転( &name )
    &加工文字列2 = #右側文字列( &加工文字列 , 5 )
    &rename = #文字列反転( &加工文字列2 )　+"***"

手続き定義終了

*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　テーブルID調査
*■*:　ターゲットのテーブルが開いてても開いて無くても表番号を返す関数
*■*:　引数１：ターゲットのテーブルアドレス
*■*:　引数２：新規で開くステータス（専有とか参照とか、すでに開いている場合はそのまま）
*■*:　戻り値：表のＩＤ
*■*:　戻り値：既にオープンされている場合は１、新規でオープンなら０
手続き定義開始 表オープン（ 文字列 &tbx ,文字列　&モード　, 参照　数値　&表ID , 参照　数値　&openF ）

        *■*-------------------------------------
        *■*--  編集表番号取得（開いてても問題無し
        *■*-------------------------------------
        &表ID = #表番号取得( &tbx )
        if ( .not &表ID ) /*←未定義なら */
            
            表 &tbx , モード = &モード , 終了状態 = &CHK 
            &表ID = #表番号取得( &tbx )　　

        &openF = 0
    else
        &openF = 1
    end

手続き定義終了