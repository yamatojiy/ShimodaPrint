*■----------------------------------------------------------
*■--- 汎用変数（共通）
*■----------------------------------------------------------
var 数値  { &CHK }        /* メッセージ判定用 */
var 文字列{ &msgtxt }     /* 表示メッセージ */

*■----------------------------------------------------------
*■--- DTPデータ変数（フォームのパレット変数）
*■----------------------------------------------------------
var 数値  { &伝票サーチ = "" }

/*▼パレット変数 */
var 数値  { &伝票ID , &確定部数 , &確定頁数 , &前回伝票 }
var 日時  { &受注日 , &確定仕上日 , &前回仕上日 , &更新日 }

var 文字列{ &分類 , &担当 , &得意先 , &品名 , &サイズ }
var 文字列{ &出力 , &組版担当 , &保管場所 }
var 文字列{ &備考 , &保存期間 , &一時保存 }

/*▼表ID */
var 数値  { &DTP表番号 }

/*▼Sub_Master */
var 文字列{ &subTbx = #一括パス名 + "submaster.tbx" }
var 数値  { &subTbxNum }

/*▼保管場所リスト */
var 文字列{ &SaveTbx = #一括パス名 + "保管場所.tbx" }
var 数値  { &SaveTbxNum }

/*▼担当リスト */
var 文字列{ &UserTbx = #一括パス名 + "03_01_シモダ社員情報.tbx" }
var 数値  { &UserTbxNum }

/*▼保管期間 */
var 文字列{ &StorageTimeTbx = #一括パス名 + "010_04_保存期間リスト.tbx" }
var 数値  { &StorageTimeTbxNum }

/*▼出力 */
var 文字列{ &OutputTbx = #一括パス名 + "出力.tbx" }
var 数値  { &OutputTbxNum }

*■----------------------------------------------------------
*■--- フォーム初期化
*■----------------------------------------------------------
手続き定義開始 フォーム::フォーム開始( 長整数 &表番号 )
    &DTP表番号 = &表番号
    手続き実行 DTP変数リセット()
手続き定義終了

*■----------------------------------------------------------
*■--- 変数リセット
*■----------------------------------------------------------
手続き定義開始 DTP変数リセット()

    &伝票ID = "" &受注日 = "" &分類 = "" &担当 = "" &得意先 = ""
    &品名 = "" &サイズ = "" &確定頁数 = "" &確定部数 = ""
    &確定仕上日 = "" &前回伝票 = "" &前回仕上日 = ""
    &出力 = "" &組版担当 = "" &保管場所 = "" &備考 = ""
    &保存期間 = "" &一時保存 = "" &更新日 = ""

    メソッド呼び出し @フォーム.変数変更()

手続き定義終了
*■----------------------------------------------------------
*■--- 行追加処理
*■----------------------------------------------------------
手続き定義開始 行追加処理()

    &msgtxt = "伝票ID：" + #文字列(&伝票ID) + "\n品名：" + &品名 + "\n上記で追加しますか？"
    メッセージボックス "DTP作業追加", &msgtxt , アイコン = ? , ボタン指定 = 5 , 制御文字展開 = する , &CHK

    if ( &CHK = 6 )
        &更新日 = #日時日付( #日時値 )
        行追加 終了状態 = &CHK ,\
        [伝票ID]=&伝票ID , [受注日]=&受注日 , [分類]=&分類 , [担当]=&担当 , [得意先]=&得意先 ,\
        [品名]=&品名 , [サイズ]=&サイズ , [確定頁数]=&確定頁数 , [確定部数]=&確定部数 ,\
        [確定仕上日]=&確定仕上日 , [前回伝票]=&前回伝票 , [前回仕上日]=&前回仕上日 ,\
        [出力]=&出力 , [組版担当]=&組版担当 , [保管場所]=&保管場所 ,\
        [備考]=&備考 , [保存期間]=&保存期間 , [一時保存]=&一時保存 , [更新日]=&更新日

        確認 "DTPデータを追加しました"

    end

手続き定義終了
*■----------------------------------------------------------
*■--- 伝票サーチ：submasterの[伝票ＮＯ]からパレット変数へ代入（追加仕様対応）
*■----------------------------------------------------------
手続き定義開始 伝票引用()

    /*▼subTbx 開閉管理 */
    &subTbxNum = #表番号取得( &subTbx )
    var 数値 { &wasOpenSub = &subTbxNum }

    if ( .not &subTbxNum )
        表 &subTbx , モード = 共有参照 , 終了状態 = &CHK
        &subTbxNum = #表番号取得( &subTbx )
    end

    編集表 &subTbxNum

    /*▼検索：伝票ＮＯ */
    検索 [伝票ＮＯ] { &伝票サーチ } , 終了状態 = &CHK

    if ( &CHK = 1 )

        &伝票ID = [伝票ＮＯ]
        &受注日 = [日付]

        /*▼分類変換（数値→文字列）*/
        条件 ( [分類] = 1 )  &分類 = "枚葉"
        条件 ( [分類] = 2 )  &分類 = "冊子"
        条件 ( [分類] = 3 )  &分類 = "新聞"
        条件 ( [分類] = 4 )  &分類 = "複写"

        &担当 = [担当]
        &得意先 = [得意先]
        &品名 = [品名]
        &サイズ = [サイズ]

        /*▼確定頁数：なければ頁数 */
        if ( [確定頁数] <> "" )
            &確定頁数 = [確定頁数]
        else
            &確定頁数 = [頁数]
        end

        /*▼確定部数：なければ部数 */
        if ( [確定部数] <> #未定義 )
            &確定部数 = [確定部数]
        else
            &確定部数 = [部数]
        end

        &確定仕上日 = [確定仕上日]
        &前回伝票 = [前回伝票]

        /*▼前回伝票IDで再検索して仕上日取得 */
        if ( &前回伝票 <> 0 )

            var 数値 { &tmpChk = 0 }
            検索 [伝票ＮＯ] { &前回伝票 } , 終了状態 = &tmpChk

            if ( &tmpChk = 1 )
                &前回仕上日 = [確定仕上日]
            end

            /*▼元の行に戻らなくて良ければこのままでOK */

        end

    end

    条件( &wasOpenSub = 0 ) 終了 表 &subTbxNum

    メソッド呼び出し @フォーム.変数変更()

手続き定義終了
