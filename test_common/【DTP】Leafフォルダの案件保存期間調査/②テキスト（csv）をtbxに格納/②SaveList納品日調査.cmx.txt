*■***********************************************************
*■*** ②SaveList納品日調査.cmx
*■***
*■***　SaveList.tbx　を起動して　納品日をSmasta.tbxから取得する
*■***
*■***********************************************************

var 数値  { &i , &CHK }
*var 数値  { &表ID }
var 日時  { &納品日 }
var 文字列{ &smastaKey = "fumio" }

/*▼表引き情報 */
var 文字列{ &SaveListTbx = #一括パス名 + "SaveList.tbx" }
var 文字列{ &smataTbx = #一括パス名 + "..\..\..\作業伝票\Smasta.tbx" }


*■*----------------------------------------------------------
*■*----- 動作開始
*■*----------------------------------------------------------
名札 MAIN

    /*▼表をアクティブにしておく */
    表 &smataTbx  , 表番号 = 21 , モード = 共有更新
    表 &SaveListTbx , 表番号 = 15 , モード = 共有更新
    
    /*▼Smasta用の利用者コードを宣言 */
    利用者コード &smastaKey
        
    編集表　15
    繰り返し（ .NOT #終端行 ）

        手続き実行 Smasta納品日調査（[伝票№] , &納品日　）
        行訂正 終了状態 = &CHK , [納品日] = &納品日
    
        ジャンプ 行番号 = 次行 
    繰り返し終了 

    終了 表 15
    終了 表 21

    確認 "Done"

終了
*■*::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
*■*:　手続き定義　Smasta納品日調査
*■*:
手続き定義開始 Smasta納品日調査（ 数値 &ID , 参照 日時　&納品日　）

    
    編集表 21

        検索 [伝票ＮＯ] { &ID } , 終了状態 = &CHK 

        if (&CHK = 1)

            &納品日 = [仕上日]

        else
        *■*:::::::::::::::::::::::::::::
        *■*:　hitしなかったら空欄で埋める

            &納品日 = ""

        end
        解除 1
    
    編集表 15

手続き定義終了